#!/usr/bin/perl -wT
#
# Copyright (c) 2000-2002, 2004, 2007 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;

# Save the tiplogs from an experiment

sub usage {
	print "Usage: $0 pid eid\n";
	exit(-1);
}

#
# Configure variables
#
my $TB          = "/home/achauhan06/CLOUDLAB/SSHCLOUD/CloudLabonESI/emulab-devel/newbuild";
my $CONTROL	= "ops.cloudlab.umass.edu";
my $sshtb	= "$TB/bin/sshtb";

#
# Testbed Support libraries
#
use lib "/home/achauhan06/CLOUDLAB/SSHCLOUD/CloudLabonESI/emulab-devel/newbuild/lib";
use libdb;
use libtestbed;
use Experiment;
use User;

# Locals
my $SAVEUID = $UID;

#
# Turn off line buffering on output
#
$| = 1;

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("*** $0:\n".
        "    Must be root! Maybe its a development version?\n");
}

#
# Please run as yourself
# 
if ($UID == 0) {
    die("*** $0:\n".
        "    Please do not run this script as root!\n");
}

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:/usr/sbin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

if (@ARGV != 2) {
    usage();
}

my ($pid,$eid) = @ARGV;

#
# Untaint args.
#
if ($pid =~ /^([-\w]+)$/) {
    $pid = $1;
}
else {
    die("*** Bad data in pid: $pid.\n");
}
if ($eid =~ /^([-\w]+)$/) {
    $eid = $1;
}
else {
    die("*** Bad data in eid: $eid.\n");
}
my $experiment = Experiment->Lookup($pid, $eid);
if (!defined($experiment)) {
    die("*** $0:\n".
	"    Could not lookup experiment object!\n");
}

#
# Verify user and get his DB uid and other info for later.
#
my $this_user = User->ThisUser();
if (! defined($this_user)) {
    die("*** $0:\n".
	"    You do not exist in the Emulab Database!\n");
}
my $user_uid   = $this_user->uid();

#
# Verify permission to muck with this experiment. This is to head off
# permission problems early; the nodes are indvidually checked later
# in the library.
#
if (!$this_user->IsAdmin() &&
    !$experiment->AccessCheck($this_user, TB_EXPT_MODIFY)) {
    die("*** $0:\n".
	"    You do not have permission to save log files in $pid/$eid!\n");
}

#
# Find the names of all physical nodes with tip lines. 
#
my $query_result =
    DBQueryFatal("select r.node_id,r.vname,t.server from reserved as r ".
		 "left join nodes as n on n.node_id=r.node_id ".
		 "left join node_types as nt on nt.type=n.type ".
		 "left join tiplines as t on t.node_id=r.node_id ".
		 "where r.pid='$pid' and r.eid='$eid' and ".
		 "      nt.isvirtnode=0 and nt.isremotenode=0 and ".
		 "      t.server is not null");

if ($query_result->numrows == 0) {
    print "No console lines found for experiment $eid in project $pid!\n";
    exit(0);
}

my $savedir = PROJROOT() . "/$pid/tiplogs/$eid";
my $cmdargs = "$TB/sbin/savelogs.proxy -u $user_uid -s $savedir ";

while (my ($nodeid, $vname) = $query_result->fetchrow_array()) {
    $cmdargs .= " $nodeid $vname";
}

#
# Now we call over to the ops node to deal with the rest of this. 
#
$EUID = $UID = 0;
system("$sshtb -host $CONTROL $cmdargs");
$EUID = $UID = $SAVEUID;

#
# Nothing else to do ... output from proxy went to stdout/stderr.
# 
exit($? >> 8);

