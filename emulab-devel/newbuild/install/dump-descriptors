#!/usr/bin/perl -w
#
# Copyright (c) 2007-2012 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Long;
use strict;

#
# Dump out an SQL file of statements to initialize the images and osids
# on a new testbed. Or maybe even an old testbed? Note that the output
# of thie script is the input to load-descriptors in this directory.
#
sub usage()
{
    print STDERR "Usage: dump-descriptors [-v vers] [-m] filename\n";
    print STDERR "       dump-descriptors -i imageid [-i imageid] filename\n";
    print STDERR "  -m      dump just the MFS descriptors.\n";
    print STDERR "  -v 1    first generation STD images (FBSD410, RHL90)\n";
    print STDERR "  -v 2    second generation STD images (FBSD62, FC6)\n";
    print STDERR "  -v 3    third generation STD images (FBSD82, FEDORA15)\n";
    print STDERR "  -v 4    fourth generation STD images (FBSD83, FEDORA15)\n";
    print STDERR "Default version is 4.\n";
    exit(-1);
}
my $version = 4;
my $mfsonly = 0;
my $images;

#
# These are the images/osids we dump out. We of course get the embedded osids.
# We should probably add an export flag to the tables so we can avoid this
# hardwired list. Note that all of these are assumed to live in the emulab-ops
# project.
#
my %imagenames    = (
    "1" => ['FBSD410+RHL90-STD', 'FBSD410-STD', 'RHL90-STD', 'FBSD410-IPFW2'],
    # XXX note that the FBSD410* images may not work on all HW but are
    # included here to resolve nextosid entries for FW-IPFW and FBSD-JAIL
    "2" => ['FBSD62+FC6-STD', 'FBSD62-STD', 'FC6-STD', 'FBSD410-STD',
	    'FBSD410-IPFW2', 'UBUNTU10-STD-FW'],
    # No longer using combined images.
    # The UBUNTU10 image is for FW-IPTABLES link. 
    "3" => ['FBSD82-STD', 'FEDORA15-STD', 'UBUNTU10-STD-FW'],
    "4" => ['FBSD83-STD', 'FBSD83-64-STD', 'FEDORA15-STD', 'FEDORA15-64-STD', 'UBUNTU10-STD-FW'],
);

my %osnames	  = (
    "1" => ['FREEBSD-MFS', 'FRISBEE-MFS', 'NEWNODE-MFS',
	    'OPSNODE-BSD', 'FW-IPFW', 'FW-IPFW2', 'FW-IPTABLES',
	    'RHL-STD', 'FBSD-STD', 'FBSD-JAIL', 'POWER-CONTROLLER'],
    "2" => ['FREEBSD-MFS', 'FRISBEE-MFS', 'NEWNODE-MFS',
	    'OPSNODE-BSD', 'FW-IPFW', 'FW-IPFW2', 'FW-IPTABLES',
	    'RHL-STD', 'FBSD-STD', 'FBSD-JAIL', 'POWER-CONTROLLER'],
    "3" => ['FREEBSD-MFS', 'FRISBEE-MFS', 'NEWNODE-MFS', 'FW-IPTABLES',
	    'OPSNODE-BSD', 'RHL-STD', 'FBSD-STD', 'POWER-CONTROLLER'],
    "4" => ['FREEBSD-MFS', 'FRISBEE-MFS', 'NEWNODE-MFS',
	    'FREEBSD-64-MFS', 'FRISBEE-64-MFS', 'NEWNODE-64-MFS',
	    'ADMIN-LINUX', 'FRISBEE-LINUX','NEWNODE-LINUX',
	    'FW-IPTABLES',
	    'OPSNODE-BSD', 'RHL-STD', 'FBSD-STD', 'POWER-CONTROLLER'],
);

my @osids	   = ();

#
# Need to map these dynamically since we do not want to hardwire the actual
# nextosid IDs (numbers) into this table.
#
my %mappings       = (
    "1" => {
	"RHL-STD"  => "RHL90-STD",
	"FBSD-STD" => "FBSD410-STD",
    },
    "2" => {
	"RHL-STD"  => "FC6-STD",
	"FBSD-STD" => "FBSD62-STD",
    },
    "3" => {
	"RHL-STD"  => "FEDORA10-STD",
	"FBSD-STD" => "FBSD82-STD",
    },
    "4" => {
	"RHL-STD"  => "FEDORA15-STD",
	"FBSD-STD" => "FBSD83-STD",
    }
);

#
# Configure variables
#
my $TB		= "/home/achauhan06/CLOUDLAB/SSHCLOUD/CloudLabonESI/emulab-devel/newbuild";
my $TBOPS       = "testbed-ops\@ops.cloudlab.umass.edu";
my $TBLOGS      = "testbed-logs\@ops.cloudlab.umass.edu";
my $BOSSNODE	= "boss.cloudlab.umass.edu";
my $DUMPDESC    = "$TB/sbin/dumpdescriptor";

#
# Testbed Support libraries
#
use lib "/home/achauhan06/CLOUDLAB/SSHCLOUD/CloudLabonESI/emulab-devel/newbuild/lib";
use libdb;
use libtestbed;
use Image;
use OSinfo;

# Locals
my %tempkeys  = ();
my $protouser = "elabman";
my $protoproj = TBOPSPID();
sub fatal($);

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Parse command arguments. Once we return from getopts, all that should
# left are the required arguments.
#

my $optlist = "v:m";

if (! GetOptions("m"    => \$mfsonly,
		 "v=i"  => \$version,
		 "i=s@" => \$images)) {
    usage();
}
if (!exists($imagenames{$version})) {
    usage();
}
if ($mfsonly) {
    %mappings   = ();
    %imagenames = ();
}
elsif (defined($images)) {
    $version    = 0;
    $mfsonly    = 0;
    %mappings   = ();
    %osnames    = ("$version" => []);
    %imagenames = ("$version" => $images);
}
usage()
    if (@ARGV != 1);
my $filename = $ARGV[0];

my %images  = ();
my %osinfos = ();

#
# Gather up the images and any osinfos they reference.
#
foreach my $imagename (@{$imagenames{$version}}) {
    my $image = Image->Lookup("$protoproj,$imagename");
    if (!defined($image)) {
	fatal("Cannot find image descriptor for $imagename");
    }
    $images{$image->imageid()} = $image;

    #
    # Get the osids it references.
    #
    foreach my $key (('part1_osid', 'part2_osid',
		      'part3_osid', 'part4_osid', 'default_osid')) {
	my $osid = $image->$key();
	next
	    if (!defined($osid));

	my $osinfo = OSinfo->Lookup($osid);
	if (!defined($osinfo)) {
	    fatal("Cannot find OS descriptor for $osid");
	}
	$osinfos{$osinfo->osid()} = $osinfo;
    }
}
#
# And the OSs
#
foreach my $osname (@{$osnames{$version}}) {
    my $osinfo = OSinfo->Lookup("$protoproj,$osname");
    if (!defined($osinfo)) {
	fatal("Cannot find os descriptor for $osname");
    }
    $osinfos{$osinfo->osid()} = $osinfo;
}
foreach my $osname (keys(%{$mappings{$version}})) {
    my $nextosname = $mappings{$version}{$osname};

    my $osinfo = OSinfo->Lookup("$protoproj,$nextosname");
    if (!defined($osinfo)) {
	fatal("Cannot find os descriptor for $nextosname");
    }
    $osinfos{$osinfo->osid()} = $osinfo;
}
#
# Open tempfile.
#
open(OUTPUT, ">$filename") or
    die("Could not open $filename for writing!\n");
print OUTPUT "<descriptors>\n";
foreach my $osname (keys(%{$mappings{$version}})) {
    my $nextosname = $mappings{$version}{$osname};

    print OUTPUT "<mapping name=\"$osname\">\n";
    print OUTPUT "    <value>$nextosname</value>\n";
    print OUTPUT "</mapping>\n";
}
close(OUTPUT);

#
# Hmm, this is interesting. We reuse OSids that were created via the EZ
# path, typically in the combo images. We need to delete those from the
# osinfos list since they get created when processing the EZ images in
# the load-descriptors script. Ordering is going to be important when
# loading them in.
#
foreach my $osid (keys(%osinfos)) {
    my $osinfo = $osinfos{$osid};

    next
	if ($osinfo->ezid());
    next
	if ($mfsonly && !$osinfo->mfs());

    system("$DUMPDESC -o $osid >> $filename") == 0
	or fatal("Could not dump $osinfo");
}
foreach my $imageid (keys(%images)) {
    my $image = $images{$imageid};
    
    system("$DUMPDESC -i $imageid >> $filename") == 0
	or fatal("Could not dump $image");
}
open(OUTPUT, ">>$filename") or
    die("Could not open $filename for writing!\n");
print OUTPUT "</descriptors>\n";
close(OUTPUT);
exit(0);

sub fatal($)
{
    my ($mesg) = @_;

    print STDERR "*** $0:\n".
	         "    $mesg\n";
    exit(-1);
}

