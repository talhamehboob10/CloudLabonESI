<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<!--

VirtualNode for use on the slice canvas

-->

<s:HGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:components="com.flack.shared.display.components.*"
		  initialize="ensureInside();"
		  gap="4"
		  paddingLeft="0"
		  paddingTop="0"
		  paddingRight="0"
		  paddingBottom="0">
	<fx:Script>
		<![CDATA[
			import com.flack.emulab.resources.virtual.VirtualNode;
			import com.flack.emulab.resources.virtual.VirtualNodeCollection;
			import com.flack.shared.utils.ColorUtil;
			import com.flack.shared.utils.ImageUtil;
			
			import flash.filters.DropShadowFilter;
			import flash.filters.GlowFilter;
			
			public function setFilters(newFilters:Array):void
			{
				nodeContainer.filters = newFilters;
			}
			
			[Bindable]
			public var editable:Boolean = true;
			
			[Bindable]
			private var node:VirtualNode;
			public function set Node(newNode:VirtualNode):void
			{
				node = newNode;
				labelBackgroundColor = ColorUtil.colorsDark[node.experiment.manager.colorIdx];
				labelColor = ColorUtil.colorsLight[node.experiment.manager.colorIdx];
				setLocation(node.x,node.y);
				if(node.os.length > 0)
				{
					osidLabel.text = node.os;
					osidLabel.visible = osidLabel.includeInLayout = true;
				}
				else
					osidLabel.visible = osidLabel.includeInLayout = false;
			}
			public function get Node():VirtualNode
			{
				return node;
			}
			
			public var canvas:ExperimentCanvas;
			
			[Bindable]
			private var labelColor:uint;
			[Bindable]
			private var labelBackgroundColor:uint;
			
			/*public function adjustWidth():void
			{
				validateNow();
				if(nameLabel != null)
				{
					//width = width + (nameLabel.measuredWidth - nameLabel.width);// + 5;
					validateNow();
					if(node != null)
						ensureInside();
				}
			}*/
			
			public function ensureInside(point:Point = null):void
			{
				var newX:Number = point == null ? x : point.x;
				var newY:Number = point == null ? y : point.y;
				// right edge
				if(parentDocument.width > 0 && (x + width) > parentDocument.width)
					newX = parentDocument.width - width;
				// bottom edge
				if(parentDocument.height > 0 && (y + height) > parentDocument.height)
					newY = parentDocument.height - height;
				// left edge
				if(x < 0)
					newX = 0;
				// top edge
				if(y < 0)
					newY = 0;
				move(newX, newY);
			}
			
			override public function move(x:Number, y:Number):void
			{
				if(node != null)
				{
					// Don't consider moving a change
					//if(x != node.flackInfo.x || y != node.flackInfo.y)
					//	node.unsubmittedChanges = true;
					node.x = x;
					node.y = y;
				}
				super.move(x, y);
			}
			
			public function showInfo(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				//DisplayUtil.viewVirtualNode(node);
				canvas.host.viewNode(node);
			}
			
			public function remove(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				canvas.removeCanvasNode(this);
			}
			
			public function removeFromCanvas():void
			{
				canvas.removeElement(this);
			}
			
			public function setLocation(newX:Number = -1, newY:Number = -1):void
			{
				var finalPoint:Point = new Point(newX, newY);
				if(newX == -1)
					finalPoint.x = Math.round(Math.random() * (parentDocument.width-60));
				if(newY == -1)
					finalPoint.y = Math.round(Math.random() * (parentDocument.height-28));
				
				// Make sure this is within bounds
				ensureInside(finalPoint);
			}
			
			public function get MiddlePoint():Point
			{
				return new Point(MiddleX, MiddleY);
			}
			
			public function get MiddleX():Number
			{
				return x + nodeContainer.width/2;
			}
			
			public function get MiddleY():Number
			{
				return y + height/2;
			}
			
			public function get ContainerWidth():Number
			{
				return nodeContainer.width;
			}
			
			public function get ContainerHeight():Number
			{
				return nodeContainer.height;
			}
			
			protected function bordercontainer1_mouseDownHandler(event:MouseEvent):void
			{
				canvas.draggingItem = this;
				canvas.setElementIndex(this, canvas.numElements-1);
			}
			
			protected function bordercontainer1_rollOverHandler(event:MouseEvent):void
			{
				if(canvas.SelectedNodes == null || !canvas.SelectedNodes.contains(node))
					nodeContainer.filters = [new GlowFilter(0x000000)];
				canvas.hoveredOverItem = this;
			}
			
			protected function bordercontainer1_rollOutHandler(event:MouseEvent):void
			{
				if(canvas.SelectedNodes == null || !canvas.SelectedNodes.contains(node))
					nodeContainer.filters = [];
				canvas.hoveredOverItem = null;
			}
			
			protected function bordercontainer1_clickHandler(event:MouseEvent):void
			{
				var newSelectedNodes:VirtualNodeCollection = new VirtualNodeCollection();
				newSelectedNodes.add(node);
				canvas.SelectedNodes = newSelectedNodes;
			}
			
		]]>
	</fx:Script>
	
	<s:BorderContainer id="nodeContainer"
					   buttonMode="true"
					   useHandCursor="true"
					   height="29"
					   borderStyle="solid"
					   backgroundColor="{labelBackgroundColor}"
					   mouseDown="bordercontainer1_mouseDownHandler(event)"
					   rollOver="bordercontainer1_rollOverHandler(event)"
					   rollOut="bordercontainer1_rollOutHandler(event)"
					   click="bordercontainer1_clickHandler(event)">
		<s:layout>
			<s:HorizontalLayout paddingBottom="2"
								paddingLeft="2"
								paddingRight="2"
								paddingTop="2"
								gap="2"
								verticalAlign="middle" />
		</s:layout>
		<components:ImageButton id="removeButton"
								icon="{ImageUtil.crossIcon}"
								enabled="{editable}"
								click="remove(event)"
								mouseDown="event.stopImmediatePropagation();" />
		<s:Label id="nameLabel"
				 text="{node.name}"
				 height="100%"
				 color="{labelColor}"
				 verticalAlign="middle"
				 textAlign="center"
				 maxDisplayedLines="1" />
		<components:ImageButton id="infoButton"
								icon="{ImageUtil.infoIcon}"
								click="showInfo(event)"
								mouseDown="event.stopImmediatePropagation();" />
	</s:BorderContainer>
	<s:VGroup id="extraGroup" gap="2">
		<s:Label id="osidLabel" fontSize="10" visible="false" includeInLayout="false" />
	</s:VGroup>
</s:HGroup>
