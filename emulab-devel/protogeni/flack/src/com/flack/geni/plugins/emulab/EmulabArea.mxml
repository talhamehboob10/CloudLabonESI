<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<plugins:PluginArea xmlns:fx="http://ns.adobe.com/mxml/2009" 
					xmlns:s="library://ns.adobe.com/flex/spark" 
					xmlns:mx="library://ns.adobe.com/flex/mx"
					xmlns:plugins="com.flack.geni.plugins.*"
					Title="Emulab">
	<fx:Script>
		<![CDATA[
			import com.flack.geni.resources.SliverTypes;
			import com.flack.geni.resources.virt.LinkType;
			import com.flack.geni.resources.virt.AggregateSliver;
			import com.flack.geni.resources.virt.VirtualLink;
			import com.flack.geni.resources.virt.VirtualLinkCollection;
			import com.flack.geni.resources.virt.VirtualNode;
			import com.flack.geni.resources.virt.VirtualNodeCollection;
			import com.flack.shared.FlackEvent;
			import com.flack.shared.SharedMain;
			import com.flack.shared.utils.ImageUtil;
			import com.flack.shared.utils.StringUtil;
			
			public function insertDelayNodes():void
			{
				var changeLinks:VirtualLinkCollection = new VirtualLinkCollection();
				for each(var checkLink:VirtualLink in host.slice.links.collection)
				{
					if(checkLink.type.name == LinkType.LAN_V2
						&& checkLink.interfaceRefs.Interfaces.Nodes.getBySliverType(DelaySliverType.TYPE_DELAY).length == 0
						&& checkLink.interfaceRefs.length == 2)
					{
						changeLinks.add(checkLink);
					}
				}
				for each(var changeLink:VirtualLink in changeLinks.collection)
				{
					var newDelayNode:VirtualNode = new VirtualNode(
						host.slice,
						changeLink.interfaceRefs.collection[0].referencedInterface.Owner.manager,
						host.slice.getUniqueId(null, "delay"),
						true,
						DelaySliverType.TYPE_DELAY);
					var originalNodes:VirtualNodeCollection = changeLink.interfaceRefs.Interfaces.Nodes;
					newDelayNode.flackInfo.x = originalNodes.MiddleX;
					newDelayNode.flackInfo.y = originalNodes.MiddleY;
					host.slice.nodes.add(newDelayNode);
					
					changeLink.removeFromSlice();
					
					var leftLink:VirtualLink = new VirtualLink(host.slice);
					var leftLinkNodes:VirtualNodeCollection = new VirtualNodeCollection();
					leftLinkNodes.add(originalNodes.collection[0]);
					leftLinkNodes.add(newDelayNode);
					leftLink.establish(leftLinkNodes);
					host.slice.links.add(leftLink);
					
					var rightLink:VirtualLink = new VirtualLink(host.slice);
					var rightLinkNodes:VirtualNodeCollection = new VirtualNodeCollection();
					rightLinkNodes.add(originalNodes.collection[1]);
					rightLinkNodes.add(newDelayNode);
					rightLink.establish(rightLinkNodes);
					host.slice.links.add(rightLink);
				}
				
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					host.slice,
					FlackEvent.ACTION_CHANGED
				);
			}
		]]>
	</fx:Script>
	
	<s:Button id="delayButton"
			  width="100%"
			  height="32"
			  label="Insert delay nodes"
			  toolTip="Insert delay nodes in the middle of all links"
			  icon="{ImageUtil.timeIcon}"
			  click="insertDelayNodes()" />
	
</plugins:PluginArea>
