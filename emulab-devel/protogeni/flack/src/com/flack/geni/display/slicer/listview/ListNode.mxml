<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   width="100%" height="29"
				   borderStyle="solid"
				   styleName="normalNodeStyle"
				   rollOver="bordercontainer1_rollOverHandler(event)"
				   rollOut="bordercontainer1_rollOutHandler(event)"
				   click="bordercontainer1_clickHandler(event)" xmlns:components="com.flack.shared.display.components.*">
	<s:layout>
		<s:HorizontalLayout paddingBottom="2"
							paddingLeft="2"
							paddingRight="2"
							paddingTop="2"
							gap="2"
							verticalAlign="middle" />
	</s:layout>
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		
		.normalNodeStyle {
			borderColor: #2F4F4F;
			backgroundColor: #EAEAEA;
		}
		
		.inprogressNodeStyle {
			borderColor: #B2B300;
			backgroundColor: #E6E600;
		}
		
		.failedNodeStyle {
			borderColor: #990000;
			backgroundColor: #F08080;
		}
		
		.pausedNodeStyle {
			borderColor: FF6600;
			backgroundColor: #FF9900;
		}
		
		.validNodeStyle {
			borderColor: #006600;
			backgroundColor: #27C427;
		}
		
	</fx:Style>
	<fx:Script>
		<![CDATA[
			import com.flack.geni.display.DisplayUtil;
			import com.flack.geni.resources.virt.AggregateSliver;
			import com.flack.geni.resources.virt.Sliver;
			import com.flack.geni.resources.virt.VirtualComponent;
			import com.flack.geni.resources.virt.VirtualNode;
			import com.flack.shared.utils.ColorUtil;
			import com.flack.shared.utils.ImageUtil;
			
			import flash.filters.DropShadowFilter;
			import flash.filters.GlowFilter;
			
			[Bindable]
			public var editable:Boolean = true;
			
			[Bindable]
			private var node:VirtualNode;
			public function set Node(newNode:VirtualNode):void
			{
				node = newNode;
				labelBackgroundColor = ColorUtil.colorsDark[node.manager.colorIdx];
				labelColor = ColorUtil.colorsLight[node.manager.colorIdx];
				setStatus();
			}
			public function get Node():VirtualNode
			{
				return node;
			}
			
			public var list:SliceList;
			
			[Bindable]
			private var labelColor:uint;
			[Bindable]
			private var labelBackgroundColor:uint;
			
			public function get AllowLink():Boolean
			{
				return node.manager.SupportsLinks && node.HasUsableExperimentalInterface;
			}
			
			public function setStatus():void
			{
				if(node != null)
				{
					switch(node.allocationState)
					{
						case Sliver.ALLOCATION_PROVISIONED:
							switch(node.operationalState)
							{
								case Sliver.OPERATIONAL_READY:
									styleName = "validNodeStyle";
									break;
								case Sliver.OPERATIONAL_FAILED:
									styleName = "failedNodeStyle";
									toolTip = "Error: " + node.error;
									break;
								case Sliver.OPERATIONAL_STOPPING:
								case Sliver.OPERATIONAL_READY_BUSY:
								case Sliver.OPERATIONAL_CONFIGURING:
									styleName = "inprogressNodeStyle";
									toolTip = "Status is changing...";
									break;
								case Sliver.OPERATIONAL_NOTREADY:
									styleName = "pausedNodeStyle";
									toolTip = "Stopped";
									break;
								default:
									styleName = "normalNodeStyle";
									toolTip = "Allocation: " + Sliver.readableAllocationState(node.allocationState) +
									" Operational: " + Sliver.readableOperationalState(node.operationalState);
							}
							break;
						case Sliver.ALLOCATION_ALLOCATED:
						case Sliver.ALLOCATION_UPDATING:
							styleName = "inprogressNodeStyle";
							toolTip = "Status is changing...";
							break;
						default:
							styleName = "normalNodeStyle";
							toolTip = "Allocation: " + Sliver.readableAllocationState(node.allocationState) +
							" Operational: " + Sliver.readableOperationalState(node.operationalState);
					}
				}
				else
				{
					styleName = "normalNodeStyle";
					toolTip = "";
				}
			}
			
			public function clearStatus():void
			{
				toolTip = "";
				styleName = "normalNodeStyle";
			}
			
			public function showInfo(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				DisplayUtil.viewVirtualNode(node);
			}
			
			public function remove(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				list.removeListNode(this);
			}
			
			public function removeFromListView():void
			{
				list.nodesVgroup.removeElement(this);
			}
			
			protected function bordercontainer1_rollOverHandler(event:MouseEvent):void
			{
				if(list.SelectedNode == null || list.SelectedNode != node)
					filters = [new GlowFilter(0x000000)];
			}
			
			protected function bordercontainer1_rollOutHandler(event:MouseEvent):void
			{
				if(list.SelectedNode == null || list.SelectedNode != node)
					filters = [];
			}
			
			protected function bordercontainer1_clickHandler(event:MouseEvent):void
			{
				list.SelectedNode = node;
			}
			
		]]>
	</fx:Script>
	<components:ImageButton id="removeButton"
							icon="{ImageUtil.crossIcon}"
							enabled="{editable}"
							click="remove(event)" mouseDown="event.stopImmediatePropagation();" />
	<s:Label id="nameLabel"
			 text="{node.clientId}"
			 width="100%"
			 height="100%"
			 verticalAlign="middle"
			 textAlign="center"
			 color="{labelColor}"
			 backgroundColor="{labelBackgroundColor}"
			 maxDisplayedLines="1" />
	<components:ImageButton id="infoButton"
							icon="{ImageUtil.infoIcon}"
							click="showInfo(event)" mouseDown="event.stopImmediatePropagation();" />
</s:BorderContainer>
