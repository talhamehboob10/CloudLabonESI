#
# Compare tipservers table vs. all servers listed in tiplines and warn
# if there is a discrepancy.
#
# With the latest version of capserver, only nodes listed in tipservers
# are allowed to report info.
#
use strict;
use libdb;

my $impotent = 0;

sub DoUpdate($$$)
{
    my ($dbhandle, $dbname, $version) = @_;
    my %servers = ();

    my $query_result =
	DBQueryFatal("select server from tipservers");
    while (my $row = $query_result->fetchrow_hashref()) {
	my $server = $row->{'server'};
	if (!defined($server) || $server eq "") {
	    print STDERR "*** WARNING: NULL tipservers entry ignored\n";
	    next;
	}
	$servers{$server} = 0;
    }

    $query_result =
	DBQueryFatal("select server from tiplines group by server");
    while (my $row = $query_result->fetchrow_hashref()) {
	my $server = $row->{'server'};
	if (!defined($server) || $server eq "") {
	    print STDERR "*** WARNING: NULL server in tiplines entries!\n";
	    next;
	}
	if (!exists($servers{$server})) {
	    print STDERR "Tip server '$server' has 'tiplines' entries ".
		"but no 'tipservers' entry\n";
	    print STDERR " Adding entry...\n";
	    DBQueryFatal("INSERT INTO tipservers VALUES ('$server')");
	} else {
	    $servers{$server} = 1;
	}
    }

    foreach my $server (keys %servers) {
	if ($servers{$server} == 0) {
	    print STDERR "*** WARNING: no 'tiplines' entries associated with ".
		"server '$server'; consider removing from 'tipservers'\n";
	}
    }

    return 0;
}

1;
