#!/usr/bin/perl -w
#
# Copyright (c) 2003-2017, 2019 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use strict;
use English;
use Getopt::Std;

#
# Grant and revoke permission to use specific images.
#
sub usage()
{
    print STDERR "Usage: grantimage [-r] [-w] [-x] ";
    print STDERR "[-g <gid> | -u <uid> | -a] <imageid>\n";
    print STDERR "       grantimage -l <imageid>\n";
    print STDERR "	-h   This message\n";
    print STDERR "	-l   List permissions\n";
    print STDERR "	-w   Grant write permission; defaults to read only\n";
    print STDERR "	-r   Revoke access instead of grant\n";
    print STDERR "	-u   Grant access to a specific user\n";
    print STDERR "	-g   Grant access to a specific group (project)\n";
    print STDERR "	-a   Grant global read-only access\n";
    print STDERR "	-x   Also grant access to protogeni users\n";
    print STDERR "      -c   Set the noclone flag. Clear with -r\n";
    print STDERR "      -p   Set the noexport flag. Clear with -r\n";
    print STDERR "      -V   Apply mods to all image Versions\n";
    print STDERR "Alternate form for permissions:\n";
    print STDERR "  -R acl   Grant project|global read-only access\n";
    print STDERR "  -W acl   Grant creator|project write access\n";
    exit(-1);
}
my $optlist  = "hg:dnru:wlaxR:W:cpV";
my $impotent = 0;
my $debug    = 0;
my $revoke   = 0;
my $writable = 0;
my $listonly = 0;
my $global   = 0;
my $protogeni= 0;
my $noclone  = 0;
my $noexport = 0;
my $allvers  = 0;
my $gid;
my $uid;
my $target;
my $read_access;
my $write_access;

# Protos
sub fatal($);

#
# Please do not run as root. Hard to track what has happened.
#
if ($UID == 0) {
    die("*** $0:\n".
	"    Please do not run this as root!\n");
}

#
# Configure variables
#
my $TB            = "@prefix@";
my $PGENISUPPORT  = @PROTOGENI_SUPPORT@;
my $POSTIMAGEDATA = "$TB/sbin/protogeni/postimagedata";

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use emdb;
use EmulabConstants;
use libtestbed;
use Experiment;
use Project;
use Group;
use User;
use OSImage;
use libEmulab;

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:";

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{h})) {
    usage();
}
if (defined($options{l})) {
    $listonly = 1;
}
if (defined($options{n})) {
    $impotent = 1;
}
if (defined($options{a})) {
    $global = 1;
}
if (defined($options{r})) {
    $revoke = 1;
}
if (defined($options{d})) {
    $debug = 1;
}
if (defined($options{w})) {
    $writable = 1;
}
if (defined($options{x})) {
    $protogeni = 1;
}
if (defined($options{c})) {
    $noclone = 1;
}
if (defined($options{p})) {
    $noexport = 1;
}
if (defined($options{V})) {
    $allvers = 1;
}
if (defined($options{g})) {
    $gid = $options{g};
}
if (defined($options{u})) {
    $uid = $options{u};
}
if (defined($options{"R"})) {
    $read_access = $options{"R"};
    usage()
	if ($read_access !~ /^(global|project)$/);
}
if (defined($options{"W"})) {
    $write_access = $options{"W"};
    usage()
	if ($write_access !~ /^(creator|project)$/);
}

usage()
    if (@ARGV != 1);
usage()
    if (! ($listonly || $global || defined($gid) || defined($uid) ||
	   defined($read_access) || defined($write_access) ||
	   $noclone || $noexport));

my $imageid = $ARGV[0];

#
# Verify user.
#
my $this_user = User->ThisUser();
if (! defined($this_user)) {
    fatal("You ($UID) do not exist!");
}

if (defined($gid)) {
    $target = Group->Lookup($gid);
    if (!defined($target)) {
	fatal("No such project or group $gid\n");
    }
}
elsif (defined($uid)) {
    $target = User->Lookup($uid);
    if (!defined($target)) {
	fatal("No such user $uid\n");
    }
}
my $image = OSImage->Lookup($imageid);
if (!defined($image)) {
    fatal("No such image exists");
}
$imageid = $image->imageid();
    
# User must have permission.
if (! $image->AccessCheck($this_user, TB_IMAGEID_EXPORT())) {
    fatal("You do not have permission to change the external permissions");
}

if ($listonly) {
    my $query_result =
	DBQueryFatal("select * from image_permissions ".
		     "where imageid='$imageid'");

    while (my $row = $query_result->fetchrow_hashref()) {
	my $perm_type = $row->{'permission_type'};
	my $perm_id   = $row->{'permission_id'};
	my $perm_idx  = $row->{'permission_idx'};
	my $write     = $row->{'allow_write'};

	print "$perm_type: $perm_id ($perm_idx) ";
	print "writable" if ($write);
	print "\n";
    }
    exit(0);
}
elsif ($noclone || $noexport) {
    my $val = ($revoke ? 0 : 1);
    
    $image->Update({"noclone" => $val}, $allvers)
	if ($noclone);
    $image->Update({"noexport" => $val}, $allvers)
	if ($noexport);
    exit(0);
}
elsif ($global || defined($read_access)) {
    if (defined($read_access) && $read_access eq "project") {
	$revoke = 1;
    }
    my $val = ($revoke ? 0 : 1);
    $image->Update({"global" => $val}, $allvers) == 0
	or fatal("Could not update global flag");
    
    $image->Update({"shared" => $val}, $allvers) == 0
	or fatal("Could not update shared flag");

    if ($protogeni) {
	$image->Update({"protogeni_export" => '1'}, $allvers) == 0
	    or fatal("Could not update protogeni flag");
    }
}
elsif ($revoke) {
    $image->RevokeAccess($target) == 0
	or fatal("Could not revoke permission for $target");

    if ($protogeni) {
	$image->Update({"protogeni_export" => '0'}, $allvers) == 0
	    or fatal("Could not update protogeni flag");
    }
}
elsif (defined($write_access)) {
    my $project = $image->GetProject();
    
    if (defined($write_access)) {
	if ($write_access eq "creator") {
	    $image->RevokeAccess($project);
	    $image->GrantAccess($project, 0);
	}
	else {
	    $image->RevokeAccess($project);
	    $image->GrantAccess($project, 1);
	}
    }
}
else {
    $image->GrantAccess($target, $writable) == 0
	or fatal("Could not grant permission for $target");

    if ($protogeni) {
	$image->Update({"protogeni_export" => '1'}, $allvers) == 0
	    or fatal("Could not update protogeni flag");
    }
}

if ($PGENISUPPORT && 
    GetSiteVar("protogeni/use_imagetracker") &&
    $image->ims_reported()) {
    my $imageid = $image->imageid();
    print "Posting image $imageid to the image server ...\n";
    system("$POSTIMAGEDATA $imageid");
    if ($?) {
	print STDERR "Could not post alias to the image server\n";
    }
}
exit(0);

sub fatal($)
{
    my ($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}

