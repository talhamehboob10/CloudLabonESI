#
# The template
#
use strict;
use libinstall;
use installvars;

sub Install($$$)
{
    my ($server, $isupdate, $impotent) = @_;

    # Replace if this script does an update for ip/domain.
    return 0
	if ($isupdate);

    Phase "ports", "Installing ports", sub {
	Phase "packages", "Installing packages", sub {
	    Phase "main", "Installing main package", sub {
		DoneIfPackageInstalled($OPS_PORT, 1);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		AddPackage($OPS_PORT, $packagedir);
	    };

	    # XXX haven't had dependency issues in a long time, skip these
	    if ($FBSD_MAJOR >= 10) {
		PhaseSkip("Packages installed");
	    }

	    if ($FBSD_MAJOR > 4) {
		Phase "php", "Installing php${PHP_VERSION} package", sub {
		    DoneIfPackageInstalled($PHP_PORT, 1);
		    if (!$packagedir) {
			PhaseSkip("No package directory provided");
		    }
		    # XXX ugh, make sure an older version is not installed
		    if ($PHP_VERSION > 4) {
			DelPackage("php4-", 0, 0);
		    }
		    AddPackage($PHP_PORT, $packagedir);
		};
	    }
	    # XXX Also temporary
	    Phase "pubsub", "Installing pubsub package", sub {
		my $pspkg = $ELVIN_COMPAT ? "pubsub-elvincompat" : "pubsub";
		DoneIfPackageInstalled($pspkg, 0);
		my $pname = GetPackage($pspkg, $packagedir);
		AddPackage($pname, $packagedir);
	    };
	    # XXX Ditto
	    if ($FBSD_MAJOR > 6 || ($FBSD_MAJOR == 6 && $FBSD_MINOR > 2)) {
		if ($FBSD_MAJOR < 8) {
		    Phase "p5-DBD-mysql50",
		    "Installing old Perl Mysql package", sub {
			my $pname = InstalledPackage("p5-DBD-mysql50-", 0);
			if ($pname) {
			    if ($pname eq $P5DBD_PKG) {
				PhaseSkip("p5-DBD-mysql50 already up to date");
			    }
			    DelPackage($pname, 0, 1);
			}			    
			if (!$packagedir) {
			    PhaseSkip("No package directory provided");
			}
			AddPackage($P5DBD_PKG, $packagedir);
		    };
		}
		# XXX cannot seem to get these dependencies into emulab-ops
		if ($FBSD_MAJOR > 6) {
		    Phase "py-m2crypto", "Installing python m2crypto package", sub {
			my $pname = InstalledPackage("py[0-9]+-m2crypto-", 0);
			if ($pname) {
			    if ($pname eq $PYM2_PKG) {
				PhaseSkip("py-m2crypto already up to date");
			    }
			    DelPackage($pname, 0, 1);
			}
			if (!$packagedir) {
			    PhaseSkip("No package directory provided");
			}
			AddPackage($PYM2_PKG, $packagedir);
		    };
		}
	    }
	};
	Phase "fs-packages", "Installing FS packages", sub {
	    if (!ISFS($server)) {
		PhaseSkip("Not FS Node");
	    }
	    DoneIfPackageInstalled($FS_PORT, 1);
	    if (!$packagedir) {
		PhaseSkip("No package directory provided");
	    }
	    AddPackage($FS_PORT, $packagedir);
	};
	if ($MAILMANSUPPORT) {
	    Phase "mailman", "Installing Mailman package", sub {
		DoneIfPackageInstalled("mailman", 0);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		my $pname = GetPackage("mailman", $packagedir);
		AddPackage($pname, $packagedir);
	    };
	}
	if ($CVSSUPPORT) {
	    Phase "cvsd", "Installing cvsd package", sub {
		PhaseSkip("cvsd not supported on ops")
		    if ($FBSD_MAJOR < 6);

		DoneIfPackageInstalled("cvsd", 0);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		my $pname = GetPackage("cvsd", $packagedir);
		AddPackage($pname, $packagedir);
	    };
	}
	if ($BUGDBSUPPORT) {
	    Phase "flyspray", "Installing flyspray package support", sub {
		PhaseSkip("flyspray not supported on ops")
		    if ($FBSD_MAJOR < 6);
		
		DoneIfPackageInstalled("adodb", 0);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		my $pname = GetPackage("adodb", $packagedir);
		AddPackage($pname, $packagedir);
	    };
	}
	PhaseSkip("Package directory provided; not installing from sources")
	    if ($packagedir);
	
	Phase "pcopy", "Copying ports into place", sub {
	    DoneIfExists("$PORTSMISCDIR/emulab-ops");
	    ExecQuietFatal("$SH $TOP_SRCDIR/ports/ports-install");
	};
	my $pwd = `$PWD`;
	chomp $pwd;
	Phase "pinstall", "Installing ports (may take a while)", sub {
	    DoneIfPackageInstalled($OPS_PORT, 1);
	    
	    #
	    # This port is dead-simple, so it's safe to do it from this script
	    #
	    chdir "$PORTSMISCDIR/emulab-ops" or
		PhaseFail "Unable to change to $PORTSMISCDIR/emulab-ops: $!";
	    ExecQuietFatal("make -DBATCH install");
	};
	Phase "fs-pinstall", "Installing FS ports (may take a while)", sub {
	    if (!ISFS($server)) {
		PhaseSkip("Not FS Node");
	    }
	    DoneIfPackageInstalled($FS_PORT, 1);
	    chdir "$PORTSMISCDIR/emulab-fs" or
		PhaseFail "Unable to change to $PORTSMISCDIR/emulab-fs: $!";
	    ExecQuietFatal("make -DBATCH install");
	};
	Phase "php-pinstall", "Installing PHP${PHP_VERSION} ports (may take a while)", sub {
	    DoneIfPackageInstalled($PHP_PORT, 1);
	    # XXX ugh, make sure an older version is not installed
	    if ($PHP_VERSION > 4) {
		DelPackage("php4-", 0, 0);
	    }
	    chdir "$PORTSMISCDIR/emulab-php${PHP_VERSION}" or
		PhaseFail "Unable to change to $PORTSMISCDIR/emulab-php${PHP_VERSION}: $!";
	    ExecQuietFatal("make -DBATCH install");
	};
	if ($MAILMANSUPPORT) {
	    Phase "mailman-pinstall", "Installing mailman ports", sub {
		DoneIfPackageInstalled("mailman", 0);
		chdir "$PORTSMISCDIR/emulab-mailman" or
		    PhaseFail("Unable to change to ".
			      "$PORTSMISCDIR/emulab-mailman: $!");
		ExecQuietFatal("make -DBATCH install");
	    };
	}
	chdir $pwd;
    };
    
    return 0;
}

# Local Variables:
# mode:perl
# End:
