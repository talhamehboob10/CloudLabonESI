#!/usr/local/bin/perl -w
#
# Copyright (c) 2006 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
#
# This is a simple little script that renews "meta" slices listed either in a 
# file  or on the command line.  The Emulab metaslices were all created through
# the PlanetLab webpage, so are "plc-instantiated," and thus cannot be renewed
# through the xmlrpc interface.  Therefore, this script logs into the web 
# interface and renews the requested slices.
#

use LWP::UserAgent;
use HTTP::Request::Common qw(POST);
use HTTP::Headers;
use HTTP::Cookies;
use Date::Parse;
use English;

use lib "@prefix@/lib";
use libtestbed;

$PLAB_CREDS_FILE = "@prefix@/etc/plab/plc.pw";
$PLAB_METASLICE_FILE = "@prefix@/etc/plab/metaslices";

$WEEK = (60*60*24*7);
$SLICE_EXP_WINDOW = $WEEK * 1;

my @slices;
my $sfile = $PLAB_METASLICE_FILE;

# grab args
while ($#ARGV > 1) {
    $arg = shift;
    if ($arg eq "-f" || $arg eq "--file") {
	$sfile = shift;
	if (!defined($sfile)) {
	    print "ERROR: -f,--file require a file with slices to renew\n";
	    exit(1);
	}
    }
    else {
	push @slices, $arg;
    }
}

# open the file and grab slices
if (!scalar(@slices)) {
    open(SFILE,"$sfile") 
	or die "could not open slice file '$sfile'!";
    while (my $line = <SFILE>) {
	chomp($line);
	
	if (!($line =~ /^\#/ || $line eq '')) {
	    push @slices, $line;
	}
    }
    close(SFILE);
}

# read in our plc credentials
open(PFILE,"$PLAB_CREDS_FILE")
    or die "could not open plc uid/passwd file '$PLAB_CREDS_FILE'!";
my @creds = <PFILE>;
close(PFILE);
if (scalar(@creds) < 2) {
    print "ERROR: malformed credentials file '$PLAB_CREDS_FILE'!\n";
    exit(1);
}
chomp($creds[0]);
chomp($creds[1]);

my $uid = $creds[0];
my $passwd = $creds[1];

#print "DEBUG: uid=$uid,passwd=$passwd\n";


$PLC_LOGIN = 'https://www.planet-lab.org/db/login.php';
$PLC_SLICE_LIST = 'https://www.planet-lab.org/db/slices/index.php';
# this doesn't give us hh:mm:ss of the exp date; have to check renew page.
#$PLC_SLICE_DETAIL = 'https://www.planet-lab.org/db/slices/index.php?id=';
$PLC_RENEW = 'https://www.planet-lab.org/db/slices/renew_slice.php?id=';
$PLC_RENEW_NOARG = 'https://www.planet-lab.org/db/slices/renew_slice.php';
$PLC_SLICE_DETAIL = $PLC_RENEW;
$PLC_LOGOUT = 'https://www.planet-lab.org/db/logout.php';

print "Checking slices";
foreach my $slice (@slices) {
    print " $slice";
}
print ".\n";

my $ua = LWP::UserAgent->new();
# make a cookie jar to store the PHPSESSID cookie
$ua->cookie_jar({});

my ($req,$res);
my $phpsessid;

# log into planetlab website
#$req = HTTP::Request->new(POST => $PLC_LOGIN);
#$req->content_type('application/x-www-form-urlencoded');
#$req->content('email=$uid&password=$passwd');
$req = POST $PLC_LOGIN, [ email => $uid, password => $passwd ];

$res = $ua->request($req);
#print $res->as_string();

# ugh, now we have to dig out the slice_name->slice_id mapping so we can get
# to the details page for each slice.

my %slicemap = ();

$req = HTTP::Request->new(GET => "$PLC_SLICE_LIST");
$res = $ua->request($req);

my $page = $res->content();
my @plines = split('\n',$page);

foreach my $line (@plines) {
    if ($line =~ /href=\'\/db\/slices\/index\.php\?id\=(\d+)\'>([_a-zA-Z0-9]+)/) {
	$slicemap{$2} = $1;
	#print "DEBUG: slice id for $2 is $1\n";
    }
}

# now figure out, for each slice, what is the current exp date
# if the slice is due to expire in less than a week, renew it
#   by selecting one of the options in the drop-down box

my %ok;
my %errors;
my %sliceexp;

my $now = time();
my $line;
foreach my $slice (@slices) {

    # check if we actually got this slice in the map from PLC:
    if (!defined($slicemap{$slice})) {
	print "WARNING: cannot determine id for slice '$slice'!\n";
	$errors{$slice} = "Cannot determine id for slice '$slice'!\n";
	next;
    }

    # dig out the exp date
    # we have to look at the detail page, not the renew page, to get the date,
    # because the renew page will just tell us we can't renew if the slice has
    # has been recently renewed.
    $req = HTTP::Request->new(GET => "$PLC_SLICE_DETAIL" . $slicemap{$slice});
    $res = $ua->request($req);

    #print $res->as_string();

    my $page = $res->content();
    my @plines = split('\n',$page);
    my $i;
    my $expdate_str = '';

    my $surl = '';
    my $sdesc = '';

    foreach $line (@plines) {
	if ($line =~ /Expiration Date: .+ (\w+\-\d+\-\d+ \d+:\d+:\d+ \w+)/) {
	    $expdate_str = $1;
	    $expdate_str =~ tr/\-/ /;
	    $sliceexp{$slice} = $expdate_str;
	    #print "DEBUG: $slice expires at '$expdate_str'\n";
	    last;
	}
    }
    
    if ($expdate_str ne '') {
	# this should be the same exptime as in the 'expires' hidden field
        # in the renew form.
	my $exptime = str2time($expdate_str);
	#print "slice '$slice' expires in " . ($exptime - $now) . "s at $exptime\n";

	if ($exptime < $now) {
	    $errors{$slice} = "Slice '$slice' already expired!";
	    print "WARNING: slice '$slice' already expired!\n";
	}
	elsif (($exptime - $now) > $SLICE_EXP_WINDOW) {
	    $ok{$slice} = "Slice does not expire for " . (($exptime - $now)/(60*60*24)) . " days\n";
	}
	else {
	    # need a renew:
	    # first GET the renew page so we can parse out the description
	    # and url fields in the form (they're easiest to grab here)
	    $req = HTTP::Request->new(GET => "$PLC_RENEW" . $slicemap{$slice});
	    $res = $ua->request($req);

	    $page = $res->content();
	    @plines = split('\n',$page);
	    foreach $line (@plines) {
		if ($line =~ /\<input.*name=[\"\']{1}url[\"\']{1} value=[\"\']{1}(.*)[\"\']{1} \/\>/) {
		    # found the url
		    $surl = $1;
		}
		elsif ($line =~ /\<textarea name=[\"\']{1}description[\"\']{1}.*\>(.*)\<\/textarea/) {
		    # found the description
		    $sdesc = $1;
		}
	    }

	    if ($surl eq '' || $sdesc eq '') {
		print "WARNING: could not get url and description for slice '$slice'; cannot renew!\n";
		$errors{$slice} = "could not get url and description for slice '$slice'";
	    }
	    else {

		#print "DEBUG: got '$surl' and '$sdesc' for url and desc\n";

		# always push it out for a month -- the longest item in the 
		# page (at least it was when I looked in 12/2006)
		# the page should accept renews up to 8 weeks though...
		$req = POST $PLC_RENEW_NOARG, 
		           [ 'id' => $slicemap{$slice},
			     'expires' => $exptime,
			     'submitted' => 'Renew Slice', 
			     'url' => $surl,
			     'description' => $sdesc,
			     'expire_len' => 4 ];
		$res = $ua->request($req);

		#print $res->as_string();

		$ok{$slice} = "Renewed";
	    }
	}
    }
    else {
	print "WARNING: could not determine slice expiration time for '$slice'\n";
	$errors{$slice} = "couldn't get expiration time for slice '$slice'";
    }
    
}


# logout
$req = HTTP::Request->new(GET => $PLC_LOGOUT);
$res = $ua->request($req);

# spam time!
# XXX: this goes to my (dmj) account right now until I am sure that things
# are working good...
my $TBOPS = "johnsond\@flux.utah.edu";

my $mailcontent = "Meta Slice Renew Report:\n\n";
foreach my $slice (@slices) {
    $mailcontent .= 
	"Slice '$slice':\n" . 
        "  Expiration time: ".$sliceexp{$slice}."\n";
    if (defined($errors{$slice})) {
	$mailcontent .= "  ERROR: " . $errors{$slice} . "\n";
    }
    elsif (defined($ok{$slice})) {
	$mailcontent .= "  Success: " . $ok{$slice} . "\n";
    }
    else {
	$mailcontent .= "  Nothing identifiable happened!\n";
    }
    $mailcontent .= "\n";
}

print "Sending report.\n";

SENDMAIL("Testbed Operators <$TBOPS>",
	 "plabmsrenew did something",
	 "$mailcontent",
	 "plabmsrenew <$TBOPS>");

exit(0);
