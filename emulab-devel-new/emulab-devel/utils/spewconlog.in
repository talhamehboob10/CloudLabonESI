#!/usr/bin/perl -wT
#
# Copyright (c) 2005-2016 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;

#
# Spew a console log from the tipserver that hosts it.
#
sub usage()
{
    print(STDOUT "Usage: spewconlog [-l linecount] [-k key [-r]] node\n");
    exit(-1);
}
my $optlist   = "l:k:r";
my $linecount = 0;
my $key;
my $reuseurl  = 0;
my $logdir    = "/var/log/tiplogs";
my $logname;
my $cmd;

#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $ELABINELAB  = @ELABINELAB@;
my $SSH		= "$TB/bin/sshtb";
my $SAVEUID	= $UID;

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin:/usr/site/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be setuid! Maybe its a development version?\n");
}

#
# This script is setuid, so please do not run it as root. Hard to track
# what has happened.
#
if ($UID == 0) {
    die("*** $0:\n".
	"    Please do not run this as root! Its already setuid!\n");
}

#
# Not yet ... need a xmlrpc hook.
#
if ($ELABINELAB) {
    print STDERR "*** $0:\n".
	"    Not supported in an inner Emulab, yet!\n";
    exit(0);
}

#
# Turn off line buffering on output. Very important for this script!
#
$| = 1; 

# Load the Testbed support stuff.
use lib "@prefix@/lib";
use libdb;
use libtestbed;
use Node;

# Be careful not to exit on transient error
$libdb::DBQUERY_MAXTRIES = 30;

#
# Locals
# 
my $logfile;

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"k"})) {
    $key = $options{"k"};
}
if (defined($options{"l"})) {
    $linecount = $options{"l"};

    # Untaint of course.
    if ($linecount =~ /^([\d]+)$/) {
	$linecount = $1;
    }
    else {
	die("*** $0:\n".
	    "    Bad data in linecount: $linecount\n");
    }
}
if (defined($options{"r"})) {
    $reuseurl = 1;
}
usage()
    if (@ARGV != 1);
my $node_id = $ARGV[0];

# Untaint
if ($node_id =~ /^([-\w]+)$/) {
    $node_id = $1;
}
else {
    die("*** Tainted node name: $node_id\n");
}
my $node = Node->Lookup($node_id);
if (!defined($node)) {
    die("*** $0:\n".
	"    No such node $node_id!\n");
}

#
# Grab DB data. 
#
my $query_result =
    DBQueryFatal("select server from tiplines where node_id='$node_id'");

if (!$query_result->numrows) {
    die("*** $0:\n".
	"    No console log (tipline) available for $node_id!\n");
}
my ($tipserver) = $query_result->fetchrow_array();

#
# Check permission. Called as nobody if there is a key.
#
if (defined($key)) {
    if ($node->CheckTipAcl($key)) {
	die("*** $0:\n".
	    "    You not have permission to view console log for $node_id!\n");
    }
    if (!$reuseurl) {
	$node->ClrTipAclUrl();
    }
}
elsif (!TBAdmin($UID) &&
       (! TBNodeAccessCheck($UID, TB_NODEACCESS_READINFO, ($node_id)))) {
    die("*** $0:\n".
	"    You not have permission to view console log for $node_id!\n");
}

#
# Form the logfile name; admin users get the .log while users get .run.
#
if ($node->isvirtnode()) {
    $logname = "/var/emulab/vms/vminfo/$node_id/${node_id}.log";
}
elsif (defined($key) || !TBAdmin($UID)) {
    $logname = "$logdir/${node_id}.run";
}
else {
    $logname = "$logdir/${node_id}.log";
}

#
# Run an ssh command in a child process, protected by an alarm to
# ensure that the ssh is not hung up forever if the machine is in
# some funky state.
#
my $syspid = fork();

#
# Parent waits for ssh to complete.
# 
if ($syspid) {
    local $SIG{ALRM} = sub { kill("TERM", $syspid); };
    alarm 20;
    waitpid($syspid, 0);
    my $exitstatus = $?;
    alarm 0;

    if ($exitstatus) {
	exit(1);
    }
    exit(0);
}

#
# Now we want to ssh over and cat the file.
#
if ($linecount) {
    $cmd = "tail -${linecount} $logname | cat -v";
}
else {
    $cmd = "cat -v $logname";
}

$UID = 0;
system("$SSH -host $tipserver $cmd");
$UID = $SAVEUID;
exit(($? == 0 ? 0 : 1));
