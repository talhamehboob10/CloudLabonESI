#!/usr/bin/perl -w
#
# Copyright (c) 2016-2019 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use strict;
use English;
use Getopt::Std;
use Date::Parse;

#
# Set up and clear node pre-reservations.
#
sub usage()
{
    print STDERR "Usage: announce -a <-p portal> [-c] [-s alert_style] [-b button_label] [-u action_url] [-m max_seen_count] <announcement_text>\n";
    print STDERR "       announce -l [active|retired|all]\n";
    print STDERR "       announce -r idx|uuid [-c]\n";
    print STDERR "       announce -R idx|uuid\n";
    print STDERR "       announce -i idx\n";
    print STDERR "       announce -h\n";
    print STDERR "   -h   This message\n";
    print STDERR "   -a   Create a new announcement with the given announcement text.\n";
    print STDERR "   -p   Set the portal to be the one given. Should be one of 'cloudlab', 'emulab', 'aptlab', 'phantomnet', 'powder'.\n";
    print STDERR "   -s   Set the style of the overall announcement box. Should normally be one of bootstraps alert-* classes. Defaults to 'alert-info'.\n";
    print STDERR "        Common styles: 'alert-success' -> green, 'alert-info' -> blue, 'alert-warning' -> yellow, 'alert-danger' -> red\n\n";
    print STDERR "   -b   If there is an action associated with this announcement, this is the text which goes into the action button. Can include HTML. If it is unset, there is no action button.\n";
    print STDERR "   -u   URL of action associated with this announcement. {uid_idx} and {uid} can be used as URL templates to generate a different URL on a per-user basis. If it is unset, there is no action button.\n";
    print STDERR "   -m   The maximum number of times that this announcement will appear to a user. Every page view (even those in a single session) counts. A value of '0' indicates that the announcement should keep appearing indefinitely until dismissed by the user or an action is taken. Defaults to 20.\n";
    print STDERR "   -l   List global announcements. Defaults to listing active announcements.\n";
    print STDERR "   -r   Retire announcement with the given idx. A retired announcement will no longer be displayed to users.\n";
    print STDERR "   -R   Purge announcement from DB with the given idx or uuid.\n";
    print STDERR "   -i   Info about a particular announcement.\n";
    print STDERR "   -c   Compatibility mode. When adding in compatibility mode, the sitevar is changed, thus setting the legacy Emulab announcement. When removing in compatibility mode, the sitevar is cleared.\n";
    print STDERR "   -U   In add mode, specify a specific target user.\n";
    print STDERR "   -P   In add mode, add 'canned' portal info announcement ".
	"for user\n";
    exit(-1);
}
my $optlist  = "haA:cp:s:b:u:m:lr:R:i:U:t:I:S:E:w:P";
my $add_mode = 0;
my $update_mode = 0;
my $update_target;
my $list_mode = 0;
my $retire_mode = 0;
my $purge_mode = 0;
my $sees_mode = 0;
my $info_mode = 0;
my $portal = undef;
my $style = "alert-info";
my $button = undef;
my $url = undef;
my $max_seen = 20;
my $retire_idx = undef;
my $info_idx = undef;
my $text = undef;
my $target_user = undef;
my $uuid = undef;
my $display_start = undef;
my $display_end = undef;
my $priority = undef;
my $list_type = "active";
my $compatibility = 0;
my $canned = 0;

my $query_result;

# Protos
sub fatal($);

#
# Configure variables
#
my $TB		 = "@prefix@";
my $TBOPS        = "@TBOPSEMAIL@";

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use emdb;
use emutil;
use libtestbed;
use libdb;
use User;

my $CANNED = "Looking for the old Emulab interface? ".
    "See this <a target=_blank href='https://gitlab.flux.utah.edu/emulab/".
    "emulab-devel/wikis/faq/Using%20the%20Testbed/Emulab%20Classic'>".
    "FAQ entry</a>.";
#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:";

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"h"})) {
    usage();
}
if (defined($options{"a"})) {
    $add_mode = 1;
}
if (defined($options{"A"})) {
    $update_mode = 1;
    $update_target = $options{"A"};
}
if (defined($options{"l"})) {
    $list_mode = 1;
}
if (defined($options{"r"})) {
    $retire_mode = 1;
    $retire_idx = $options{"r"};
}
if (defined($options{"R"})) {
    $purge_mode = 1;
    $retire_idx = $options{"R"};
}
if (defined($options{"w"})) {
    $sees_mode  = 1;
    $retire_idx = $options{"w"};
}
if (defined($options{"i"})) {
    $info_mode = 1;
    $info_idx = $options{"i"};
}
if (defined($options{"p"})) {
    $portal = $options{"p"};
}
if (defined($options{"s"})) {
    $style = $options{"s"};
}
if (defined($options{"b"})) {
    $button = $options{"b"};
}
if (defined($options{"u"})) {
    $url = $options{"u"};
}
if (defined($options{"t"})) {
    $uuid = $options{"t"};
    if ($uuid !~ /^[-\w]*$/) {
	print STDERR "Invalid uuid\n";
	exit(1);
    }
}
if (defined($options{"m"})) {
    $max_seen = $options{"m"};
}
if (defined($options{"U"})) {
    $target_user = User->Lookup($options{"U"});
    if (!defined($target_user)) {
	print STDERR "No such user!\n";
	exit(-1);
    }
}
if (defined($options{"S"})) {
    $display_start = $options{"S"};
    if ($display_start !~ /^\d+$/) {
	$display_start = str2time($display_start);
	if (!defined($display_start)) {
	    fatal("Could not parse -S option.");
	}
    }
}
if (defined($options{"E"})) {
    $display_end = $options{"E"};
    if ($display_end !~ /^\d+$/) {
	$display_end = str2time($display_end);
	if (!defined($display_end)) {
	    fatal("Could not parse -E option.");
	}
    }
}
if (defined($options{"I"})) {
    $priority = $options{"I"};
    if ($priority !~ /^\d+$/) {
	fatal("Could not parse -I option.");
    }
}
if (defined($options{"P"})) {
    $canned = 1;
}
if (defined($options{"c"})) {
    $compatibility = 1;
}

if ($add_mode)
{
    if (! defined($portal)) {
	print STDERR "Portal must be defined when adding an announcement\n\n";
	usage();
    }
    elsif ($portal ne "cloudlab" && $portal ne "emulab" &&
	   $portal ne "aptlab" && $portal ne "phantomnet" &&
	   $portal ne "powder") {
	print STDERR "Portal $portal is invalid. Must be cloudlab, emulab, aptlab, phantomnet, or powder.\n\n";
	usage();
    }
}

if ($add_mode + $update_mode + $list_mode +
    $retire_mode + $sees_mode + $purge_mode + $info_mode != 1) {
    print STDERR "No mode selected. Must use one of -a, -A, -r, -R, -l, or -i\n\n";
    usage();
}

if ($add_mode || $update_mode) {
    if ($canned) {
	$text = $CANNED;
    }
    elsif (!@ARGV) {
	usage();
    }
    else {
	$text = join(' ', @ARGV);
    }
}

if (! $list_mode && ! $add_mode && ! $update_mode &&
    ! $purge_mode && scalar(@ARGV) > 0) {
    usage();
}

if ($list_mode && scalar(@ARGV) > 0) {
    $list_type = $ARGV[0];
}

if ($add_mode) {
    #
    # Add a new announcement
    #
    my $query = "insert into apt_announcements set ";
    $query .= "created=NOW()";
    $query .= ", portal=" . DBQuoteSpecial($portal);
    $query .= ", max_seen=" . DBQuoteSpecial($max_seen);
    $query .= ", text=" . DBQuoteSpecial($text);
    $query .= ", style=" . DBQuoteSpecial($style);
    if (defined($button) && defined($url)) {
	$query .= ", link_label=" . DBQuoteSpecial($button);
	$query .= ", link_url=" . DBQuoteSpecial($url);
    }
    if (defined($target_user)) {
	$query .= ", uid_idx='" . $target_user->uid_idx() . "'";
    }
    if (defined($uuid)) {
	$query .= ", uuid='$uuid'";
    }
    else {
	$query .= ", uuid=uuid()";
    }
    if (defined($display_start)) {
	$query .= ", display_start=FROM_UNIXTIME($display_start)";
    }
    if (defined($display_end)) {
	$query .= ", display_end=FROM_UNIXTIME($display_end)";
    }
    if (defined($priority)) {
	$query .= ", priority='$priority'";
    }
    
    DBQueryFatal($query);
    if ($compatibility)
    {
	TBSetSiteVar("web/banner", $text);
    }
} elsif ($update_mode) {
    #
    # Update existing announcement.
    #
    my $query = "update apt_announcements set ";
    $query .= "text=" . DBQuoteSpecial($text);
    if (defined($button) && defined($url)) {
	$query .= ", link_label=" . DBQuoteSpecial($button);
	$query .= ", link_url=" . DBQuoteSpecial($url);
    }
    if (defined($display_start)) {
	$query .= ", display_start=FROM_UNIXTIME($display_start)";
    }
    if (defined($display_end)) {
	$query .= ", display_end=FROM_UNIXTIME($display_end)";
    }
    $query .= " ";
    if (ValidUUID($update_target)) {
	$query .= "where uuid='$update_target'";
    }
    elsif ($update_target =~ /^\d+$/) {
	$query .= "where idx='$update_target'";
    }
    else {
	fatal("Invalid argument for -A mode, must be uuid or idx");
    }
    DBQueryFatal($query);
} elsif ($list_mode) {
    #
    # List announcements
    #
    my $condition = "retired=0 and ";
    if ($list_type eq "all") {
	$condition = "";
    } elsif ($list_type eq "retired") {
	$condition = "retired=1 and ";
    }
    $query_result = 
	DBQueryFatal("select idx, portal, text from apt_announcements where ".$condition."uid_idx is NULL");
    print "idx\tPortal    Text\n";
    print "---\t-------    ----\n";
    while (my ($idx, $portal, $text) = $query_result->fetchrow_array()) {
	my $textbit = substr($text, 0, 55);
	if (length($text) > 55) {
	    $textbit = $textbit . "...";
	}
	my $portalpad = sprintf("%-10s", $portal);
	print "$idx\t$portalpad $textbit\n"
    }
} elsif ($purge_mode) {
    #
    # Purge an announcement
    #
    my $clause = (ValidUUID($retire_idx) ?
		  "a.uuid='$retire_idx'" : "a.idx='$retire_idx'");
    DBQueryFatal("delete a, i ".
		 "from apt_announcements as a ".
		 "join apt_announcement_info as i on a.idx = i.aid ".
		 "where $clause");
} elsif ($retire_mode) {
    #
    # Retire an announcement
    #
    my $clause = (ValidUUID($retire_idx) ?
		  "uuid='$retire_idx'" : "idx='$retire_idx'");
    $query_result =
	DBQueryFatal("update apt_announcements ".
		     "set retired=1 where $clause");
    if ($compatibility)
    {
	TBSetSiteVar("web/banner", "");
    }
} elsif ($sees_mode) {
    #
    # Force a retired announcement back to work.
    #
    my $clause = (ValidUUID($retire_idx) ?
		  "uuid='$retire_idx'" : "idx='$retire_idx'");
    $query_result =
	DBQueryFatal("update apt_announcements ".
		     "set retired=0 where $clause");
} elsif ($info_mode) {
    #
    # Details about an announcement
    #
    $query_result =
	DBQueryFatal("select count(*) from apt_announcement_info where aid=".DBQuoteSpecial($info_idx));
    my ($seen) = $query_result->fetchrow_array();

    $query_result =
	DBQueryFatal("select count(*) from apt_announcement_info where clicked=1 and aid=".DBQuoteSpecial($info_idx));
    my ($clicked) = $query_result->fetchrow_array();

    $query_result =
	DBQueryFatal("select count(*) from apt_announcement_info where dismissed=1 and aid=".DBQuoteSpecial($info_idx));
    my ($dismissed) = $query_result->fetchrow_array();
    $query_result =
	DBQueryFatal("select idx, created, uid_idx, portal, retired, max_seen, text, style, link_label, link_url from apt_announcements where idx=".DBQuoteSpecial($info_idx));
    if (my ($idx, $created, $uid_idx, $portal, $retired,
	    $max_seen, $text, $style, $link_label,
	    $link_url) = $query_result->fetchrow_array())
    {
	print "-----------------------------\n";
	print "Details for announcement $idx\n";
	print "-----------------------------\n\n";
	print "Created:\t$created\n";
	print "Portal:\t$portal\n\n";
	if (defined($link_label) && defined($link_url)) {
	    print "Button Label:\t$link_label\n";
	    print "Button URL:\t$link_url\n";
	}
	print "Style:\t\t$style\n";
	if ($max_seen == 0) {
	    print "Shown until dismissed or clicked\n";
	} else {
	    print "Shown a maximum of $max_seen times\n";
	}
	print "\n";

	if ($retired) {
	    print "* Announcement is retired\n";
	} else {
	    print "* Announcement is active\n";
	}
	print "* Seen by $seen users\n";
	if (defined($link_label) && defined($link_url)) {
	    print "* Clicked by $clicked users\n";
	}
	print "* Dismissed by $dismissed users\n";

	print "\nText of announcement:\n\n";
	print "$text\n\n";
    }
}

sub fatal($)
{
    my ($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}

