#!/usr/bin/perl -w

#
# Copyright (c) 2000-2002, 2006, 2008 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#


#
# schemadiff - Find the differences between two database schemas, as reported
# by mysqldump. Can print out a summary of changes, or SQL commands to make
# the necessary changes
#

use strict;
use Getopt::Std; 

#
# Default settings for the options
#
my $debug = 0;
my $sql = 1;
my $quiet = 0;

#
# Return value - number of differences between the two files
#
my $diff = 0;

#
# Process command-line args
#
my %opt;
getopts('hdsq', \%opt); 
if ($opt{h}) {
    exit &usage;
}
if ($opt{d}) {
    $debug = 1;
}
if ($opt{s}) {
    $sql = 0;
}
if ($opt{q}) {
    $quiet = 1;
    $sql = 0;
    $debug = 0;
}

if (@ARGV != 2) {
    exit &usage
}

sub read_sitevars($$);

my ($file1,$file2) = @ARGV;

my %sitevars1 = ();
my %sitevars2 = ();

my %sitevars  = ();

read_sitevars(\%sitevars1, $file1);
read_sitevars(\%sitevars2, $file2);

#
# Take a union of the keynames.
#
foreach my $i (keys %sitevars1) { $sitevars{$i} = 1; }
foreach my $i (keys %sitevars2) { $sitevars{$i} = 1; }

foreach my $i (keys %sitevars) {
    if (exists $sitevars1{$i} && exists $sitevars2{$i}) {
	my @setClause = ();
	if ($sitevars1{$i}{"dval"} ne $sitevars2{$i}{"dval"}) {	
	    push @setClause, "defaultvalue=" . $sitevars2{$i}{"dval"};
	}
	if ($sitevars1{$i}{"desc"} ne $sitevars2{$i}{"desc"}) {	
	    push @setClause, "description=" . $sitevars2{$i}{"desc"};
	}
	if ($sitevars1{$i}{"ns_include"} ne $sitevars2{$i}{"ns_include"}) {
	    push @setClause, "ns_include=" . $sitevars2{$i}{"ns_include"};
	}
	if (@setClause) {
	    $diff++;
	    if (!$quiet) { print "# sitevar $i was changed.\n"; }
	    if ($sql) {
		print "UPDATE sitevariables SET " . 
		      join(",",@setClause) . " " .
		      "WHERE name=$i;\n\n";
	    }
	}
    } elsif (exists $sitevars1{$i} && ! exists $sitevars2{$i}) {
	$diff++;
	if (!$quiet) { print "# sitevar $i was deleted.\n"; }
	if ($sql) {
	    print "DELETE FROM sitevariables WHERE name=$i;\n\n";
	}
    } elsif (! exists $sitevars1{$i} && exists $sitevars2{$i}) {
	$diff++;
	if (!$quiet) { print "# sitevar $i was added.\n"; }
	if ($sql) {
	    print "INSERT INTO sitevariables VALUES ".
		  "(".
	          "$i,".
	          "NULL" . ",".
	          $sitevars2{$i}{"dval"} . ",".
	          $sitevars2{$i}{"desc"} . ",".
		  $sitevars2{$i}{"ns_include"} .
	          ");\n\n";
	}
    }
}

if ($diff > 0) {
    exit(1);
} else {
    exit(0);
}

######################################################################
# Subs
######################################################################

sub read_sitevars($$) {

    my $hashref = shift;
    
    #
    # Open the file
    #
    my $filename = shift;
    open(FH,"<$filename") or die "Unable to open $filename: $!\n";

    while(<FH>) {
	my ($name, $val, $dval, $desc, $ns_include);

	if (/^INSERT( IGNORE)? INTO `?sitevariables`?/) {
	    #
	    # Either match NULL, or
	    #   a quote followed by some number of:
	    #     + characters which aren't backslashes or quotes, or
	    #     + slashes followed by any character
	    #   followed by a closed quote.
	    #
	    my $match = "^(('([^\\']|(\\\\.))*')|(NULL))";

	    #
	    # Either match NULL, or
	    #   a nonempty sequence of digits
            #
	    my $matchint = "^(([0-9]+)|(NULL))";

	    s/^INSERT( IGNORE)? INTO `?sitevariables`? VALUES \(//;
	    ($name) = /${match},/;
	    s/${match},// or die "bad name parse $_";
	    ($val) = /${match},/;
	    s/${match},// or die "bad val parse $_";
	    ($dval) = /${match},/;
	    s/${match},// or die "bad dval parse $_";
	    ($desc) = /${match},/;
	    s/${match},// or die "bad desc parse $_";
	    ($ns_include) = /${matchint}\);/;
	    s/${matchint}\);// or die "bad ns_include parse $_";

            debug("Adding variable $name...\n");
            ${$hashref}{$name}{"dval"} = $dval;
            ${$hashref}{$name}{"desc"} = $desc;
	    ${$hashref}{$name}{"ns_include"} = $ns_include;
        }
    }

    close (FH);
}

#
# Print the message, only if the gloabal $debug variable is set
#
sub debug(@) {
    if ($debug) {
	print @_;
    }
}

#
# Usage message
#
sub usage() {
    print "usage: $0 [-h] [-s] [-d] <file1> <file2>\n";
    print "Prints on stdout a set of SQL commands to change the database\n";
    print "from file1 so that it has the same sitevars as the database from\n";
    print "file2 (same argument order as diff)\n\n";
    print "-h    This message\n";
    print "-s    Summary: Don't print SQL statments to apply changes\n";
    print "-q    Quiet: Don't print anything, just return a value\n";
    print "-d    Turn on debugging output\n";
    return 1;
}





