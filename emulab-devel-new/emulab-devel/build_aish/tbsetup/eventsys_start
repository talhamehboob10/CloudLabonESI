#!/usr/bin/perl -wT
#
# Copyright (c) 2000-2007 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use strict;
use English;

#
# Start event schedulers for all swapped-in experiments.
# Run whenever the boss node is rebooted.
#

#
# Configure variables
#
my $TB		= "/users/achauhan/Cloudlab/CloudLabonESI/emulab-devel-new/emulab-devel/build_aish";
my $TBOPS       = "testbed-ops\@ops.cloudlab.umass.edu";
my $BOSSADDR	= "boss.cloudlab.umass.edu";
my $EVENTSYS    = 1;
my $TESTMODE    = 0;

my $evcontrol	= "$TB/bin/eventsys_control";

#
# Only root can do this.
#
if ($UID) {
    die("*** $0:\n".
	"    You do not have permission to control the event system!\n");
}

#
# The event system is currently optional.
#
if (! $EVENTSYS) {
    exit(0);
}

#
# Do nothing when testing.
#
if ($TESTMODE) {
    print "Testing run - no event system.\n";
    exit(0);
}

#
# Testbed Support libraries
#
use lib "/users/achauhan/Cloudlab/CloudLabonESI/emulab-devel-new/emulab-devel/build_aish/lib";
use libdb;
use libtestbed;
use Experiment;

sub failed($$);

#
# Turn off line buffering on output
#
$| = 1;

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Find all the active experiments.
#
my @experiments = Experiment->AllActive();

foreach my $experiment (@experiments) {
    my $pid     = $experiment->pid();
    my $eid     = $experiment->eid();
    my $swapper = $experiment->GetSwapper();
    my $project = $experiment->GetProject();
    my $group   = $experiment->GetGroup();

    failed($experiment, "Could not load swapper object")
	if (!defined($swapper));
    failed($experiment, "Could not load project object")
	if (!defined($project));
    failed($experiment, "Could not load group object")
	if (!defined($group));
    
    #
    # Ignore "special" experiments
    #
    next
	if ($pid eq TB_OPSPID);

    my $unix_uid  = $swapper->unix_uid();
    my $unix_pgid = $project->unix_gid();
    my $unix_egid = $group->unix_gid();
    
    #
    # Set uid/gid to that of the user.  We need to be in both the
    # project's unix group and the experiments's unix group in order
    # for the scheduler to write its logfile (in eventsys_control).
    #
    # Set some environment too.
    #
    $GID = $unix_pgid;
    $EGID = "$unix_pgid $unix_pgid $unix_egid";
    $EUID = $UID = $unix_uid;
    $ENV{'USER'} = $swapper->uid();

    print "Starting event scheduler for $experiment ...\n";
    system("$evcontrol start $pid,$eid");
    if ($?) {
	failed($experiment, "$evcontrol failed, status=$?");
    }
    else {
	print "Started event scheduler for $pid/$eid\n";
    }
    # set uid back to root
    $EUID = $UID = 0;
}
exit(0);

sub failed($$)
{
    my ($experiment, $msg) = @_;

    warn("$msg");
    warn("WARNING: did not start event scheduler for $experiment");
}
