#!/usr/bin/perl -wT
#
# Copyright (c) 2005, 2007 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;

#
# Change delay params for a link.
#
sub usage()
{
    print(STDERR
	  "Usage: setdest -x xpos -y ypos -o orientation node_id\n");
    # Web interface cares about this return value!
    exit(-1);
}
my $optlist = "dx:y:o:";
my $debug   = 0;
my $destx;
my $desty;
my $desto;

#
# Configure variables
#
my $TB		= "/users/achauhan/Cloudlab/CloudLabonESI/emulab-devel-new/emulab-devel/build_aish";
my $TBOPS       = "testbed-ops\@ops.cloudlab.umass.edu";
my $TEVC        = "$TB/bin/tevc";
my $nodeid;

#
# Testbed Support libraries
#
use lib "/users/achauhan/Cloudlab/CloudLabonESI/emulab-devel-new/emulab-devel/build_aish/lib";
use libdb;
use libtestbed;
use Node;

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:/usr/sbin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"x"})) {
    $destx = $options{"x"};
}
if (defined($options{"y"})) {
    $desty = $options{"y"};
}
if (defined($options{"o"})) {
    $desto = $options{"o"};
}
if (@ARGV != 1) {
    usage();
}
if (! (defined($destx) && defined($desty) && defined($desto))) {
    usage();
}
$nodeid = $ARGV[0];

#
# Untaint args.
#
if ($destx =~ /^([\d.]+)$/) {
    $destx = $1;
}
else {
    die("*** Bad data in destx: $destx.\n");
}
if ($desty =~ /^([\d.]+)$/) {
    $desty = $1;
}
else {
    die("*** Bad data in desty: $desty.\n");
}
if ($desto =~ /^([-\d.]+)$/) {
    $desto = $1;
}
else {
    die("*** Bad data in desto: $desto.\n");
}

#
# Check args and permission
#
#
my $this_user = User->ThisUser();
if (! defined($this_user)) {
    die("*** $0:\n".
	"    You ($UID) do not exist!\n");
}

my $node = Node->Lookup($nodeid);
if (!defined($node)) {
    die("*** $0:\n".
	"    Node does not exist: $nodeid\n");
}
if (!$node->AccessCheck($this_user, TB_NODEACCESS_MODIFYINFO())) {
    die("*** $0:\n".
	"    Not enough permission: $nodeid\n");

}
my $experiment = $node->Reservation();
if (!defined($experiment)) {
    die("*** $0:\n".
	"    Could not map nodeid to experiment: $nodeid\n");
}
my $pid = $experiment->pid();
my $eid = $experiment->eid();
my $vname = $node->vname();

#
# Inject an event. 
#
my $inject_string = "$TEVC -e $pid/$eid now $vname SETDEST " .
    "X=" . $destx . " Y=" . $desty . " ORIENTATION=" . $desto;

if ($debug) {
    print $inject_string . "\n";
}

system($inject_string) &&
    die("*** $0:\n".
	"    Failed to inject setdest event!\n");
    
exit(0);

