#!/usr/bin/perl -w
#
# Copyright (c) 2003-2007 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#

#
# Simple little script to start up thttpd on planetlab nodes - it actually
# restarts the server gracefully, so that any downloads already going on will
# get to complete
#

use lib '/users/mshobana/CloudLabonESI/emulab-devel-new/emulab-devel/sample_build/lib';
use libdb;

#
# Username that we'll use to ssh into planetlab nodes
#
my $PLAB_USER = "utah_svc_slice";

#
# Script to run on the plab nodes to start the webserver
#
my $THTTPD_START = "netbed_files/sbin/thttpd.restart";

#
# max parallelization
#
my $MAX_CHILDREN = 10;

#
# SSH command
#
my $SSH = "ssh -q -oBatchMode=yes -oStrictHostKeyChecking=no -l $PLAB_USER";

my @nodes = ();

if (@ARGV) {
    @nodes = @ARGV;
}

if (!scalar(@nodes)) {
    #
    # Get a list of planetlab nodes that are up
    #
    my $query_result = 
	DBQueryFatal("SELECT n.node_id FROM nodes as n " .
		     "LEFT JOIN node_status AS s ON n.node_id=s.node_id " .
		     "LEFT JOIN reserved AS r ON n.node_id = r.node_id " .
		     "WHERE n.type=\"pcplabphys\" AND s.status=\"up\" AND " .
		     "!(r.pid=\"" . NODEDEAD_PID .
		     "\" AND r.eid=\"" . NODEDEAD_EID .  "\") " . 
		     "order by n.node_id");

    while (my ($node) = $query_result->fetchrow()) {
	push @nodes, $node;
    }
}

#
# Run up to $max_children rsyncs at a time
#
my $current_children = 0;
my @failed = ();
my %children = ();
while (@nodes || $current_children) {
    if (($current_children < $MAX_CHILDREN) && @nodes) {
	#
	# If we have room for another child, start one up
	#
        my $node = pop @nodes;
        if (my $pid = fork()) {
            $current_children++;
            $children{$pid} = $node;
        } else {
            print "Starting up webserver on $node\n\n";
            exec "$SSH $node $THTTPD_START";
        }
    } else {
	#
	# Wait for a child to die, and see if it failed
	#
        my $childpid = wait();
        if ($childpid < 0) {
            die "Bad return value from wait(): $childpid\n";
        }
        if ($children{$childpid}) {
            $current_children--;
            if ($?) {
                push @failed, $children{$childpid};
            }
        }
    }
}

if (@failed) {
    print "Some nodes failed: \n";
    print map {"$_\n"} @failed;
}

exit scalar @failed;
