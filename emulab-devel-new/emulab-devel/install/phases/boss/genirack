#
# TODO:
#
# Bake in the routable ip space if possible
# Add partial reverse zone stuff. See the NOTES file.
#
# Add /usr/testbed/bin and sbin to elabman path.
# Kill OUTERBOSS_NODENAME and OUTERBOSS_SSLCERTNAME from defs

use strict;
use libinstall;
use installvars;
use libtestbed;
use emutil;
use libEmulab;
use emdb;
use User;
use NodeType;
use OSImage;
use Node;
use EmulabFeatures;

my $EMULABDEVEL  = "git://git-public.flux.utah.edu/emulab-devel.git";
my $EMULABSTABLE = "git://git-public.flux.utah.edu/emulab-stable.git";
my $NODETYPEXML  = "$TOP_SRCDIR/install/genirack/nodetype.xml";
my $SWITCHSQL    = "$TOP_SRCDIR/install/genirack/tbdb.sql";
my $CONFIGVARS   = "$PREFIX/configvars.txt";
my $RACKVARS     = "$PREFIX/etc/genirack/variables.txt";
my $RACKIPS      = "$PREFIX/etc/genirack/ips.txt";
my $RACKILO      = "$PREFIX/etc/genirack/ilo.xml";
my $RACKWIRES    = "$PREFIX/etc/genirack/wiring.xml";
my $RACKWIRESDEF = "$TOP_SRCDIR/install/genirack/wiring-Gen9.xml";
my $ILOPASSWORD  = "$PREFIX/etc/ilo.pswd";
my $ELABPASSWORD = "$PREFIX/etc/elabman.pswd";
my $HPPASSWORD   = "$PREFIX/etc/switch.pswd";
my $PORTSRC      = "http://www.emulab.net/downloads/FreeBSD-9.0-ports.tar.gz";
if ($FBSD_MAJOR >= 10) {
    $PORTSRC     = "http://www.emulab.net/downloads/FreeBSD-" .
	$FBSD_MAJOR . "." . $FBSD_MINOR . "-ports.tar.gz";
}
my $ZZZ		 = "/usr/local/etc/rc.d/zzz-inelab.sh";
my $DEFAULTOSID  = "UBUNTU14-64-STD";
my @EXPORTEDOSIDS= ("UBUNTU14-64-STD");
my $PGOSSITEVAR  = "protogeni/default_osname";
my $CHPASS	 = "/usr/bin/chpass";
my $SSHTB	 = "$PREFIX/bin/sshtb";
my $DELETENODE	 = "$PREFIX/sbin/deletenode";
my $RACKNTPCONF  = "$TOP_SRCDIR/install/genirack/ntp.conf";
my $ETCNTPCONF   = "/etc/ntp.conf";
my $ELABMANDIR   = "/users/elabman";

my %INTERFACES = (
    "xn1"   => "inet 10.1.1.1 netmask 255.255.255.0",
    "xn2"   => "inet 10.2.1.1 netmask 255.255.255.0",
    "xn3"   => "inet 10.3.1.1 netmask 255.255.255.0",
    # Direct connect to FOAM VM on the same node.
    "xn4"   => "inet 10.4.1.1 netmask 255.255.255.0",
);

my %HOSTNAMES = (
    "procurve1"       => "10.1.1.253",
    "procurve1-alt"   => "10.2.1.253",
    "procurve2"       => "10.3.1.253",
    "foam-priv"	      => "10.4.1.2",
    "flowvisor-priv"  => "10.4.1.3",
);

sub Install($$$)
{
    my ($server, $isupdate, $impotent) = @_;
    my %configvars = ();

    # Replace if this script does an update for ip/domain.
    return 0
	if ($isupdate);

    SET_TESTBED_VERSION("Genirack");

    Phase "genirack", "Doing additional Geni Rack tasks", sub {
	PhaseSkip("Not a Rack")
	    if (!$PROTOGENI_GENIRACK);

	Phase "config", "Reading in Emulab config variables", sub {
	    PhaseFail("No config file")
		if (! -e $CONFIGVARS);
	    
	    open(CN, $CONFIGVARS)
		 or PhaseFail("Could not open $CONFIGVARS: $!");
	    while (<CN>) {
		if ($_ =~ /^([-\w]*)\s*=\s*(.*)$/) {
		    my $key = $1;
		    my $val = $2;
		    if ($val =~ /^'(.*)'$/) {
			$val = $1;
		    }
		    $configvars{$key} = "$val";
		}
	    }
	    close(CN);
	};

	Phase "rackconfig", "Reading in GeniRack config variables", sub {
	    PhaseFail("No config file")
		if (! -e $RACKVARS);
	    
	    open(CN, $RACKVARS)
		 or PhaseFail("Could not open $RACKVARS: $!");
	    while (<CN>) {
		if ($_ =~ /^([-\w]*)\s*=\s*(.*)$/) {
		    my $key = $1;
		    my $val = $2;
		    if ($val =~ /^'(.*)'$/) {
			$val = $1;
		    }
		    $configvars{$key} = "$val";
		}
	    }
	    close(CN);
	};

	#
	# Have not figured this out yet; it runs during boss setup but
        # the line in fstab gets lost. Run again and it works fine. 
	#
	Phase "swapon", "Reruning rc.freebsd", sub {
	    PhaseSkip("already enabled") unless `grep -q swap $FSTAB`;
	    ExecQuietFatal("/usr/local/etc/emulab/rc/rc.freebsd");
	};

	Phase "rclocal", "Removing $ZZZ", sub {
	    PhaseSkip("already deleted")
		if (! -e $ZZZ);
	    DeleteFileFatal($ZZZ);
	};

	#
	# Update geniuser password.
	#
	if (exists($configvars{'PROTOGENI_PASSWORD'}) &&
	    $configvars{'PROTOGENI_PASSWORD'} ne "") {
	    my $geniuser = User->Lookup("geniuser");
	    PhaseFail("No geniuser account")
		if (!defined($geniuser));
	    my $passhash = PassWordHash($configvars{'PROTOGENI_PASSWORD'});
	    $geniuser->SetPassword($passhash);
	}

	#
	# This is the password to use when creating the elabman user
	# on each node iLo.
	#
	if (exists($configvars{'GENIRACK_ILO_PASSWORD'}) &&
	    $configvars{'GENIRACK_ILO_PASSWORD'} ne "" &&
	    ! -e $ILOPASSWORD) {
	    CreateFileFatal($ILOPASSWORD, $configvars{'GENIRACK_ILO_PASSWORD'});
	    ExecQuietFatal("$CHMOD 400 $ILOPASSWORD");
	}

	#
	# This is the password for the switches.
	#
	if (exists($configvars{'GENIRACK_SWITCH_PASSWORD'}) &&
	    $configvars{'GENIRACK_SWITCH_PASSWORD'} ne "" &&
	    ! -e $HPPASSWORD) {
	    CreateFileFatal($HPPASSWORD,
			    $configvars{'GENIRACK_SWITCH_PASSWORD'});
	    ExecQuietFatal("$CHMOD 400 $HPPASSWORD");
	}

	#
	# This is the elabman password.
	#
	if (exists($configvars{'PASSWORD'}) && $configvars{'PASSWORD'} ne "") {
	    my $PASSWORD = $configvars{'PASSWORD'};
	    my $passhash = PassWordHash($PASSWORD);
	    
	    Phase "elabman_pswd", "Storing the elabman password", sub {
		PhaseSkip("exists")
		    if (-e $ELABPASSWORD);
		
		CreateFileFatal($ELABPASSWORD, $PASSWORD);
		ExecQuietFatal("$CHMOD 400 $ELABPASSWORD");
	    };

	    #
	    # Make sure root/toor have the same password on boss/ops.
	    #
	    Phase "root_pswd", "Setting root password on boss", sub {
		ExecQuietFatal("$CHPASS -p '$passhash' root");
		ExecQuietFatal("$CHPASS -p '$passhash' toor");
	    };

	    # shell escape.
	    $passhash =~ s/\$/\\\$/g;
	    $passhash =~ s/\*/\\\*/g;

	    Phase "root_pswdops", "Setting root password on ops", sub {
		ExecQuietFatal("$SSHTB -host $USERNODE ".
			       "  $CHPASS -p '$passhash' root");
		ExecQuietFatal("$SSHTB -host $USERNODE ".
			       "  $CHPASS -p '$passhash' toor");
	    };
	}

	#
	# Create an ssh config file for root, to ssh to the switches.
	# Need to also put root public key here tftp can get it.
	#
	my $SSHCONFIG = "/root/.ssh/config";
	my $SSHPUBKEY = "/root/.ssh/id_rsa.pub";
	my $COPYPUBKEY= "$TFTP_DIR/id_rsa.pub";
	
	Phase "sshconfig", "Creating $SSHCONFIG", sub {
	    DoneIfExists($SSHCONFIG);
	    CreateFileFatal($SSHCONFIG,
			    "Host procurve1 procurve2", 
			    "User manager",
			    "IdentitiesOnly yes",
			    "IdentityFile /root/.ssh/id_rsa");
	    ExecQuietFatal("$CHMOD 644 $SSHCONFIG");
	};
	Phase "sshpubkey", "Copying $SSHPUBKEY to $TFTP_DIR", sub {
	    DoneIfExists($COPYPUBKEY);
	    ExecQuietFatal("/bin/cp -p $SSHPUBKEY $COPYPUBKEY");
	};

	#
	# This is the elabman SSL cert password.
	#
	my $SSLCERT_PSWDFILE = "/users/elabman/.ssl/password";
	
	if (exists($configvars{'ELABMAN_SSLCERT_PASSWORD'}) &&
	    $configvars{'ELABMAN_SSLCERT_PASSWORD'} ne "") {
	    if (! -e $SSLCERT_PSWDFILE) {
		CreateFileFatal($SSLCERT_PSWDFILE,
				$configvars{'ELABMAN_SSLCERT_PASSWORD'});
		ExecQuietFatal("$CHMOD 400 $SSLCERT_PSWDFILE");
		ExecQuietFatal("$CHOWN elabman:emulab-ops $SSLCERT_PSWDFILE");
	    }
	    #
	    # Create encrypted SSL cert for elabman, for testing PG later.
	    #
	    ExecQuietFatal("$SUDO -u $PROTOUSER $PREFIX/sbin/mkusercert ".
	         " -p " . $configvars{'ELABMAN_SSLCERT_PASSWORD'} . " elabman");
	}

	#
	# Trun on arp lockdown.
	#
	Phase "arplockdown", "Turning on arplockdown", sub {
	    ExecQuietFatal("$PREFIX/sbin/setsitevar ".
			   "general/arplockdown static");
	};

	#
	# Mike says: With the default server/client settings, the
	# client is still dropping packets and causing re-requests of
	# blocks. So for now, use the sitevars to scale back the frisbeed
	# send rate to 50000000 (50Mb/sec) for both system and user
	# images. The run time for single client is actually slightly
	# improved at this lower rate due to many fewer re-requests.
	#
	Phase "frisbeerate", "Setting frisbeed max rate", sub {
	    ExecQuietFatal("$PREFIX/sbin/setsitevar ".
			   "images/frisbee/maxrate_std 50000000");
	    ExecQuietFatal("$PREFIX/sbin/setsitevar ".
			   "images/frisbee/maxrate_usr 50000000");
	};

	#
	# A reasonable default image.
	#
	Phase "sitevar1", "Setting sitevar $PGOSSITEVAR", sub {
	    my $osimage = OSImage->LookupByName($DEFAULTOSID);
	    PhaseFail("Could not look up $DEFAULTOSID osid")
		if (!defined($osimage));
	    
	    ExecQuietFatal("$PREFIX/sbin/setsitevar ".
			   "'$PGOSSITEVAR' '$DEFAULTOSID'");
	};

	#
	# Latitude/Longitude
	#
	if (exists($configvars{'TARGETSYS_COUNTRY'}) &&
	    $configvars{'TARGETSYS_COUNTRY'} ne "") {
	    Phase "sitevar2", "Setting country sitevar", sub {
		SetSiteVar("general/default_country",
			   $configvars{'TARTGETSYS_COUNTRY'})
		    or PhaseFail("failed");
	    };
	}
	if (exists($configvars{'TARGETSYS_LATITUDE'}) &&
	    $configvars{'TARGETSYS_LATITUDE'} ne "") {
	    Phase "sitevar3", "Setting latitude sitevar", sub {
		SetSiteVar("general/default_latitude",
			   $configvars{'TARGETSYS_LATITUDE'})
		    or PhaseFail("failed");
	    };
	}
	if (exists($configvars{'TARGETSYS_LONGITUDE'}) &&
	    $configvars{'TARGETSYS_LONGITUDE'} ne "") {
	    Phase "sitevar4", "Setting longitude sitevar", sub {
		SetSiteVar("general/default_longitude",
			   $configvars{'TARGETSYS_LONGITUDE'})
		    or PhaseFail("failed");
	    };
	}
	
	
	#
	# Shutdown the testbed for the rest of this.
	#
	Phase "shutdown", "Shutting down the testbed", sub {
	    PhaseSkip("not running")
		if (!CheckDaemonRunning("stated"));
	    ExecQuietFatal("$TESTBED_CONTROL shutdown");
	};

	#
	# Create a clone of emulab-devel while we are close to the server.
	#
	if (0) {
	Phase "cloning1", "Cloning emulab-devel repository", sub {
	    DoneIfExists("$PREFIX/src/emulab-devel");
	    ExecQuietFatal("cd $PREFIX/src; ".
			   "    git clone $EMULABDEVEL emulab-devel");
	};
	Phase "cloning2", "Cloning emulab-stable repository", sub {
	    DoneIfExists("$PREFIX/src/emulab-stable");
	    ExecQuietFatal("cd $PREFIX/src; ".
			   "    git clone $EMULABSTABLE emulab-stable");
	};
	}

	#
	# Also grab the port source while close to the server.
	#
	Phase "portsrc", "Getting port source tarball", sub {
	    DoneIfExists("$PREFIX/ports.tar.gz");
	    FetchFileFatal($PORTSRC, "$PREFIX/ports.tar.gz");
	};
	Phase "ports", "Unpacking port source tarball", sub {
	    DoneIfExists("$PREFIX/ports/Mk");
	    ExecQuietFatal("cd $PREFIX; $TAR zxf $PREFIX/ports.tar.gz");
	};
	Phase "portlink", "Linking to unpacked ports tree", sub {
	    ExecQuietFatal("/bin/rm -rf /usr/ports")
		if (-e "/usr/ports" && -d "/usr/ports");
	    ExecQuietFatal("cd /usr; ln -sf testbed/ports .");
	};

	# Need to add a symlink. 
	
	#
	# Need to remove the ELABINELAB flag.
	#
	my $OLDDEFSFILE = "$main::TOP_SRCDIR/defs-elabinelab";
	my $NEWDEFSFILE = "$main::TOP_SRCDIR/defs-genirack";
	
	Phase "elabinelab", "Removing ELABINELAB stuff from defs file", sub {
	    DoneIfExists($NEWDEFSFILE);
	    # And some other stuff.
	    ExecQuietFatal("echo 'EC2META_ENABLE=1' | ".
			   " cat $OLDDEFSFILE - | sed ".
			   "   -e 's/^ELABINELAB/#ELABINELAB/' ".
			   "   -e 's/^WWWHOST=boss/WWWHOST=www/' ".
			   "   -e 's/^EXTERNAL_NTPS/#EXTERNAL_NTPS/' ".
			   "   > $NEWDEFSFILE");
	};

	#
	# Build and install software without ELABINELAB defined.
	#
	Phase "Software", "Rebuilding and Reinstalling Software", sub {
	    DoneIfExists("$PREFIX/marker");
	    
	    Phase "cleaning", "Cleaning (please be patient)", sub {
		ExecQuietFatal("cd $TOP_OBJDIR; $GMAKE clean");
	    };
	    Phase "config", "Configuring (please be patient)", sub {
		ExecQuietFatal("cd $TOP_OBJDIR; ".
			       "   $main::TOP_SRCDIR/configure ".
			       "      --with-TBDEFS=$NEWDEFSFILE ".
			       "      --disable-windows");
	    };
	    Phase "building", "Building (please be patient)", sub {
		ExecQuietFatal("cd $TOP_OBJDIR; $GMAKE");
	    };
	    Phase "installing", "Installing (please be patient)", sub {
		ExecQuietFatal("cd $TOP_OBJDIR; $GMAKE boss-install");
	    };
	    Phase "rc.testbed", "Installing testbed RC scripts", sub {
		ExecQuietFatal("$GMAKE -C $TOP_OBJDIR/rc.d install");
	    };
	    Phase "ntpd", "Installing testbed ntp config", sub {
		ExecQuietFatal("$GMAKE -C $TOP_OBJDIR/ntpd install");
	    };
	    CreateFile("$PREFIX/marker", "genirack install marker");
	};

	#
	# Add /etc/hosts entries for the switches. 
	#
	Phase "etchosts", "Adding hosts entries for switches", sub {
	    my @strings = ();
	    
	    foreach my $switch (keys(%HOSTNAMES)) {
		my $ip = $HOSTNAMES{$switch};
		push(@strings, "$ip\t$switch");
	    }
	    DoneIfEdited($HOSTS);
	    AppendToFileFatal($HOSTS, @strings);
	};

	#
	# Also need to do:
	#
	# * Comment out all the natd stuff.
	# * Comment out all static route lines except frisbee.
	# * Comment out firewall stuff.
	# * Comment out xn0 alias.
	# * Set the xn0 config to the real config.
	# * Set the defaultrouter to the real one. 
	
	Phase "rcconf", "Updating rcconf for actual boot", sub {
	    my @strings = ();
	    my @ifaces  = ();

	    # /etc/rc.conf entries for the switches.
	    foreach my $iface (keys(%INTERFACES)) {
		my $line = $INTERFACES{$iface};
		push(@strings, "ifconfig_${iface}=\"$line\"");
		push(@ifaces, $iface);
	    }
	    push(@strings,
		 "network_interfaces=\"\$network_interfaces @ifaces\"");
	    push(@strings,
		 "ifconfig_xn0=\"inet ". $configvars{"TARGETSYS_BOSSIP"} . 
		 " netmask " . $configvars{"TARGETSYS_NETMASK"} . " -tso\"");
	    # Jail network.
	    push(@strings,
		 "ifconfig_xn0_alias0=\"inet 172.17.254.254 ".
		 "netmask 255.240.0.0\"");
	    # ilo network. The control node takes .253
	    push(@strings,
		 "ifconfig_xn0_alias1=\"inet 10.249.249.254 ".
		 "netmask 255.255.255.0\"");
	    # Actual default router.
	    push(@strings,
		 "defaultrouter=\"" . $configvars{"TARGETSYS_ROUTER"} . "\"");
	    # Going to lose all static routes below, so add this back.
	    push(@strings,
		 "static_routes=\"\$static_routes frisbee\"");

	    #
	    # Okay, we want to comment out a bunch of stuff.
	    #
	    my @patterns = (qr(^natd),
			    qr(^firewall),
			    qr(^defaultrouter),
			    qr(^static_routes),
			    qr(^route_genirack),
			    qr(^route_outerboss),
			    qr(^route_vnodes),
			    qr(^ifconfig_xn0),
			    qr(^ifconfig_xn0_alias0),
	    );
	    DoneIfEdited($RCCONF);
	    UpdateFileFatal($RCCONF, \@patterns, @strings);
	};

	#
	# Add in the switch stuff which is all hard coded sql.
	#
	Phase "sql", "Adding switch goo to the database", sub {
	    ExecQuietFatal("$MYSQL $DBNAME < $SWITCHSQL");
	};

	#
	# Extra install
	# 
	Phase "genirack", "Installing extra scripts", sub {
	    ExecQuietFatal("cd $TOP_OBJDIR/install/genirack; $GMAKE install");
	};

	#
	# Add the nodes to the DB.
	#
	my $ILOIP = $configvars{"GENIRACK_ILOIP"};

	Phase "nodes", "Adding nodes to the DB", sub {
	    PhaseSkip("No ILO definitions file")
		if (! -e $RACKILO);
	    
	    my $query_result =
		DBQueryWarn("select node_id from nodes where node_id='pc1'");
	    PhaseFail("DB error")
		if (!$query_result);
	    PhaseSkip("already added")
		if ($query_result->numrows);

	    # Allow for rack specific wiring file.
	    my $wiring = (-e $RACKWIRES ? $RACKWIRES : $RACKWIRESDEF);

	    ExecQuietFatal("cd $TOP_OBJDIR/install/genirack; ".
			   "  $SUDO -u $PROTOUSER $WAP ".
			   "  perl mknewconfig -i $ILOIP ".
			   "       /tmp/output $RACKIPS $RACKILO $wiring");

	    PhaseFail("initilo.sh not generated")
		if (! -e "/tmp/output/initilo.sh");
	    ExecQuietFatal("/bin/cp -p /tmp/output/initilo.sh $PREFIX/etc");
	};

	Phase "outlets", "Adding outlets to the DB", sub {
	    my $query_result =
		DBQueryWarn("select node_id from nodes where role='testnode'");
	    PhaseFail("DB error")
		if (!$query_result);

	    while (my ($node_id) = $query_result->fetchrow_array()) {
		Phase "$node_id", "Adding outlet for $node_id", sub {
		    my $outlet_result =
			DBQueryWarn("select * from outlets ".
				    "where node_id='$node_id'");
		
		    PhaseFail("DB error")
			if (!$outlet_result);
		    PhaseSkip("already added")
			if ($outlet_result->numrows);

		    ExecQuietFatal("cd $TOP_OBJDIR/install/genirack; ".
				   "  $SUDO perl initilo.pl -o $node_id");
		};
	    }
	};

	#
	# Add this feature so we get the right libvtop.
	#
	my $LIBVTOP_FEATURE     = "LibVtopTest";
	my $LIBVTOP_DESRIPTION  = "Test version of libvtop";
    
	my $feature = EmulabFeatures->Lookup($LIBVTOP_FEATURE);
	Phase "feature", "Adding libvtop_test feature", sub {
	    PhaseSkip("already exists")
		if (defined($feature));
	    $feature = EmulabFeatures->Create($LIBVTOP_FEATURE,
					      $LIBVTOP_DESRIPTION);
	    PhaseFail("Failed to create feature")
		if (!defined($feature));
	};
	Phase "feature", "Enabling libvtop_test feature", sub {
	    $feature->SetGlobalEnable(1) == 0
		or PhaseFail("Could not enable feature");
	    $feature->SetGlobalDisable(0) == 0
		or PhaseFail("Could not clear disable flag on feature");
	};

	#
	# Update the DB with the switch community string.
	#
	if (exists($configvars{'GENIRACK_COMMUNITY'})) {
	    my $safe_password = DBQuoteSpecial($configvars{'GENIRACK_COMMUNITY'});
	    DBQueryWarn("update switch_stack_types set ".
			"snmp_community=$safe_password")
		or PhaseFail("Could not set snmp community strings");
	}

	#
	# Delete existing ops node; we recreate it later. 
	#
	Phase "opsnode", "Deleting ops node", sub {
	    PhaseSkip("already deleted")
		if (!defined(Node->Lookup("ops")));
	    ExecQuietFatal("$DELETENODE -f -s ops");
	};

	#
	# Shutdown DHCP and fix the dynamic range that rc.mkelab changed.
	# This is okay since the subnet is different then Emulab.
	#
	Phase "dhcpd", "Fixing dhcpd configuration", sub {
	    ExecQuietFatal("/bin/rm -f $DHCPD_TEMPLATE");
	    ExecQuietFatal("cd $TOP_OBJDIR/dhcpd; $GMAKE install");
	    ExecQuietFatal("$DHCPD_MAKECONF -i");
	};

	#
	# This adds the nightly download of new images from Utah.
	#
	Phase "crontab", "Editing $CRONTAB", sub {
	    DoneIfEdited($CRONTAB);
	    AppendToFileFatal($CRONTAB,
		      "0  \t6\t*\t*\t*\troot\t$PREFIX/sbin/getimages");
	};

	foreach my $osid (@EXPORTEDOSIDS) {
	    Phase "$osid", "Enabling protogeni export for $osid", sub {
		my $osimage = OSImage->LookupByName($osid);
		PhaseFail("No such image $osid")
		    if (!defined($osimage));
		$osimage->Update({"protogeni_export" => 1}) == 0
		    or PhaseFail("DB query failed");
	    };
	}

	#
	# Create a local named forward fragment file with some extra stuff.
	#
	my $reversedots;
	my $forwarddots;
	my @forward_strings = ();
	my @reverse_strings = ();
	my %local_dns = ("GENIRACK_FOAMIP"    => "foam",
			 "GENIRACK_FLOWIP"    => "flowvisor",
			 "GENIRACK_CONTROLIP" => "control",
			 "GENIRACK_ILOIP"     => "control-ilo");

	foreach my $name (keys(%local_dns)) {
	    next
		if (!exists($configvars{$name}));

	    my $hostname = $local_dns{$name};
	    my $ip = $configvars{$name};
	    my ($a,$b,$c,$num) = split(/\./, $ip);
	    $forwarddots = "${a}.${b}.${c}";
	    $reversedots = "${c}.${b}.${a}";
	    
	    push(@forward_strings, "$hostname\tIN\tA\t$ip");
	    push(@reverse_strings, "$num\tIN\tPTR\t${hostname}.${OURDOMAIN}.");
	}
	my $FORWARDFRAGMENT1 = "$NAMED_DIR/${OURDOMAIN}.internal.db.local";
	my $FORWARDFRAGMENT2 = "$NAMED_DIR/${OURDOMAIN}.db.local";
	my $REVERSEFRAGMENT  = "$NAMED_DIR/reverse/${forwarddots}.db.local";
	Phase "forward1", "Creating $FORWARDFRAGMENT1", sub {
	    DoneIfExists($FORWARDFRAGMENT1);
	    CreateFileFatal($FORWARDFRAGMENT1, @forward_strings);
	};
	Phase "forward2", "Creating $FORWARDFRAGMENT2", sub {
	    DoneIfExists($FORWARDFRAGMENT2);
	    CreateFileFatal($FORWARDFRAGMENT2, @forward_strings);
	};
	Phase "reverse", "Creating $REVERSEFRAGMENT", sub {
	    DoneIfExists($REVERSEFRAGMENT);
	    CreateFileFatal($REVERSEFRAGMENT, @reverse_strings);
	};

	#
	# 
	#
	Phase "buildenv", "Creating build environment", sub {
	    Phase "mkdir", "Creating directory in elabman", sub {
		DoneIfExists("$ELABMANDIR/emulab-devel");
		mkdir "$ELABMANDIR/emulab-devel",0775 or
		    PhaseFail("Unable to create $ELABMANDIR/emulab-devel: $!");

	    };
	    Phase "cpdefs", "Coping defs file to elabman", sub {
		DoneIfExists("$ELABMANDIR/emulab-devel/defs-genirack");
		ExecQuietFatal("/bin/cp -f $PREFIX/src/testbed/defs-genirack ".
			       "        $ELABMANDIR/emulab-devel");
	    };
	    Phase "mvsrc", "Moving src directory to elabman", sub {
		DoneIfExists("$ELABMANDIR/emulab-devel/emulab-devel");
		ExecQuietFatal("/bin/mv -f $PREFIX/src/testbed ".
			       "        $ELABMANDIR/emulab-devel/emulab-devel");
	    };
	    Phase "mvobj", "Moving obj directory to elabman", sub {
		DoneIfExists("$ELABMANDIR/emulab-devel/obj");
		ExecQuietFatal("/bin/mv -f $PREFIX/obj/testbed ".
			       "        $ELABMANDIR/emulab-devel/obj");
	    };
	    Phase "srcsymlink", "Leaving src symlink behind", sub {
		DoneIfExists("$PREFIX/src/testbed");
		ExecQuietFatal("cd $PREFIX/src; ln -s ".
			       "  $ELABMANDIR/emulab-devel/emulab-devel ".
			       "  testbed");
	    };
	    Phase "objsymlink", "Leaving obj symlink behind", sub {
		DoneIfExists("$PREFIX/obj/testbed");
		ExecQuietFatal("cd $PREFIX/obj; ln -s ".
			       "  $ELABMANDIR/emulab-devel/obj obj");
	    };
	    Phase "chown", "Changing owner and group", sub {
		ExecQuietFatal("$CHOWN -R elabman:emulab-ops ".
			       "   $ELABMANDIR/emulab-devel");
		ExecQuietFatal("$CHOWN -R -h elabman:emulab-ops ".
			       "   $ELABMANDIR/emulab-devel");
	    };
	};

	PhaseSucceed("Rack Setup Done");
    };
    return 0;
}

# Local Variables:
# mode:perl
# End:
