#
# The template
#
use strict;
use libinstall;
use installvars;

sub Install($$$)
{
    my ($server, $isupdate, $impotent) = @_;

    my ($o1) = split(/\./, $FRISADDR);
    my $route_frisbee = qq|route_frisbee="-net $o1.0.0.0/8 -iface $BOSSNODE_IP"|;

    if ($isupdate) {
	Phase "frisbee", "Updating frisbee route", sub {
	    DoneIfUpdated($RCCONF, $route_frisbee);
	    UpdateFileFatal($RCCONF, [ qr(^route_frisbee) ], $route_frisbee);
	};
	return 0;
    }

    my $apache = ("$APACHE_VERSION" eq "22" ? "apache22" :
		  "$APACHE_VERSION" eq "24" ? "apache24" : "");

    Phase "rc.conf", "Adding testbed content to $RCCONF", sub {
	DoneIfEdited($RCCONF);
	my @strings = (qq|rpcbind_enable="YES"|,
		       qq|nfs_client_enable="YES"|,
		       qq|inetd_enable="YES"|,
		       qq|inetd_flags="-wW -R 0"|,
		       qq|syslogd_flags=""|,
		       qq|tftpd_flags="-lvvvv -C 40 -s /tftpboot"|,
		       (("$APACHE_VERSION" eq "22" ||
			 "$APACHE_VERSION" eq "24") ?
			("${apache}_enable=\"YES\"",
			 "${apache}_flags=\"-DSSL\"",
			 "${apache}_profiles=\"www\"",
			 "${apache}_www_configfile=\"${HTTPD_CONF}\"",
			 "${apache}_www_enable=\"YES\"") :
			(qq|apache_enable="YES"|,
			 qq|apache_flags="-DSSL"|)),
		       qq|pubsubd_flags="-T 10"|);

	# OPSVM
	if (ISFS($server)) {
	    # don't enable NFS server unless we are the fileserver
	    push(@strings, qq|mountd_enable="YES"|);
	    push(@strings, qq|mountd_flags="-r -p 900 -S"|);
	    push(@strings, qq|nfs_server_enable="YES"|);
	    push(@strings, qq|nfs_server_flags="-u -t -n 16"|);

	    if ($WINSUPPORT) {
		if ($FBSD_MAJOR > 10) {
		    ;
		} elsif ($FBSD_MAJOR > 8 ||
			 ($FBSD_MAJOR == 8 && $FBSD_MINOR > 2)) {
		    push(@strings, qq|samba_enable="YES"|);
		    push(@strings, qq|nmbd_enable="NO"|);
		} else {
		    push(@strings, qq|smbd_enable="YES"|);
		}
	    }
	    if ($QUOTA_FSLIST ne "") {
		if ($FBSD_MAJOR > 8 || ($FBSD_MAJOR == 8 && $FBSD_MINOR > 2)) {
		    push(@strings, qq|quota_enable="YES"|);
		} else {
		    push(@strings, qq|enable_quotas="YES"|);
		}
	    }
	}

	#
	# Starting at FreeBSD 6 we use the default version of bind,
	# not the port. Er, back to a port as of FreeBSD 10.
	#
	if ($FBSD_MAJOR < 6) {
	    push(@strings, qq|named_enable="NO"|);
	}
	elsif ($FBSD_MAJOR < 10) {
	    push(@strings, (qq|named_enable="YES"|,
			    qq|named_chrootdir=""|,
			    qq|named_flags=""|,
			    qq|named_uid="root"|,
			    qq|named_pidfile="$VARRUN/named.pid"|));
	}
	else {
	    push(@strings, (qq|named_enable="YES"|,
			    qq|named_conf="/etc/namedb/named.conf"|,
			    qq|named_chrootdir=""|,
			    qq|named_flags=""|,
			    qq|named_uid="root"|));
	}

	# At FreeBSD 6, they also changed the name of the ntp start var.
	if ($FBSD_MAJOR < 6) {
	    push(@strings, qq|xntpd_enable="YES"|);
	} else {
	    push(@strings, qq|ntpd_enable="YES"|);
	}

	# add multicast route for frisbee
	my ($o1) = split(/\./, $FRISADDR);
	push(@strings, (qq|static_routes="\$static_routes frisbee"|,
			$route_frisbee));

	# enable mrouted if needed
	if ($NEEDMROUTED) {
	    push(@strings, qq|mrouted_enable="YES"|);
	}

	# Turn on firewall.
	if ($FIREWALL_BOSS) {
	    push(@strings, qq|firewall_enable="YES"|);
	    push(@strings, qq|firewall_type="/etc/boss.ipfw"|);

	}

	AppendToFileFatal($RCCONF, @strings);
    };
    return 0;
}

# Local Variables:
# mode:perl
# End:
