#
# The template
#
use strict;
use libinstall;
use installvars;

sub Install($$$)
{
    my ($server, $isupdate, $impotent) = @_;

    my $phase_message = ($isupdate ?
			 "Updating named configuration" :
			 "Setting up initial named configuration");

    if ($isupdate) {
    	Phase "backup", "Backup up old files and templates", sub {
	    BackupDirectoryFatal($NAMED_DIR);
	};
    }
    Phase "named", "Setting up initial named configuration", sub {
	Phase "building", "Building named files and templates", sub {
	    ExecQuietFatal("cd $TOP_OBJDIR/named; $GMAKE");
	};
	Phase "installing", "Installing named files and templates", sub {
	    ExecQuietFatal("cd $TOP_OBJDIR/named; $GMAKE install-real");
	};
	Phase "generating", "Generating named zone files", sub {
	    ExecQuietFatal("$NAMED_SETUP -norestart");
	};

	if (!$isupdate) {
	    # Starting at 6.0 we use the default version of bind, not the port.
	    # Starting at 10.0 we are back to a port!
	    my $named_control = "/etc/rc.d/named";
	    if ($FBSD_MAJOR < 6 && -e "$RCDIR/1.named.sh") {
		$named_control = "$RCDIR/1.named.sh";
	    } elsif ($FBSD_MAJOR >= 10 && -e "/usr/local/etc/rc.d/named") {
		$named_control = "/usr/local/etc/rc.d/named";
		if (-e "/etc/rc.d/named") {
		    rename("/etc/rc.d/named", "/etc/rc.d/.named.NOPE");
		}
	    }
	    
	    Phase "stopping", "Stopping named", sub {
		if (ExecQuiet("killall -s named")) {
		    PhaseSkip("named not running");
		}
		if (ExecQuiet("$named_control stop")) {
		    # XXX we have seen cases where the pid file is gone
		    if (ExecQuiet("killall named")) {
			PhaseSkip("named not running!?");
		    }
		}
		sleep 1;
	    };
	    Phase "starting", "Starting named", sub {
		#
		# XXX starting sometime in FBSD6, the named start/stop script
		# returns non-zero status whether it works or not.  So we just
		# check for the existance of the pidfile after running the start
		# script.
		#
		if ($FBSD_MAJOR >= 6) {
		    my $pf1 = "$VARRUN/named.pid";
		    my $pf2 = "$VARRUN/named/named.pid";
		    my $pf3 = "$VARRUN/named/pid";
		    unlink($pf1, $pf2, $pf3);
		    if (ExecQuiet("$named_control start")) {
			PhaseFail("Unable to start named");
		    }
		    sleep 1;
		    if (! -e $pf1 && ! -e $pf2 && ! -e $pf3) {
			PhaseFail("Unable to start named");
		    }
		} else {
		    ExecQuietFatal("$named_control start");
		}
	    };

	    # Lets make sure that old file is gone!
	    if ($FBSD_MAJOR >= 6) {
		Phase "cleanup", "Cleaning up old files", sub {
		    DoneIfDoesntExist("$RCDIR/1.named.sh");
		    
		    if (!unlink "$RCDIR/1.named.sh") {
			PhaseFail("Unable to remove $RCDIR/1.named.sh: $!");
		    }
		};
	    }
	    # Restart sendmail so it picks up named changes.
	    Phase "sendmail", "Restarting sendmail", sub {
		ExecQuietFatal("/etc/rc.d/sendmail restart");
	    };
	}
	Phase "copying", "Copying resolv.conf over to ops", sub {
	    ExecQuietFatal("$SCP $TOP_OBJDIR/named/resolv.conf.ops ".
			   "     ${USERNODE}:/etc/resolv.conf");
	};
	Phase "copying2", "Copying resolv.conf over to fs", sub {
	    if ($USERNODE eq $FSNODE) {
		PhaseSkip("FS node is ops node");
	    }
	    if ($BOSSNODE eq $FSNODE) {
		PhaseSkip("FS node is boss node");
	    }
	    ExecQuietFatal("$SCP $TOP_OBJDIR/named/resolv.conf.ops ".
			   "     ${FSNODE}:/etc/resolv.conf");
	};
	if ($MAILERNODE eq "boss") {
	    Phase "accessfile", "Installing mail access file", sub {
		DoneIfExists("/etc/mail/access");
		ExecQuietFatal("$CP $TOP_OBJDIR/named/mail.access ".
			       "     /etc/mail/access");
	    };
	    Phase "accessdb", "Creating mail access db file", sub {
		ExecQuietFatal("/usr/sbin/makemap hash /etc/mail/access < ".
			       "    /etc/mail/access");
	    };
	}
	else {
	    Phase "copying3", "Copying mail access file over to ops", sub {
		ExecQuietFatal("$SCP $TOP_OBJDIR/named/mail.access ".
			       "     ${USERNODE}:/etc/mail/access");
		ExecQuietFatal("$SSH -o 'BatchMode=yes' root\@${USERNODE} ".
			       "     makemap hash /etc/mail/access \\< ".
			       "          /etc/mail/access");
	    };
	}
    };

    return 0;
}

# Local Variables:
# mode:perl
# End:
