#
# The template
#
use strict;
use libinstall;
use installvars;

sub Install($$$)
{
    my ($server, $isupdate, $impotent) = @_;

    # Replace if this script does an update for ip/domain.
    return 0
	if ($isupdate);

    Phase "ports", "Installing ports", sub {
	Phase "packages", "Installing packages", sub {
	    Phase "main", "Installing main package", sub {
		DoneIfPackageInstalled($BOSS_PORT, 1);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		AddPackage($BOSS_PORT, $packagedir);
	    };

	    # XXX haven't had dependency issues in a long time, skip these
	    if ($FBSD_MAJOR >= 10) {
		PhaseSkip("Packages installed");
	    }

	    if ($FBSD_MAJOR > 4) {
		Phase "php", "Installing php${PHP_VERSION} package", sub {
		    DoneIfPackageInstalled($PHP_PORT, 1);
		    if (!$packagedir) {
			PhaseSkip("No package directory provided");
		    }
		    # XXX ugh, make sure an older version is not installed
		    if ($PHP_VERSION > 4) {
			DelPackage("php4-", 0, 0);
		    }
		    AddPackage($PHP_PORT, $packagedir);
		};
		# XXX temporary: only needed til emulab-boss package updated
		Phase "tftpd", "Updating tftpd installation", sub {
		    my $pname = InstalledPackage("emulab-tftp-hpa-", 0);
		    if ($pname) {
			if ($pname eq $TFTPD_PKG) {
			    PhaseSkip("tftpd already up to date");
			}
			DelPackage($pname, 0, 1);
		    }
		    if (!$packagedir) {
			PhaseSkip("No package directory provided");
		    }
		    AddPackage($TFTPD_PKG, $packagedir);
		};
		# XXX Also temporary
		Phase "pubsub", "Installing pubsub package", sub {
		    my $pspkg = $ELVIN_COMPAT ? "pubsub-elvincompat" : "pubsub";
		    DoneIfPackageInstalled($pspkg, 0);
		    my $pname = GetPackage($pspkg, $packagedir);
		    AddPackage($pname, $packagedir);
		};
		# XXX Ditto
		if ($FBSD_MAJOR > 6 || ($FBSD_MAJOR == 6 && $FBSD_MINOR > 2)) {
		    if ($FBSD_MAJOR < 8) {
			Phase "p5-DBD-mysql50", "Installing old Perl Mysql package", sub {
			    my $pname = InstalledPackage("p5-DBD-mysql50-", 0);
			    if ($pname) {
				if ($pname eq $P5DBD_PKG) {
				    PhaseSkip("p5-DBD-mysql50 already up to date");
				}
				DelPackage($pname, 0, 1);
			    }
			    if (!$packagedir) {
				PhaseSkip("No package directory provided");
			    }
			    AddPackage($P5DBD_PKG, $packagedir);
			};
		    }
		    if ($FBSD_MAJOR > 6) {
			Phase "py-m2crypto", "Installing python m2crypto package", sub {
			    my $pname = InstalledPackage("py[0-9]+-m2crypto-", 0);
			    if ($pname) {
				if ($pname eq $PYM2_PKG) {
				    PhaseSkip("py-m2crypto already up to date");
				}
				DelPackage($pname, 0, 1);
			    }
			    if (!$packagedir) {
				PhaseSkip("No package directory provided");
			    }
			    AddPackage($PYM2_PKG, $packagedir);
			};

			#
			# XXX Remove conflicting bind package.
			#
			if ($FBSD_MAJOR < 10) {
			    Phase "bind9", "Removing conflicting bind package", sub {
				my $pname = InstalledPackage("bind9", 0);
				if (!$pname) {
				    PhaseSkip("bind9 not installed");
				}
				DelPackage("bind9", 1, 1);
			    };
			}
		    }
		    if ($NEEDMROUTED &&
			$FBSD_MAJOR > 7 || ($FBSD_MAJOR == 7 && $FBSD_MINOR > 2)) {
			Phase "mrouted", "Installing mrouted", sub {
			    DoneIfPackageInstalled("mrouted", 0);
			    my $pname = GetPackage("mrouted", $packagedir);
			    AddPackage($pname, $packagedir);
			};
		    }
		}
	    }
	    else {
		# XXX Also temporary
		Phase "pubsub", "Installing pubsub package", sub {
		    my $pspkg = $ELVIN_COMPAT ? "pubsub-elvincompat" : "pubsub";
		    DoneIfPackageInstalled($pspkg, 0);
		    my $pname = GetPackage($pspkg, $packagedir);
		    AddPackage($pname, $packagedir);
		}
	    }
	};
	if ($PGENISUPPORT) {
	    Phase "protogeni", "Installing protogeni packages", sub {
		if ($FBSD_MAJOR > 6 || ($FBSD_MAJOR == 6 && $FBSD_MINOR > 2)) {
		    DoneIfPackageInstalled("emulab-protogeni", 0);
		    my $pname = GetPackage("emulab-protogeni", $packagedir);
		    AddPackage($pname, $packagedir);
		} else {
		    PhaseSkip("OS install too old for Protogeni");
		}
	    };
	}
	if (ISFS($server)) {
	    Phase "fs-packages", "Installing FS packages", sub {
		DoneIfPackageInstalled($FS_PORT, 1);
		if (!$packagedir) {
		    PhaseSkip("No package directory provided");
		}
		AddPackage($FS_PORT, $packagedir);
	    };
	}
	PhaseSkip("Package directory provided; not installing from sources")
	    if ($packagedir);

	PhaseSkip("Not installing from sources in impotent mode")
	    if ($impotent);

	Phase "pcopy", "Copying ports into place", sub {
	    DoneIfExists("$PORTSMISCDIR/emulab-boss");
	    ExecQuietFatal("$SH $TOP_SRCDIR/ports/ports-install");
	};
	if ($FBSD_MAJOR == 4) {
	    # Ick. The php4 port is broken with SSL, so we have to patch
	    # it - hopefully it'll get fixed someday, and we remove this
	    Phase "php4patch", "Patching php4 port", sub {
		if (!ExecQuiet("$PATCH -C -f -l -R -p0 -i $PHP4_PATCH")) {
		    PhaseSkip("Patch already applied");
		}
		ExecQuietFatal("$PATCH -f -l -p0 -i $PHP4_PATCH");
	    };
	}
	if (ISFS($server)) {
	    Phase "fs-pinstall", "Installing FS ports (may take a while)", sub {
		DoneIfPackageInstalled($FS_PORT, 1);
		chdir "$PORTSMISCDIR/emulab-fs" or
		    PhaseFail "Unable to change to $PORTSMISCDIR/emulab-fs: $!";
		ExecQuietFatal("make -DBATCH install");
	    };
	}
	PhaseFail("Please install ports manually, since some\n of them are " .
		  "interactive. Run: \n" .
		  "    cd $PORTSMISCDIR/emulab-boss && make install\n" .
		  "    cd $PORTSMISCDIR/emulab-php${PHP_VERSION} && make install\n" .
		  ($PGENISUPPORT ?
		   "    cd $PORTSMISCDIR/emulab-protogeni && make install\n" : "") .
		  "then re-run this script.");
    };

    return 0;
}

# Local Variables:
# mode:perl
# End:
