#!/usr/bin/perl -w

#
# Copyright (c) 2000-2003 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#

use English;
use Getopt::Std;
use BSD::Resource;
use POSIX qw(:signal_h);
    
#
# Simply a wrapper for the parser. Passes on its arguments to parse.tcl,
# which spits out some stuff, which the caller reads back. Typically,
# this is via ssh from boss, since we want to run the parser on ops.
# The input to this script (on stdin) is the NS file supplied by the user,
# prepended with some hand crafted TCL code to set up the initial state
# of the parser (DB state the parser needs in order to run).
#
# The first argument option is the user to run this script as, since we
# gets invoked by a root ssh from boss. This option is not passed onto
# the parser of course!
#
# When run in "impotent" mode, there is no output, just an exit code. 
#
sub usage()
{
    print STDOUT
	"Usage: nse.parse.proxy -u user [args ...]\n".
        "Where options and arguments are those required by parse.tcl\n";
 
    exit(-1);
}

#
# Configure variables
#
my $TB       = "@prefix@";
my $TBOPS    = "@TBOPSEMAIL@";
my $TESTMODE = @TESTMODE@;
my $parser   = "$TB/lib/nseparse/nse.parse.tcl";

# Locals
my $tempdir  = "/tmp/parse-$$";
my $nsfile   = "$$.ns";
my $defsfile = "$$.defs.ns";

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin:/sbin:/usr/sbin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use libtestbed;

#
# First option has to be the -u option, the user to run this script as.
# In testmode, we are not run as root, so run as the current user, and
# in the current directory (not invoked with ssh in testmode). 
#
if (! $TESTMODE) {
    if ($UID != 0) {
	die("*** $0:\n".
	    "    Must be root to run this script!");
    }
    if ($ARGV[0] ne "-u") {
	die("*** $0:\n".
	    "    Improper first argument. Must be the user name!\n");
    }

    my $user = $ARGV[1];
    shift(@ARGV);
    shift(@ARGV);

    (undef,undef,$unix_uid) = getpwnam($user) or
	die("*** $0:\n".
	    "    No such user $user\n");

    #
    # Need the entire group list for the user, cause of subgroups, and
    # cause thats the correct thing to do. Too bad perl does not have a
    # getgrouplist function like the C library.
    #
    my $glist = `id -G $user`;
    if ($glist =~ /^([\d ]*)$/) {
	$glist = $1;
    }
    else {
	die("*** $0:\n".
	    "    Unexpected results from 'id -G $user': $glist\n");
    }

    # Need to split off the first group and create a proper list for $GUID.
    my @gglist   = split(" ", $glist);
    my $unix_gid = $gglist[0];
    $glist       = "$unix_gid $glist";

    # Flip to user and never go back!
    $GID            = $unix_gid;
    $EGID           = $glist;
    $EUID = $UID    = $unix_uid;
    $ENV{'USER'}    = $user;
    $ENV{'LOGNAME'} = $user;

    #
    # Create the tempdir and chmod it to keep people out.
    #
    if (! mkdir($tempdir, 0750)) {
	die("Could not mkdir $tempdir: $!\n");
    }
    if (! chmod(0750, $tempdir)) {
	die("Could not chmod $tempdir to 0750: $!\n");
    }
    if (! chdir($tempdir)) {
	die("Could not chdir to $tempdir: $!\n");
    }
}

#
# Take our input and write it to the temp file. 
#
open(TMPDEFS, ">$defsfile") ||
    fatal("Couldn't open $defsfile\n");
open(TMP, ">$nsfile") ||
    fatal("Couldn't open $nsfile\n");
my $WRITEFILE = TMPDEFS;
while (<STDIN>) {
    if ( /###### begin nsfile ######/ ) {
	 $WRITEFILE = TMP;
    }
    print $WRITEFILE $_;
}
close(TMPDEFS);
close(TMP);
chmod(0644, $defsfile);
chmod(0644, $nsfile);

#
# Fork a child process to run the parser in.
#
my $pid = fork();

if (!defined($pid)) {
    die("*** $0:\n".
	"    Could not fork a new process!");
}

#
# Child runs the parser, niced down, and then exits.
#
if (! $pid) {
    # Set the CPU limit for us.
    setrlimit(RLIMIT_CPU, 180, 180);
    
    # Give parent a chance to react.
    sleep(1);

    exec("nice -15 $parser @ARGV $defsfile $nsfile");
    die("Could not exec the nse parser!\n");
}

#
# Parent waits.
#
waitpid($pid, 0);
my $exit_status = $?;

#
# If the child was KILLed, then it overran its time limit.
# Send email. Otherwise, exit with result of child.
#
if (($exit_status & 0xff) == SIGKILL) {
    my $msg = "Parser Exceeded CPU Limit";

    SENDMAIL($TBOPS, "Nse parser Exceeded CPU Limit", $msg,
	     undef, undef, ($nsfile));
    
    print STDERR "$msg\n";
    if (-d $tempdir) {
	system("/bin/rm -r $tempdir");
    }
    exit(15);
}
if (-d $tempdir) {
    system("/bin/rm -r $tempdir");
}
exit($exit_status >> 8);

