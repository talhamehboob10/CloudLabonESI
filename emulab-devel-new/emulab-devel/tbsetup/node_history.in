#!/usr/bin/perl -w
#
# Copyright (c) 2005-2015, 2019 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;
use Date::Parse;
use Time::Local;

#
# Drill down the node history data in the DB
#

# un-taint path
$ENV{'PATH'} = '/bin:/usr/bin:/usr/local/bin:/usr/site/bin';
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1; 

# Load the Testbed support stuff.
use lib "@prefix@/lib";
use libdb;

use POSIX qw(ctime);

#
# Magic: Wed Jan 12 13:59:00 2005
# When Leigh implemented the history log.
#
my $epoch = 1105563540;

sub usage {
    print("Usage: $0 [-ARalrsvw] [node ...]\n".
    "  -A       print history of all nodes (you do NOT want to do this)\n".
    "  -T type  print history of all nodes of a given type\n".  
    "  -R       print raw records (default is to combine some records)\n".
    "  -S field sort list by field (date, node, pideid, uid, elapsed)\n".
    "  -a       show only when allocated to experiment\n".
    "  -l       list records\n".
    "  -n num   report on only the first num records (after sorting)\n".
    "             use negative value for last num records\n".
    "  -r       reverse order (most recent first)\n".
    "  -s       show a summary of node's past usage\n".
    "  -d date  Start at YYYY-MM-DD HH:MM:SS\n".
    "  -c       used with -d, show current status of all nodes\n".
    "  -v       verbose output\n".
    "  -w       print warnings about anomolous records\n");
    exit(1);
}

my $optlist = "AT:RS:aln:rswvd:i:ct:x:m:";
my $warnme = 0;
my $verbose = 0;
my $showall = 0;
my $showtype = "";
my $showcurrent = 0;
my $showalloconly = 0;
my $list = 0;
my $summary = 0;
my $raw = 0;
my $numrecs = 0;
my $datetime;
my $datelimit;
my $startrecord;
my $ip;
my $mac;
my @nodes = ();

#
# Sort stuff.  sortby value should correspond to record field format:
#
my %sortmap = (
    "node"    => 0,
    "pideid"  => 6,
    "uid"     => 3,
    "date"    => 1,
    "elapsed" => 2
);
my $sortbydate = $sortmap{date};
my $sortby = $sortbydate;
my $revorder = 0;

# Sort function: first by indicated field, secondarily by date
sub byfield()
{
    # already sorted by date
    return 0 if ($sortby == $sortbydate);

    # int compare for elapsed
    if ($sortby == $sortmap{elapsed}) {
	return @$a[$sortby] <=> @$b[$sortby] ||
	    @$a[$sortbydate] <=> @$b[$sortbydate];
    }

    # string compare for everything else
    return @$a[$sortby] cmp @$b[$sortby] ||
	@$a[$sortbydate] <=> @$b[$sortbydate];
}


#
# Parse command arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"A"})) {
    $showall = 1;
}
if (defined($options{"T"})) {
    $showtype = $options{"T"};
}
if (defined($options{"R"})) {
    $raw = 1;
}
if (defined($options{"S"})) {
    if (!exists($sortmap{$options{"S"}})) {
	print STDERR "invalid sort field '$options{S}'\n";
	usage();
    }
    $sortby = $sortmap{$options{"S"}};
}
if (defined($options{"a"})) {
    $showalloconly = 1;
}
if (defined($options{"l"})) {
    $list = 1;
}
if (defined($options{"n"})) {
    $numrecs = $options{"n"};
}
if (defined($options{"r"})) {
    $revorder = 1;
}
if (defined($options{"s"})) {
    $summary = 1;
}
if (defined($options{"d"})) {
    $datetime = $options{"d"};
    
    if ($datetime !~ /^\d+$/) {
	$datetime = timelocal(strptime($datetime));
    }
    $summary  = 0;
}
if (defined($options{"x"})) {
    $startrecord = $options{"x"};
    $summary  = 0;
}
if (defined($options{"t"})) {
    if (!defined($datetime)) {
	$datetime  = time() - $options{"t"};
    }
    else {
	$datelimit = $datetime;
	$datetime  = $datetime - $options{"t"};
    }
}
if (defined($options{"c"})) {
    $showcurrent = 1;
    usage()
	if (!defined($datetime));
}
if (defined($options{"i"})) {
    $ip = $options{"i"};
    $summary  = 0;
}
if (defined($options{"m"})) {
    $mac = $options{"m"};
    $mac =~ s/://g;
    $summary  = 0;
}
if (defined($options{"w"})) {
    $warnme = 1;
}
if (defined($options{"v"})) {
    $verbose = 1;
}
if (!($showall || $showtype) &&
    @ARGV == 0 && !(defined($ip) || defined($mac))) {
    usage();
}

if ($showtype) {
    my $query_result =
	DBQueryFatal("SELECT node_id FROM nodes where type='$showtype'");
    while (my %row = $query_result->fetchhash()) {
	push @nodes, $row{node_id};
    }
} else {
    @nodes = @ARGV
	if (! (defined($ip) || defined($mac)));
}

if (!$list && !$summary) {
    $list = 1;
}

my %nodeinfo;	# [ expt, starttime, uid ]
my @rawrecords;
my %startinfo;

#
# Common case: a single node.
# Just fetch data for it, otherwise fetch data for all.
# 
#
# If we have a datetime, then we want to start with records greater
# then the stamp, but we will not know the state at that point, since
# it will be the most recent record *before* the requested stamp, that
# says what the node is doing at the time. So, this query does some
# amazing magic to find those records without an explosion of terms
# that takes forever. Kudos to Gary for figuring out this query. 
#
if ($datetime) {
    my $querymod = "";
    
    if (@nodes) {
	$querymod = " AND (" .
	            join(" or ", map {"h1.node_id='$_'"} @nodes) . ")";
    }
    elsif (defined($ip)) {
	$querymod = " AND h1.cnet_ip='$ip'";
    }
    elsif (defined($mac)) {
	$querymod = " AND h1.cnet_mac='$mac'";
    }

    my $query_result =
	DBQueryFatal("SELECT h1.node_id,h1.op,h1.stamp,h1.uid,h1.exptidx,".
		     "       s.pid,s.eid,history_id ".
		     "    FROM node_history as h1 ".
		     "JOIN (SELECT h3.node_id,MAX(h3.stamp) as stamp ".
		     "         FROM node_history as h3 ".
		     "      WHERE stamp < $datetime ".
		     "      GROUP BY h3.node_id ".
		     "     ) AS h2 ON h1.node_id=h2.node_id AND ".
		     "                h1.stamp = h2.stamp ".
		     "left join nodes as n on n.node_id=h1.node_id ".
		     "left join node_types as t on t.type=n.type ".
		     "left join experiment_stats as s on s.exptidx=h1.exptidx ".
		     "where t.isplabdslice=0 and t.isplabphysnode=0 and ".
		     "      (n.role='testnode' or n.role='virtnode') ".
		     "$querymod order by h1.node_id");
    
    while (my %row = $query_result->fetchhash()) {
	my $pid = $row{pid};
	my $eid = $row{eid};
	my $pideid = "$pid/$eid";
	my $exptidx = $row{"exptidx"};
	my $node = $row{node_id};
	my $stamp = $row{stamp};
	my $uid = $row{uid};
	my $op = $row{op};
	my $history_id = $row{"history_id"};
	my $cstamp = ctime($stamp);
	my $diff = $datetime - $stamp;
	chomp($cstamp);

	if ($op eq "alloc" || $op eq "move" || $op eq "create") {
	    if ($showcurrent) {
		next
		    if ($pid eq TBOPSPID());

		if ($verbose) {
		    print "$node '$stamp' $diff $uid $pid $eid\n";
		}
		else {
		    print "$node REC $stamp $diff $uid $pid $eid ".
			"$exptidx $history_id\n";
		}
	    }
	    else {
		$nodeinfo{$node} =
		    [ $pideid, $exptidx, $stamp, $uid, $history_id ];
	    }
	}
	elsif ($op eq "free" || $op eq "destroy") {
	    if (!$showcurrent) {
		$nodeinfo{$node} = [ "", undef, $stamp, $uid, $history_id ];
	    }
	}
    }
}
exit(0)
    if ($showcurrent);

#
# If nodes are specified, get the node inception date to limit the
# initial record. This really only matters for summary stats.
#
my %nodestarts = ();
if (@nodes > 0) {
    my $nlist = "(" . join(" or ", map {"node_id='$_'"} @nodes) . ")";

    my $query_result =
	DBQueryFatal("SELECT node_id,UNIX_TIMESTAMP(inception) as tstamp ".
		     " FROM nodes WHERE $nlist");
    while (my %row = $query_result->fetchhash()) {
	my $node = $row{'node_id'};

	# use most recent of inception date...
	my $ts = $row{'tstamp'};
	# ...and specified date...
	if ($datetime && $ts < $datetime) {
	    $ts = $datetime;
	}
	# ...and epoch
	if ($ts < $epoch) {
	    $ts = $epoch;
	}
	$nodestarts{$node} = $ts;
    }
}

my $querymod = "";
if ($startrecord) {
    if ($revorder) {
	$querymod = " AND history_id<='$startrecord'";
    } else {
	$querymod = " AND history_id>='$startrecord'";
    }
}
if (@nodes) {
    $querymod .= " AND (" . join(" or ", map {"node_id='$_'"} @nodes) . ")";
}
elsif (defined($ip)) {
    $querymod .= " AND cnet_ip='$ip'";
}
elsif (defined($mac)) {
    $querymod .= " AND cnet_mac='$mac'";
}

if ($datetime) {
    $querymod .= " AND stamp>='$datetime'";
}
if ($datelimit) {
    $querymod .= " AND stamp<='$datelimit'";
}

# Bogus, but reading the entire table is nuts! Millions of rows. 
my $limitby = ($showall && $numrecs ? "limit " . $numrecs * 10 : "");

#
# If we are not getting all the records and we want the most recent
# then we have to reverse the initial DB sort and then reverse again
# using mysql sub-query fu.
#
my $query_result;
if ($revorder && $limitby) {
    my $orderby = " ORDER BY stamp DESC";
    $query_result =
	DBQueryFatal("SELECT * from (SELECT node_id,stamp,op,uid,pid,eid, ".
		     "  experiment_stats.exptidx,cnet_ip,phys_nodeid, ".
		     "  history_id ".
		     "FROM node_history,experiment_stats ".
		     "WHERE node_history.exptidx=experiment_stats.exptidx ".
		     "$querymod $orderby $limitby) SUB ORDER BY stamp ASC");
} else {
    my $orderby = " ORDER BY stamp ASC";
    $query_result =
	DBQueryFatal("SELECT node_id,stamp,op,uid,pid,eid, ".
		     "  experiment_stats.exptidx,cnet_ip,phys_nodeid, ".
		     "  history_id ".
		     "FROM node_history,experiment_stats ".
		     "WHERE node_history.exptidx=experiment_stats.exptidx ".
		     "$querymod $orderby $limitby");
}

my $lastid = 0;
my $laststamp = 0;
while (my %row = $query_result->fetchhash()) {
    my $pideid = "$row{pid}/$row{eid}";
    my $exptidx = $row{"exptidx"};
    my $node = $row{node_id};
    my $stamp = $row{stamp};
    my $uid = $row{uid};
    my $history_id = $row{"history_id"};

    if ($stamp < $laststamp) {
	print STDERR "$node ($pideid): timestamp decrease $laststamp (ID:$lastid) to $stamp (ID: $history_id)\n" if ($warnme);
    }
    $lastid = $history_id;
    $laststamp = $stamp;

    #
    # XXX if this is the first record for a node, consider it as free
    # from the epoch (or the low date specified) til now.
    #
    if (!exists($nodeinfo{$node})) {
	my $ts = $nodestarts{$node};
	$nodeinfo{$node} = [ "", undef, ($ts ? $ts : $epoch),
			     "root", $history_id ];
    }

    my ($opideid, $oidx, $ostamp, $ouid, $ohistory_id) = @{$nodeinfo{$node}};
    my $elapsed = $stamp - $ostamp;

    #
    # Allocating node to experiment.
    # Should currently be free.
    #
    if ($row{op} eq "alloc") {
	if ($opideid ne "") {
	    print STDERR "$node (ID: $history_id): dup alloc: already allocated to $opideid\n"
		if ($warnme);
	    # XXX possibly missing state in the DB, treat as move
	}
	$nodeinfo{$node} = [ $pideid, $exptidx, $stamp, $uid, $history_id ];
    }

    #
    # Free or move terminates a node's time in an experiment.
    #
    elsif ($row{op} eq "free") {
	if ($opideid ne $pideid) {
	    print STDERR "$node (ID: $history_id): mismatched alloc,free records: $opideid,$pideid\n"
		if ($warnme);
	}
	$nodeinfo{$node} = [ "", undef, $stamp, $uid, $history_id ];
    }
    elsif ($row{op} eq "move") {
	if (!$raw) {
	    # Moves from reloadpending to reloading are combined as reloading
	    if ($opideid eq "emulab-ops/reloadpending" &&
		$pideid eq "emulab-ops/reloading") {
		$nodeinfo{$node} = [ $pideid, $exptidx, $ostamp, $ouid,
				     $history_id ];
		next;
	    }
	}
	$nodeinfo{$node} = [ $pideid, $exptidx, $stamp, $uid, $history_id ];
    }
    elsif ($row{op} eq "create") {
	if ($opideid ne "") {
	    print STDERR "$node (ID: $history_id): dup alloc: already allocated to $opideid\n"
		if ($warnme);
	    # XXX possibly missing state in the DB.
	}
	$nodeinfo{$node} = [ $pideid, $exptidx, $stamp, $uid, $history_id ];
    }
    elsif ($row{op} eq "destroy") {
	if ($opideid ne $pideid) {
	    print STDERR "$node (ID: $history_id): mismatched create,destroy records: ".
		"$opideid,$pideid\n"
		if ($warnme);
	}
	$nodeinfo{$node} = [ "", undef, $stamp, $uid, $history_id ];
    }
    #print "R: $node, $opideid, $oidx, $ouid, $ostamp, $elapsed\n";
    
    # save off the record
    push(@rawrecords,
	 [ $node, $opideid, $oidx, $ouid, $ostamp, $elapsed, $ohistory_id ]);
}

if (! ($datelimit || $startrecord)) {
    # Include the current state of nodes in a final record
    my $stamp = time();
    for $node (sort(keys(%nodeinfo))) {
	my ($opideid, $oidx, $ostamp, $ouid, $ohistory_id)= @{$nodeinfo{$node}};
	my $elapsed = $stamp - $ostamp;
	push(@rawrecords,
	     [ $node, $opideid, $oidx, $ouid, $ostamp, $elapsed, $ohistory_id]);
    }
}

#
# Loop over the raw records, computing summary stats and creating
# a another set of records to print (if desired).
#
my @records = ();

for my $rec (@rawrecords) {
    my ($node, $pideid, $exptidx, $uid, $stamp, $elapsed, $history_id)= @{$rec};

    #print "RR: $node, $elapsed\n";

    if (!exists($nodestats{$node})) {
	$nodestats{$node} = [ 0, 0, 0, 0 ];
    }
    my ($ftime, $atime, $rtime, $dtime) = @{$nodestats{$node}};
    my $isalloced = 0;
    if ($pideid eq "") {
	$ftime += $elapsed;
    } elsif ($pideid eq "emulab-ops/reloadpending" ||
	     $pideid eq "emulab-ops/reloading") {
	$rtime += $elapsed;
    } elsif ($pideid eq "emulab-ops/hwdown") {
	$dtime += $elapsed;
    } else {
	$atime += $elapsed;
	$isalloced = 1;
    }
    $nodestats{$node} = [ $ftime, $atime, $rtime, $dtime ];

    if ($list) {
	my ($pid, $eid);
	if ($pideid eq "") {
	    $pid = $eid = "<FREE>";
	    $exptidx = 0;
	} else {
	    ($pid, $eid) = split("/", $pideid);
	}
	push(@records,
	     [$node, $stamp, $elapsed, $uid, $pid, $eid, $exptidx,
	      # For sort
	      "$pid/$eid", $history_id])
	    if (!$showalloconly || $isalloced);
    }
}

# Sort the list as desired
if ($sortby ne $sortbydate) {
    @records = sort byfield @records;
}
if ($revorder) {
    @records = reverse(@records);
}

# Prune to the proper number of entries (first/last $numrecs entries)
if ($numrecs && $numrecs < $#records) {
    if ($numrecs > 0) {
	@records = @records[0 .. $numrecs-1];
    } else {
	@records = @records[$numrecs .. -1 ];
    }
}

if (@records) {
    foreach my $record (@records) {
	my ($node, $stamp, $elapsed, $uid, $pid, $eid,
	    $exptidx, undef, $history_id) = @$record;

	if ($verbose) {
	    $stamp = ctime($stamp);
	    chomp($stamp);
	    print "$node '$stamp' $elapsed $uid $pid $eid\n";
	}
	else {
	    print "$node REC $stamp $elapsed $uid $pid $eid ".
		"$exptidx $history_id\n";
	}
    }
}

#
# Print out summary information
#
my ($t_ftime, $t_atime, $t_rtime, $t_dtime) = (0, 0, 0, 0);
for $node (sort keys(%nodestats)) {
    if ($summary) {
	my ($ftime, $atime, $rtime, $dtime) = @{$nodestats{$node}};
	my $ttime = $ftime + $atime + $rtime + $dtime;

	if ($verbose) {
	    my $pct;
	    print "$node SUMMARY\n";
	    if ($atime > 0) {
		$pct = ($atime * 100) / $ttime;
		printf("  Allocated: %9d sec (%5.1f%%)\n", $atime, $pct);
		$t_atime += $atime;
	    }
	    if ($ftime > 0) {
		$pct = ($ftime * 100) / $ttime;
		printf("  Free:      %9d sec (%5.1f%%)\n", $ftime, $pct);
		$t_ftime += $ftime;
	    }
	    if ($rtime > 0) {
		$pct = ($rtime * 100) / $ttime;
		printf("  Reloading: %9d sec (%5.1f%%)\n", $rtime, $pct);
		$t_rtime += $rtime;
	    }
	    if ($dtime > 0) {
		$pct = ($dtime * 100) / $ttime;
		printf("  Down:      %9d sec (%5.1f%%)\n", $dtime, $pct);
		$t_dtime += $dtime;
	    }
	} else {
	    print "$node SUM $atime $ftime $rtime $dtime\n";
	}
    }
}
if ($verbose) {
    my $ttime = $t_ftime + $t_atime + $t_rtime + $t_dtime;
    my $n = scalar(keys %nodestats);
    my $pct;

    print "TOTAL ($n nodes";
    if ($datelimit) {
	print " from ", scalar(localtime($datetime)),
	      " to ", scalar(localtime($datelimit));
    } elsif ($datetime) {
	print " since ", scalar(localtime($datetime));
    }
    print ")\n";
    if ($t_atime > 0) {
	$pct = ($t_atime * 100) / $ttime;
	printf("  Allocted:  %9d sec (%5.1f%%)\n", $t_atime, $pct);
    }
    if ($t_ftime > 0) {
	$pct = ($t_ftime * 100) / $ttime;
	printf("  Free:      %9d sec (%5.1f%%)\n", $t_ftime, $pct);
    }
    if ($t_rtime > 0) {
	$pct = ($t_rtime * 100) / $ttime;
	printf("  Reloading: %9d sec (%5.1f%%)\n", $t_rtime, $pct);
    }
    if ($t_dtime > 0) {
	$pct = ($t_dtime * 100) / $ttime;
	printf("  Down:      %9d sec (%5.1f%%)\n", $t_dtime, $pct);
    }
}

exit(0);
