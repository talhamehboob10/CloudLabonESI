#!/usr/bin/perl -w
#
# Copyright (c) 2004-2007 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;

# XXX should not be hardwired
my $ELABDOMAIN = "emulab.net";

#
# This script is the plab vserver equiv of ../common/rc.bootsetup. It runs
# inside the vserver and does a limited set of bootstrapping tasks.
#
sub usage()
{
    print "Usage: " . scriptname() . " boot|shutdown|reconfig|reset\n";
    exit(1);
}

my %options = ();
my $optlist = "";
my $action  = "boot";

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Script specific goo.
my $RCDIR	 = "$BINDIR/rc";
my $PLABCONFIG   = "$BOOTDIR/rc.plab";
my $SVCFILE      = "$BOOTDIR/svcslice";
my $IPADDRFILE   = "$BOOTDIR/myip";
my $PWDFILE      = "/etc/passwd";
my $TMPLPWDFILE  = "/etc/passwd.tmpl";

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself.
#
# Plab setup slightly different than Jail setup. libsetup already knows
# our vnodeid, and has configured the tmcc library with that info as well.
# 
use libsetup;
use libtmcc;
use librc;

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();
sub doplabconfig();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

#
# Boot Action.
#
sub doboot()
{
    print("Checking Emulab reservation status\n");
    my ($pid, $eid, $vname) = plabsetup();

    print("Checking Testbed plab configuration\n");
    doplabconfig();

    #
    # Created by doplabconfig().
    #
    if (-x $PLABCONFIG) {
	print("Configuring Planetlab vserver\n");
	system("$PLABCONFIG");
	if ($?) {
	    fatal("Error running $PLABCONFIG");
	}
    }

    if (-x "$BINDIR/watchdog") {
        print("Starting testbed update watchdog\n");
	system("$BINDIR/watchdog start");
	if ($?) {
	    fatal("Error running $BINDIR/watchdog");
	}
    }

    #
    # Fire up event server and evproxy if we are the service sliver
    # on this node
    #
    if (-e $SVCFILE and -x "$BINDIR/runevents") {
        print("Starting event server and evproxy");
        system("$BINDIR/runevents");
        if ($?) {
            fatal("Error starting up event server and/or evproxy!");
        }
    }

    #
    # At this point, if we are a free node just return. Something went wacky.
    #
    if (!defined($pid)) {
	return;
    }

    #
    # This is where we run all of the config scripts. These talk to the
    # DB and setup the node the way it is supposed to be. 
    # 
    print("Running config scripts\n");
    system("$RCDIR/rc.config boot");
    if ($?) {
	fatal("Error running $RCDIR/rc.config");
    }

    #
    # Create the /local/logs directory if necessary
    #
    if (! -e "/local/logs") {
        my @tmccresults = ();
        if (tmcc(TMCCCMD_CREATOR, undef, \@tmccresults) < 0) {
            fatal("Unable to lookup experiment creator!\n");
        }
        my ($creator) = $tmccresults[0] =~ /CREATOR=([-\w]+)/
            or fatal("Can't parse out creator!");
        my @pwent = getpwnam($creator)
            or fatal("No password entry for experiment creator: $creator!");
        mkdir("/local", 0775);
        chown($pwent[2],$pwent[3],"/local");
        mkdir("/local/logs", 0775);
        chown($pwent[2],$pwent[3],"/local/logs");
    }

    print("Informing Emulab Control that we are up and running\n");
    if (tmcc(TMCCCMD_STATE, "ISUP") < 0) {
	fatal("Error sending ISUP to Emulab Control!");
    }

    #
    # After everything is setup, run any startup command.
    #
    # Note that this mechanism is legacy, and will be replaced
    # by the program agent once the event system becomes available
    # on plab vnodes.
    #
    if (-x "$RCDIR/rc.startcmd") {
        TBDebugTimeStamp("running $RCDIR/rc.startcmd");
        system("$RCDIR/rc.startcmd boot");
        if ($?) {
            fatal("Error running $RCDIR/rc.startcmd");
        }
    }
}

#
# Shutdown Action.
#
sub doshutdown()
{
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
}

#
# Create configuration script. Currently sets up sshd and the DNS resolver. 
# 
sub doplabconfig()
{
    my $plabconfig;
    my @tmccresults;

    # get the slice user from the passwd file, fix it, and replace file
    # XXX: need to actually use the slice name, but we don't really have
    #      it stored anywhere yet.
    print "Fixing up the passwd file.\n";
    my $slicename = `cat /etc/slicename`;
    chomp($slicename);
    my $sliceuserent = "";
    open(PWD, "< $PWDFILE") or
        fatal("Can't open password file for reading!");
    while (my $pwln = <PWD>) {
        chomp($pwln);
        if ($pwln =~ /^$slicename/) {
            my @pwdfields = split /:/, $pwln;
            $pwdfields[1] = "*";
            $sliceuserent = join(":", @pwdfields);
            last;
        }
    }
    close(PWD);
    if (!$sliceuserent) {
        fatal("Couldn't find slice user entry in passwd file!");
    }
    !system("cp -f $TMPLPWDFILE $PWDFILE.new") or
        fatal("Can't copy template file!");
    open(PWNEW, ">> $PWDFILE.new") or
        fatal("Can't open new password file for append!");
    print PWNEW $sliceuserent . "\n";
    close(PWNEW);
    if (! -e "$PWDFILE.orig") {
        !system("cp -f $PWDFILE $PWDFILE.orig") or
            fatal("Can't backup passwd file!");
    }
    !system("mv -f $PWDFILE.new $PWDFILE") or
        fatal("Could not move new password file into place!");

    #
    # Grab plab specific config data and process it.
    #
    if (tmcc(TMCCCMD_PLABCONFIG, undef, \@tmccresults) < 0) {
	fatal("Could not get plabconfig from server!");
    }
    return 0
	if (! @tmccresults);
    $plabconfig = $tmccresults[0];

    # plabconfig defaults
    my $sshdport = 0;
    my $svcslice = 0;
    my $ipaddr = "0.0.0.0";

    # parse the config line
    PLCONFPARSE: for ($plabconfig) {
        /SSHDPORT=(\d+)/ && do {
            $sshdport = $1;
        };

        /SVCSLICE=(\d)/ && do {
            $svcslice = $1;
        };

        /IPADDR=([\d\.]+)/ && do {
            $ipaddr = $1;
        };
    }

    # Create the svcslice file if this is the service sliver.
    if ($svcslice) {
        open(SVCFILE, ">$SVCFILE")
            or fatal("Could not open $SVCFILE: $!");
        print SVCFILE "$svcslice\n";
        close(SVCFILE);
    }

    # Create the control_ipaddr file
    open(IPADDR, ">$IPADDRFILE")
        or fatal("Could not open $IPADDRFILE: $!");
    print IPADDR "$ipaddr\n";
    close(IPADDR);

    open(RC, ">$PLABCONFIG")
	or fatal("Could not open $PLABCONFIG: $!");

    if ($sshdport) {
	print RC "#!/bin/sh\n";

	# Note that it's important to never directly modify the config
	# file unless it's already been recreated due to vserver's
	# immutable-except-delete flag
	print(RC
	      "function setconfigopt()\n".
	      "{\n".
	      "    file=\$1\n".
	      "    opt=\$2\n".
	      "    value=\$3\n".
	      "    if ( ! grep -q \"^\$opt[ \t]*\$value\\\$\" \$file ); then\n".
	      "        sed -e \"s/^\\(\$opt[ \t]*.*\\)/#\\1/\" < \$file".
	      " > \$file.tmp\n".
	      "        mv -f \$file.tmp \$file\n".
	      "        echo \$opt \$value >> \$file;\n".
	      "    fi\n".
	      "}\n\n");

	# Make it look like it's in Emulab domain
	print RC "setconfigopt /etc/resolv.conf domain $ELABDOMAIN\n";
	print RC "setconfigopt /etc/resolv.conf search $ELABDOMAIN\n\n";

	# XXX make sure we can resolve our own name
	print RC "if ! ping -c 1 `hostname` >/dev/null 2>&1; then\n".
	         "    myip=`cat /var/emulab/boot/myip`\n".
	         "    myhn=`hostname`\n".
	         "    if [ -n \"\$myip\" -a -n \"\$myhn\" ]; then\n".
		 "        myal=\"\"\n".
	         "        mynn=`cat /var/emulab/boot/nickname`\n".
	         "        if [ -n \"\$mynn\" ]; then\n".
		 "            myal=\"\$mynn.$ELABDOMAIN\"\n".
	         "        fi\n".
	         "        setconfigopt /etc/hosts \$myip \"\$myhn \$myal\"\n".
	         "    fi\n".
		 "fi\n";

	# No SSH X11 Forwarding
	print RC "setconfigopt /etc/ssh/sshd_config X11Forwarding no\n";

	# Set SSH port
	print RC "setconfigopt /etc/ssh/sshd_config Port $sshdport\n";

        # Start syslogd
        print RC "touch /var/log/messages\n";
        print RC "touch /var/log/maillog\n";
        print RC "touch /var/log/secure\n";
        print RC "chmod 600 /var/log/secure\n";
        print RC "touch /var/log/cron\n";
        print RC "/sbin/syslogd\n";

	# Start sshd
        print RC "/usr/sbin/useradd -u 22 -g 22 -d /var/empty ".
                 "-c \"sshd separation account\" -s /nonexistent sshd\n";
	print RC "/etc/init.d/sshd restart\n";
    }
    else {
	fatal("Bad plab config line: $plabconfig");
    }

    close(RC);
    chmod(0755, $PLABCONFIG);
    return 0;
}
