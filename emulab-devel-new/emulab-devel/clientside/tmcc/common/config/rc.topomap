#!/usr/bin/perl -w
#
# Copyright (c) 2004-2018 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;

sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:";
my $action  = "boot";
my $vnodeid;

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

my $LTGZIPONLY_FILE = "$ETCDIR/ltmap-gzip";

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use libtmcc;
use librc;

# Only root.
# XXX on Windows we let non-root run this since linktest does not run as root.
if ($EUID != 0 && !WINDOWS()) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

#
# Not all clients support this.
#
exit(0)
    if (MFS() || (REMOTE() && !(REMOTEDED() || PLAB() || JAILED())));

# Protos.
sub doboot($);
sub doshutdown();
sub doreconfig();
sub docleanup();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot(0);
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

# More protos
sub dotopomap();

#
# Boot Action.
#
sub doboot($)
{
    my ($allowjails) = @_;

    # Handled elsewhere for other node types.
    dotopomap()
	if (!PLAB() && ($allowjails || !JAILED()) && !IXP());
}

#
# Shutdown Action.
#
sub doshutdown()
{
    # Nothing to do
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    doshutdown();
    return doboot(1);
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
    unlink TMTOPOMAP();
}

#
# Get topomap and assorted linktest files.
# Right now we first try to fetch across NFS.  Failing that we use tmcc.
# Eventually we should get this via http or frisbee.
#
# Failure to get the topomap is considered fatal, not so with linktest files.
#
sub dotopomap()
{
    #
    # Copy over the topomap so that it is locally resident.
    #
    my $datadir = EXPDIR() . "/tbdata";
    my $topomap = "$datadir/topomap.gz";
    my $ltmap = "$datadir/ltmap.gz";
    my $ltpmap = "$datadir/ltpmap.gz";
    my $tempmap = "/tmp/topomap.gz";
    my $templt = "/tmp/ltmap.gz";
    my $templtp = "/tmp/ltpmap.gz";

    #
    # Make sure we always try to fetch it fresh
    #
    unlink("$tempmap");
    unlink("$templt");
    unlink("$templtp");
    
    if (! (USESFS() || WINDOWS() || REMOTEDED() || (FSRVTYPE() eq "LOCAL"))) {
	if (!copyfilefromnfs($topomap, $tempmap, 0)) {
	    warning("NFS cp of $topomap failed, will try tmcc ...");
	    unlink("$tempmap");
	}
	if (!copyfilefromnfs($ltmap, $templt, 0)) {
	    warning("NFS cp of $ltmap failed, will try tmcc ...");
	    unlink("$templt");
	}
	if (!copyfilefromnfs($ltpmap, $templtp, 0)) {
	    warning("NFS cp of $ltpmap failed, will try tmcc ...");
	    unlink("$templtp");
	}
    }
    my $optarg = (defined($vnodeid) ? "-n $vnodeid" : "");
    my $idarg  = (exists($ENV{'IDKEY'}) ? "IDKEY=" . $ENV{'IDKEY'} : "");
    
    if (! -e $tempmap) {
	#
	# Try using tmcc. It returns compressed data so do it directly.
	# XXX note that tmcc does not return a meaningful exit value.
	#
	system(TMCCBIN() . " $optarg $idarg topomap > $tempmap");
    }

    if (! -e $templt) {
	#
	# Try using tmcc. It returns compressed data so do it directly.
	# XXX note that tmcc does not return a meaningful exit value.
	#
	system(TMCCBIN() . " $optarg $idarg ltmap > $templt");
    }

    if (! -e $templtp) {
	#
	# Try using tmcc. It returns compressed data so do it directly.
	# XXX note that tmcc does not return a meaningful exit value.
	#
	system(TMCCBIN() . " $optarg $idarg ltpmap > $templtp");
    }

    if (-s $tempmap) {
	system("cat ${tempmap} | gunzip > " . TMTOPOMAP());
	if ($?) {
	    fatal("Could not unzip topomap");
	}
    }
    else {
	#
	# We make this fatal since route setup depends on this file.
	# We could let it go, in which case route setup would non-fatally
	# fail, but then we also wouldn't run linktest (since the topomap
	# is missing) so we might not detect that route setup failed.
	#
	fatal("Could not get topomap");
    }

    if (-s $templt) {
	if (-e $LTGZIPONLY_FILE) {
	    system("mv ${templt} " . TMLTMAPGZ());
	    if ($?) {
		fatal("Could not mv ${templt} to " . TMLTMAPGZ());
	    }
	}
	else {
	    system("cat ${templt} | gunzip > " . TMLTMAP());
	    if ($?) {
		fatal("Could not unzip ltmap");
	    }
	}
    }
    else {
	warning("Could not get ltmap");
    }

    if (-s $templtp) {
	if (-e $LTGZIPONLY_FILE) {
	    system("mv ${templtp} " . TMLTPMAPGZ());
	    if ($?) {
		fatal("Could not mv ${templtp} to " . TMLTPMAPGZ());
	    }
	}
	else {
	    system("cat ${templtp} | gunzip > " . TMLTPMAP());
	    if ($?) {
		fatal("Could not unzip ltpmap");
	    }
	}
    }
    else {
	warning("Could not get ltpmap");
    }

    unlink("$tempmap");
    unlink("$templt");
    unlink("$templtp");
}
