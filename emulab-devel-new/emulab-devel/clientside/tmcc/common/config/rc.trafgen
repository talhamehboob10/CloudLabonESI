#!/usr/bin/perl -w
#
# Copyright (c) 2004-2011 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;
use POSIX ":sys_wait_h";
use POSIX qw(setsid);

sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:";
my $action  = "boot";
my $vnodeid;

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Script specific goo.
my %trafgenpids = ();

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use libtmcc;
use librc;
use libtestbed qw(TBBackGround);

#
# Not all clients support this.
#
exit(0)
    if (MFS() || (REMOTE() && !(REMOTEDED() || FAKEJAILED() || JAILED())));

# Now safe to do this since MFS does not have full perl install.
require Socket;
import Socket;

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Local goo
# After libsetup include; trafgens run outside in fake vnode setup.
my $PIDFILE  = (defined($vnodeid) ? CONFDIR() : "/var/run") . "/trafgen.pid";
my $LOGFILE  = (defined($vnodeid) ? CONFDIR() : $LOGDIR)    . "/trafgen.debug";

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

# Signal handler to initiate cleanup in parent and the children.
sub Pcleanup($)
{
    my ($signame) = @_;

    $SIG{TERM} = 'IGNORE';
    $SIG{INT}  = 'IGNORE';
    $SIG{HUP}  = 'IGNORE';

    foreach my $pid (values(%trafgenpids)) {
	system("kill $pid");
	waitpid($pid, 0);
    }

    unlink $PIDFILE;
    exit(0);
}

#
# Boot Action.
#
sub doboot()
{
    my @trafgens;
    my $errors = 0;

    print STDOUT "Checking Testbed trafgen configuration ... \n";

    if (gettrafgenconfig(\@trafgens)) {
	fatal("Could not get trafgen configuration from libsetup!");
    }

    return
	if (!@trafgens);

    my ($pid, $eid, $vname) = check_nickname();

    #
    # Now daemonize and let the boot continue. We have to disconnect and
    # change our session id so that we do not killed off (nor the trafgen
    # children) when boot script terminates. This cause the trafgen does not
    # properly disconnect and it gets killed once /etc/rc exits. Not sure
    # exactly why this is, but this is how I deal with it! The downside is
    # that we cannot report errors to the caller. We have to catch them
    # later, somehow.
    #
    if (TBBackGround($LOGFILE)) {
	sleep(2);
	exit(0);
    }
    # Fully disconnect from bootup. 
    setsid();

    #
    # Write our pid to a file so we can be killed later.
    # 
    system("echo '$PID' > $PIDFILE") == 0
	or fatal("Could not create $PIDFILE!");
    
    $SIG{TERM} = \&Pcleanup;
    $SIG{INT}  = \&Pcleanup;
    $SIG{HUP}  = \&Pcleanup;
    
    my $evserver = getlocalevserver();
    my $evport   = "";

    if (SHADOW()) {
	my $boss = $ENV{'BOSSNAME'};
	if (!defined($boss)) {
	    fatal("BOSSNAME is not set in the environment");
	}
	my ($domain) = ($boss =~ /^[^\.]+\.(.*)$/);
	$evserver = "event-server.${domain}";

	#
	# See if we need to connect to the compatability gateway.
	# Version 0, no elvin compat.
	#
	if (exists($ENV{'V0_COMPAT'})) {
	    $evport = "-p " . $ENV{'V0_COMPAT'};
	}
    }
    # We connect to the local elvind and talk to the master via the proxy.
    my $cmdline = "$BINDIR/trafgen -s $evserver $evport";
    if ($pid) {
	$cmdline .= " -E $pid/$eid -k " . TMEVENTKEY();
    }

    foreach my $trafgen (@trafgens) {
	my $name      = $trafgen->{"NAME"};
	my $ownaddr   = $trafgen->{"SRCHOST"};
	my $ownport   = $trafgen->{"SRCPORT"};
	my $peeraddr  = $trafgen->{"PEERHOST"};
	my $peerport  = $trafgen->{"PEERPORT"};
	my $proto     = $trafgen->{"PROTO"};
	my $role      = $trafgen->{"ROLE"};
	my $generator = $trafgen->{"GENERATOR"};
	my $target;
	my $source;

	# Skip if not specified as a TG generator. NSE handled elsewhere.
	next
	    if ($generator eq "NSE");

	if ($role eq "sink") {
	    $target = "$ownaddr.$ownport";
	    $source = "$peeraddr.$peerport";
	}
	else {
	    $target = "$peeraddr.$peerport";
	    $source = "$ownaddr.$ownport";
	}

	#
	# Fork a child to run the trafgen. The parent saves up all the
	# pids so that we can react to a shutdown later. We do this cause
	# the trafgen is 3rd party and we do not have very good control
	# over its output, and we want to avoid a shell wrapper around it
	# by using a redirect. 
	#
	my $childpid = fork();
	if ($childpid) {
	    #
	    # Parent. Save pid and keep going.
	    #
	    $trafgenpids{"$name"} = $childpid;
	    sleep(1);
	}
	else {
	    $SIG{TERM} = 'DEFAULT';
	    $SIG{INT}  = 'IGNORE';
	    $SIG{HUP}  = 'DEFAULT';

	    # Redirect stdout/stderr to log file.
	    open(STDOUT, ">>$LOGDIR/${name}-${pid}-${eid}.debug")
		or fatal("Could not redirect STDOUT");
	    open(STDERR, ">&STDOUT")
		or fatal("Could not redirect STDERR");

	    exec("$cmdline -N $name -S $source -T $target -P $proto -R $role");
	    die("*** $0:\n".
		"    Could not exec $cmdline -N $name ...\n");
	}
    }

    #
    # Loop, waiting for children to exit. They are not supposed to, so if
    # they do its an error. Log that to the console in the hope that someone
    # is looking!
    #
    while ((my $childpid = waitpid(-1, 0)) != 0) {
	foreach my $name (keys(%trafgenpids)) {
	    if ($trafgenpids{$name} == $childpid) {
		logit("trafgen", "Trafgen $name has exited with status $?");
		delete($trafgenpids{$name});
		last;
	    }
	}
	$errors++;
	# No more running, might as well stop now. 
	last
	    if (! scalar(keys(%trafgenpids)));
	sleep(1);
    }
    logit("trafgen", "All trafgens have exited!");
    exit($errors);
}

#
# Shutdown Action.
#
sub doshutdown()
{
    # Shutdown trafgen before doing reconfig.
    if (-e $PIDFILE) {
	system("kill `cat $PIDFILE`");
	sleep(1);
	# Wait for it to die.
	while (-e $PIDFILE) {
	    print "Waiting for $PIDFILE to die ...\n";
	    sleep(5);
	}
    }
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    doshutdown();
    return doboot();
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
    unlink $PIDFILE, $LOGFILE;
}
