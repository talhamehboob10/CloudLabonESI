#!/usr/bin/perl -w
#
# Copyright (c) 2004-2013 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use Getopt::Std;

sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:";
my $action  = "boot";

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Script specific goo
my $RPMINSTALL  = "/usr/local/bin/install-rpm %s %s";
my $RPMDBFILE   = "$DBDIR/testbed.rpms";

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use libtmcc;
use librc;

#
# Not all clients support this.
#
exit(0)
    if (MFS() || (REMOTE() && !(REMOTEDED() || JAILED() || PLAB())));

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    my $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

#
# Boot Action.
#
sub doboot()
{
    my @rpms   = ();
    my $errors = 0;
   
    print STDOUT "Checking Testbed RPM configuration ... \n";

    if (tmcc(TMCCCMD_RPM, undef, \@rpms) < 0) {
	fatal("Could not get rpm list from server!");
    }
    return
	if (! @rpms);

    #
    # Option to install-rpm: Locally, always use the copy (-c) option
    # to get the file via NFS, but avoid the stupid changing-exports-file
    # server race (install-rpm knows how to deal with said race).
    # Remote nodes get the file via the network (-t) option.
    #
    # Note: Windows also uses the -t option since root does not have access
    # to NFS mounted FSes.
    #
    my $MAXCPSIZE = (256 * 1024 * 1024);

    foreach my $rpm (@rpms) {
	my $useweb = (REMOTE() || SHADOW() || WINDOWS()) ? 1 : 0;

	if ($rpm =~ /SERVER=(\S+)\s+RPM=(.+)/ ||
	    $rpm =~ /SERVER=(\S+)\s+USEWEB=(\d)\s+RPM=(.+)/) {
	    my $server = $1;
	    my $rpm;
	    if (defined($3)) {
		$rpm = $3;
		$useweb = $2;
	    }
	    else {
		$rpm = $2;
	    }
	    my $installoption = $useweb ? "-t" : "-c";

	    #
	    # We also use -t if the file is large. This way we avoid
	    # thrashing the NFS server's link for an extended period
	    # (in particular, if we are using UDP-based NFS) and also
	    # avoids the stupid server race mentioned above. But we only
	    # do this if SERVER=user, since otherwise boss will have to
	    # get the file across NFS from the server before sending it
	    # to us via the web, which doesn't save any wear and tear
	    # on the NFS server link.
	    #
	    # Why "user" instead of "fs"? Because in a configuration
	    # where the fs node is seperate from the user node, we don't
	    # currently install a web server on fs.
	    # 
	    if (!$useweb && $server && $server =~ /^users\./ &&
		-s "$rpm" > $MAXCPSIZE) {
		print STDOUT "NOTICE: downloading large RPM via https\n";
		$installoption = "-t";
	    }
	    if ($server && $installoption eq "-t") {
		$installoption .= " -S $server";
	    }
	    my $rpmline = sprintf($RPMINSTALL, $installoption, $rpm);
		    
	    $server = "www" if (!$server);
	    print STDOUT "$server:$rpm: Installing RPM\n";
	    my $stamp = time();
	    if (system($rpmline)) {
		if ($? >> 8 == 255) {
		    $errors++;
		}
	    }
	    print STDOUT "$server:$rpm: finished in ", time() - $stamp, " seconds\n";
	}
	else {
	    warning("Bad RPM line: $rpm\n");
	}
    }
    exit($errors);
}

#
# Shutdown Action.
#
sub doshutdown()
{
    # Nothing to do.
    return;
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    # Same as booting
    return doboot();
}

#
# Node cleanup action (node is reset to clean state, as if just allocated).
#
sub docleanup()
{
    # Remove DB file. No attempt is made to uninstall though.
    if (-e "$RPMDBFILE") {
	unlink "$RPMDBFILE";
    }
}
