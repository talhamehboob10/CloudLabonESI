<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<s:HGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  verticalAlign="middle"
		  width="100%"
		  gap="2"
		  creationComplete="init()"
		  remove="destroy()">
	<fx:Script>
		<![CDATA[
			import com.flack.geni.display.areas.SliceArea;
			import com.flack.geni.resources.virt.AggregateSliver;
			import com.flack.geni.resources.virt.Slice;
			import com.flack.geni.resources.virt.Sliver;
			import com.flack.shared.FlackEvent;
			import com.flack.shared.SharedMain;
			
			import mx.core.FlexGlobals;
			
			import spark.components.CheckBox;
			import spark.components.RadioButtonGroup;
			
			[Bindable]
			public var slice:Slice;

			[Bindable]
			public var showCheckbox:CheckBox;
			
			[Bindable]
			public var radioGroup:RadioButtonGroup;
			
			public function init():void
			{
				SharedMain.sharedDispatcher.addEventListener(FlackEvent.CHANGED_SLICE, tryUpdate);
				update();
			}
			
			public function destroy():void
			{
				SharedMain.sharedDispatcher.removeEventListener(FlackEvent.CHANGED_SLICE, tryUpdate);
			}
			
			public function tryUpdate(e:FlackEvent):void
			{
				if(e.changedObject == slice)
					update();
			}
			
			public function update():void
			{
				switch(slice.AllocationState)
				{
					case Sliver.ALLOCATION_PROVISIONED:
						switch(slice.OperationalState)
						{
							case Sliver.OPERATIONAL_READY:
								sliceSelect.styleName = "validStyle";
								break;
							case Sliver.OPERATIONAL_FAILED:
								sliceSelect.styleName = "failedStyle";
								break;
							case Sliver.MIXED_CHANGING:
							case Sliver.OPERATIONAL_STOPPING:
							case Sliver.OPERATIONAL_READY_BUSY:
							case Sliver.OPERATIONAL_CONFIGURING:
								sliceSelect.styleName = "inprogressStyle";
								break;
							case Sliver.MIXED_FINISHED:
							case Sliver.OPERATIONAL_NOTREADY:
								sliceSelect.styleName = "pausedStyle";
								break;
							default:
								sliceSelect.styleName = "unknownStyle";
						}
						break;
					case Sliver.ALLOCATION_ALLOCATED:
					case Sliver.ALLOCATION_UPDATING:
					case Sliver.MIXED_ALLOCATED:
					case Sliver.MIXED_PROVISIONED:
					case Sliver.MIXED_UNALLOCATED:
					case Sliver.MIXED_UPDATING:
						sliceSelect.styleName = "inprogressStyle";
						break;
					default:
						sliceSelect.styleName = "unknownStyle";
				}
				
				toolTip = Sliver.describeState(slice.AllocationState, slice.OperationalState);
			}
			
			public function viewSlice():void
			{
				//DisplayUtil.viewSlice(slice);
				
				var sliceArea:SliceArea = new SliceArea();
				FlexGlobals.topLevelApplication.viewContent(sliceArea);
				sliceArea.validateDisplayList();
				sliceArea.slice = slice;
			}

		]]>
	</fx:Script>
	<s:RadioButton enabled="{showCheckbox.selected}"
				   id="sliceSelect"
				   toolTip="View only this slice on the map"
				   group="{radioGroup}"
				   value="{slice}"
				   fontSize="10" />
	<s:Button id="sliceButton"
			   height="24"
			   width="100%"
			   label="{slice.Name}"
			   click="viewSlice()" />
</s:HGroup>
