<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   width="100%" height="100%"
				   borderStyle="solid"
				   styleName="normalCanvasStyle"
				   implements="com.flack.geni.display.slicer.IComponentsArea"
				   dragEnter="bordercontainer1_dragEnterHandler(event)"
				   dragDrop="bordercontainer1_dragDropHandler(event)"
				   mouseMove="bordercontainer1_mouseMoveHandler(event)"
				   mouseDown="bordercontainer1_mouseDownHandler(event)"
				   mouseUp="bordercontainer1_mouseUpHandler(event)"
				   resize="bordercontainer1_resizeHandler(event)">
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		
		.normalCanvasStyle {
			borderColor: #CDC2C2;
			backgroundColor: #F6F6F6;
		}
		
		.inprogressCanvasStyle {
			borderColor: #B2B300;
			backgroundColor: #FEFF80;
		}
		
		.failedCanvasStyle {
			borderColor: red;
			backgroundColor: #FFD9D9;
		}
		
		.pausedCanvasStyle {
			borderColor: FF6600;
			backgroundColor: #FEB380;
		}
		
		.validCanvasStyle {
			borderColor: green;
			backgroundColor: #CCFFCC;
		}
		
	</fx:Style>
	<fx:Script>
		<![CDATA[
			import com.flack.geni.display.areas.SliceArea;
			import com.flack.geni.resources.SliverTypes;
			import com.flack.geni.resources.physical.PhysicalNode;
			import com.flack.geni.resources.sites.GeniManager;
			import com.flack.geni.resources.virt.AggregateSliver;
			import com.flack.geni.resources.virt.Slice;
			import com.flack.geni.resources.virt.Sliver;
			import com.flack.geni.resources.virt.VirtualComponent;
			import com.flack.geni.resources.virt.VirtualLink;
			import com.flack.geni.resources.virt.VirtualNode;
			import com.flack.geni.resources.virt.VirtualNodeCollection;
			import com.flack.shared.FlackEvent;
			import com.flack.shared.SharedMain;
			import com.flack.shared.utils.ColorUtil;
			
			import flash.filters.DropShadowFilter;
			import flash.filters.GlowFilter;
			
			import mx.controls.Alert;
			import mx.core.UIComponent;
			import mx.events.DragEvent;
			import mx.events.ResizeEvent;
			import mx.managers.DragManager;
			
			import spark.components.List;
			
			public var host:SliceArea;
			
			private var editable:Boolean = true;
			
			[Bindable]
			private var slice:Slice;
			public function get SliceEditing():Slice
			{
				return slice;
			}
			public function set SliceEditing(s:Slice):void
			{
				slice = s;
			}
			
			public var allNodes:CanvasNodeCollection = new CanvasNodeCollection();
			public var allLinks:CanvasLinkCollection = new CanvasLinkCollection();
			
			/**
			 * Loads a slice into the canvas
			 */
			public function load(newSlice:Slice):void
			{
				clear();
				slice = newSlice;
				
				updateCanvas();
			}
			
			/**
			 * Remove all of the resources except the slice
			 */
			public function clear():void
			{
				removeAllElements();
				allNodes = new CanvasNodeCollection();
				allLinks = new CanvasLinkCollection();
				styleName = "normalCanvasStyle";
				selectedItem = null;
				draggingItem = null;
				drawingLink = false;
				dropItem = null;
				previewLink = null;
				startItem = null;
				endItem = null;
				hoveredOverItem = null;
			}
			
			private function updateCanvas(positionOnly:Boolean = false):void
			{
				var i:int;
				// Remove stuff which doesn't exist at all anymore
				// Make sure existing point to the correct ones
				// Update old
				for (i = 0; i < allNodes.length; i++)
				{
					var existingNode:CanvasNode = allNodes.collection[i];
					if(!slice.nodes.contains(existingNode.Node)
						&& slice.nodes.getByClientId(existingNode.Node.clientId) == null)
					{
						existingNode.removeFromCanvas();
						allNodes.remove(existingNode);
						i--;
					}
					else
					{
						existingNode.editable = editable;
						if(slice.nodes.contains(existingNode.Node))
							existingNode.Node = existingNode.Node;
						else
							existingNode.Node = slice.nodes.getNodeByClientId(existingNode.Node.clientId);
					}
				}
				// Add new nodes
				for each(var node:VirtualNode in slice.nodes.collection)
				{
					if(!allNodes.VirtualNodes.contains(node))
						addVirtualNodeToCanvas(node);
				}
				
				// Remove links no longer existing
				// Update existing links
				for(i = 0; i < allLinks.length; i++)
				{
					var existingLink:CanvasLink = allLinks.collection[i];
					var newlyCreatedLink:VirtualLink = slice.links.getLinkByClientId(existingLink.link.clientId);
					if(!slice.links.contains(existingLink.link) && newlyCreatedLink == null)
					{
						existingLink.removeFromCanvas();
						allLinks.remove(existingLink);
						i--;
					}
					else
					{
						if(slice.links.contains(existingLink.link))
						{
							if(positionOnly)
								existingLink.drawEstablished();
							else
								existingLink.establishFromExisting(existingLink.link);
						}
						else
							existingLink.establishFromExisting(newlyCreatedLink);
						existingLink.setEditable(editable);
					}
				}
				// Add new links
				for each(var link:VirtualLink in slice.links.collection)
				{
					if(!allLinks.VirtualLinks.contains(link))
						addVirtualLinkToCanvas(link);
				}
				
				if(slice.useInputRspecInfo.version > slice.nodes.Managers.MaximumRspecVersion)
					slice.useInputRspecInfo.version = slice.nodes.Managers.MaximumRspecVersion;

				switch(slice.AllocationState)
				{
					case Sliver.ALLOCATION_PROVISIONED:
						switch(slice.OperationalState)
						{
							case Sliver.OPERATIONAL_READY:
								styleName = "validCanvasStyle";
								break;
							case Sliver.OPERATIONAL_FAILED:
								styleName = "failedCanvasStyle";
								break;
							case Sliver.MIXED_CHANGING:
							case Sliver.OPERATIONAL_STOPPING:
							case Sliver.OPERATIONAL_READY_BUSY:
							case Sliver.OPERATIONAL_CONFIGURING:
								styleName = "inprogressCanvasStyle";
								break;
							case Sliver.MIXED_FINISHED:
							case Sliver.OPERATIONAL_NOTREADY:
								styleName = "pausedCanvasStyle";
								break;
							default:
								styleName = "normalCanvasStyle";
						}
						break;
					case Sliver.ALLOCATION_ALLOCATED:
					case Sliver.ALLOCATION_UPDATING:
					case Sliver.MIXED_ALLOCATED:
					case Sliver.MIXED_PROVISIONED:
					case Sliver.MIXED_UNALLOCATED:
					case Sliver.MIXED_UPDATING:
						styleName = "inprogressCanvasStyle";
						break;
					default:
						styleName = "normalCanvasStyle";
				}
			}
			
			public function updateInterface():void
			{
				updateCanvas();
			}
			
			public function clearStatus():void
			{
				styleName = "normalCanvasStyle";
				
				for each(var sn:CanvasNode in allNodes.collection)
					sn.clearStatus();
					
				for each(var cl:CanvasLink in allLinks.collection)
					cl.clearStatus();
			}
			
			/**
			 * Removes a virtual node
			 */
			public function removeVirtualNode(node:VirtualNode):void
			{
				for each(var sn:CanvasNode in this.allNodes.collection)
				{
					if(sn.Node == node)
					{
						removeCanvasNode(sn);
						return;
					}
				}
			}
			
			/**
			 * Removes a slice node
			 */
			public function removeCanvasNode(sliceNode:CanvasNode):void
			{
				slice.pushState();
				slice.history.stateName = "Removed " + sliceNode.Node.clientId;
				
				sliceNode.Node.removeFromSlice();
				updateCanvas();
				
				// Make sure everyone knows the slice history changed
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					slice
				);
			}
			
			public function removeVirtualLink(link:VirtualLink):void
			{
				for each(var cl:CanvasLink in this.allLinks.collection)
				{
					if(cl.link == link)
					{
						removeCanvasLink(cl);
						return;
					}
				}
			}
			
			public function removeCanvasLink(link:CanvasLink):void
			{
				slice.pushState();
				slice.history.stateName = "Removed " + link.link.clientId;
				
				link.link.removeFromSlice();
				updateCanvas();
				
				// Make sure everyone knows the slice history changed
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					slice
				);
			}
			
			/**
			 * Adds a virtual link to the canvas.  Nodes from the link should already be added prior to this call.  This is the method to create links from outside SliceCanvas.
			 */
			public function addVirtualLinkToCanvas(virtualLink:VirtualLink):CanvasLink
			{
				if(allLinks.VirtualLinks.contains(virtualLink))
					return allLinks.getForVirtualLink(virtualLink);
				
				var cl:CanvasLink = new CanvasLink(this);
				cl.setEditable(editable);
				addElement(cl);
				allLinks.add(cl);
				cl.establishFromExisting(virtualLink);
				validateNow();
				return cl;
			}
			
			/**
			 * Creates a slice node for the virtual node and adds it to the canvas and resources.
			 * 
			 * If outside of SliceCanvas, use addVirtualNode.
			 */
			public function addVirtualNodeToCanvas(virtualNode:VirtualNode):CanvasNode
			{
				if(allNodes.VirtualNodes.contains(virtualNode))
					return allNodes.getForVirtualNode(virtualNode);
				
				var sn:CanvasNode = new CanvasNode();
				sn.canvas = this;
				sn.editable = editable;
				allNodes.add(sn);
				addElement(sn);
				sn.Node = virtualNode;
				validateNow();
				return sn;
			}
			
			/**
			 * Does absolutely everything to add a physical node.  This is what should be called outside of SliceCanvas.
			 */
			public function addPhysicalNode(pn:PhysicalNode, x:int = -1, y:int = -1):void
			{
				if(!editable)
				{
					Alert.show("Canvas is not editable currently, most likely due to operations being performed");
					return;
				}
				
				if(!pn.available)
					return;
				if(pn.subNodeOf != null && !pn.subNodeOf.available)
					return;
				var existingNodes:VirtualNodeCollection = allNodes.VirtualNodes.getBoundTo(pn);
				if(existingNodes.length > 0)
				{
					if(!existingNodes.collection[0].Vm)
						return;
				}
				
				// make sure this can happen...
				var newVirtualNode:VirtualNode = new VirtualNode(slice,pn.manager as GeniManager);
				newVirtualNode.Physical = pn;
				newVirtualNode.flackInfo.unbound = false;
				newVirtualNode.flackInfo.x = x;
				newVirtualNode.flackInfo.y = y;
				
				slice.pushState();
				
				slice.history.stateName = "Added " + newVirtualNode.clientId;
				slice.nodes.add(newVirtualNode);
				
				if(pn.subNodeOf != null)
				{
					var newSuperVirtualNode:VirtualNode = new VirtualNode(slice,pn.subNodeOf.manager as GeniManager);
					newSuperVirtualNode.Physical = pn.subNodeOf;
					newSuperVirtualNode.flackInfo.unbound = false;
					newSuperVirtualNode.flackInfo.x = x == -1 ? -1 : x-40;
					newSuperVirtualNode.flackInfo.y = y == -1 ? -1 : y-40;
					newVirtualNode.superNode = newSuperVirtualNode;
					if(newSuperVirtualNode.subNodes == null)
						newSuperVirtualNode.subNodes = new VirtualNodeCollection();
					newSuperVirtualNode.subNodes.add(newVirtualNode);
					slice.nodes.add(newSuperVirtualNode);
				}
				
				updateCanvas();
				
				// Make sure everyone knows the slice history changed
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					slice
				);
			}
			
			public function addCloneOf(virtualComponent:VirtualComponent):void
			{
				if(virtualComponent is VirtualNode)
					addBasicCloneOfVirtualNode(virtualComponent as VirtualNode);
				else if(virtualComponent is VirtualLink)
					addBasicCloneOfVirtualLink(virtualComponent as VirtualLink);
			}
			
			/**
			 * Adds a basic unbound node like the one given
			 * 
			 * If outside of SliceCanvas, use addVirtualNode.
			 */
			public function addBasicCloneOfVirtualNode(virtualNode:VirtualNode, x:int = -1, y:int = -1):void
			{
				if(!editable)
				{
					Alert.show("Canvas is not editable currently, most likely due to operations being performed");
					return;
				}
				
				var nvn:VirtualNode = virtualNode.UnboundCloneFor(slice);
				nvn.flackInfo.x = x;
				nvn.flackInfo.y = y;
				
				// Make sure it's alright
				if(nvn.sliverType.sliverTypeSpecific != null
					&& !nvn.sliverType.sliverTypeSpecific.canAdd(nvn))
				{
					Alert.show("You cannot add a " + nvn.sliverType.name + " because of constraints on the sliver type", "Cannot add");
					return;
				}
				
				slice.pushState();
				
				slice.history.stateName = "Added " + nvn.clientId;
				slice.nodes.add(nvn);
				var sliver:AggregateSliver = slice.aggregateSlivers.getOrCreateByManager(nvn.manager, slice);
				//sliver.UnsubmittedChanges = true;
				
				addVirtualNodeToCanvas(nvn);
				
				// Make sure everyone knows the slice history changed
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					slice
				);
			}
			
			public function addBasicCloneOfVirtualLink(virtualLink:VirtualLink, x:int = -1, y:int = -1):void
			{
				if(!editable)
				{
					Alert.show("Canvas is not editable currently, most likely due to operations being performed");
					return;
				}
				
				var nvn:VirtualLink = virtualLink.UnboundCloneFor(slice);
				nvn.flackInfo.x = x;
				nvn.flackInfo.y = y;
				
				slice.pushState();
				
				slice.history.stateName = "Added " + nvn.clientId;
				slice.links.add(nvn);
				
				addVirtualLinkToCanvas(nvn);
				
				// Make sure everyone knows the slice history changed
				SharedMain.sharedDispatcher.dispatchChanged(
					FlackEvent.CHANGED_SLICE,
					slice
				);
			}
			
			// Selected
			private var selectedItem:DisplayObject;
			public function set SelectedItem(value:*):void
			{
				if(selectedItem != null)
				{
					if(selectedItem is CanvasNode)
						(selectedItem as CanvasNode).setFilters([]);
					else
						(selectedItem as CanvasLink).setFilters([]);
				}
				selectedItem = value;
				if(value != null)
				{
					if(selectedItem is CanvasNode)
						(selectedItem as CanvasNode).setFilters([new DropShadowFilter()]);
					else
						(selectedItem as CanvasLink).setFilters([new DropShadowFilter()]);
				}
			}
			public function get SelectedItem():*
			{
				return selectedItem;
			}
			public function set SelectedNode(n:VirtualNode):void
			{
				for each(var canvasNode:CanvasNode in this.allNodes.collection)
				{
					if(canvasNode.Node == n)
					{
						SelectedItem = canvasNode;
						return;
					}
				}
			}
			public function get SelectedNode():VirtualNode
			{
				if(selectedItem != null && selectedItem is CanvasNode)
				{
					return (selectedItem as CanvasNode).Node;
				}
				else
					return null;
			}
			
			// Mouse stuff
			public var draggingItem:*;
			public var previewLink:UIComponent;
			public var startItem:* = null;
			public var endItem:* = null;
			public var drawingLink:Boolean = false;
			public var hoveredOverItem:* = null;
			
			protected function bordercontainer1_mouseMoveHandler(event:MouseEvent):void
			{
				if(draggingItem != null && event.buttonDown)
				{
					draggingItem.setLocation(mouseX - draggingItem.ContainerWidth/2, mouseY - draggingItem.ContainerHeight/2);
					updateCanvas(true);
					return;
				}
				
				// Ignore while user is dragging from the outside
				if(DragManager.isDragging)
					return;
				
				var rawSprite:Sprite;
				if(drawingLink)
				{
					endItem = null;
					previewLink.filters = [];
					rawSprite = previewLink.getChildAt(0) as Sprite;
					rawSprite.graphics.clear();
					var color:uint = ColorUtil.unknownDark;
					// See if over something
					if(hoveredOverItem != null)
					{
						if(hoveredOverItem is CanvasNode)
						{
							var hoveredOverNode:CanvasNode = hoveredOverItem as CanvasNode;
							if(hoveredOverNode != startItem)
							{
								if(startItem is CanvasNode)
								{
									var connectingNodes:VirtualNodeCollection = new VirtualNodeCollection();
									connectingNodes.add(startItem.Node);
									connectingNodes.add(hoveredOverItem.Node);
									if(VirtualLink.canEstablish(connectingNodes)
										&& startItem.Node.interfaces.Links.getConnectedToNode(hoveredOverNode.Node).length == 0)
									{
										color = ColorUtil.validDark;
										previewLink.filters = [new GlowFilter(ColorUtil.validDark)];
										endItem = hoveredOverNode;
									}
									else
									{
										color = ColorUtil.invalidDark;
										previewLink.filters = [new GlowFilter(ColorUtil.invalidDark)];
									}
								}
								else
								{
									if((startItem as CanvasLink).link.canAddNode(hoveredOverNode.Node))
									{
										color = ColorUtil.validDark;
										previewLink.filters = [new GlowFilter(ColorUtil.validDark)];
										endItem = hoveredOverNode;
									}
									else
									{
										color = ColorUtil.invalidDark;
										previewLink.filters = [new GlowFilter(ColorUtil.invalidDark)];
									}
								}
							}
						}
						else if(hoveredOverItem is CanvasLink)
						{
							var hoveredOverLink:CanvasLink = hoveredOverItem as CanvasLink;
							if(startItem is CanvasNode
								&& (hoveredOverItem as CanvasLink).link.canAddNode(startItem.Node))
							{
								color = ColorUtil.validDark;
								previewLink.filters = [new GlowFilter(ColorUtil.validDark)]
								endItem = hoveredOverLink;
							}
							else
							{
								color = ColorUtil.invalidDark;
								previewLink.filters = [new GlowFilter(ColorUtil.invalidDark)];
							}
						}
					}
					rawSprite.graphics.lineStyle(3, color, 1.0, true,
						LineScaleMode.NORMAL, CapsStyle.ROUND);
					rawSprite.graphics.moveTo(startItem.MiddleX, startItem.MiddleY);
					rawSprite.graphics.lineTo(mouseX, mouseY);
				}
				else
				{
					if(hoveredOverItem != null)
					{
						startItem = null;
						if(previewLink != null)
						{
							removeElement(previewLink);
							previewLink = null;
						}
					}
					// see if near node and editable, preview link
					else if(editable)
					{
						for(var i:Number = 2; i < 60; i+=2)
						{
							var hitRect:Rectangle = new Rectangle(mouseX-i, mouseY-i, i*2, i*2);
							for each(var cn:CanvasNode in allNodes.collection)
							{
								if(cn.getRect(this).intersects(hitRect))
								{
									startItem = cn;
									
									if(previewLink == null)
									{
										previewLink = new UIComponent();
										rawSprite = new Sprite();
										previewLink.addChild(rawSprite);
										addElementAt(previewLink, 0);
									}
									else
										rawSprite = previewLink.getChildAt(0) as Sprite;
									
									rawSprite.graphics.clear();
									rawSprite.graphics.lineStyle(4, ColorUtil.unknownDark, 1.0, true,
										LineScaleMode.NORMAL, CapsStyle.ROUND);
									rawSprite.graphics.moveTo(cn.MiddleX, cn.MiddleY);
									rawSprite.graphics.lineTo(mouseX, mouseY);
									return;
								}
							}
							for each(var cl:CanvasLink in allLinks.collection)
							{
								if(cl.button.getRect(this).intersects(hitRect))
								{
									startItem = cl;
									
									if(previewLink == null)
									{
										previewLink = new UIComponent();
										rawSprite = new Sprite();
										previewLink.addChild(rawSprite);
										addElementAt(previewLink, 0);
									}
									else
										rawSprite = previewLink.getChildAt(0) as Sprite;
									
									rawSprite.graphics.clear();
									rawSprite.graphics.lineStyle(
										4,
										ColorUtil.unknownDark,
										1.0,
										true,
										LineScaleMode.NORMAL,
										CapsStyle.ROUND
									);
									rawSprite.graphics.moveTo(cl.button.MiddleX, cl.button.MiddleY);
									rawSprite.graphics.lineTo(mouseX, mouseY);
									return;
								}
							}
						}
					}
					if(previewLink != null)
					{
						startItem = null;
						removeElement(previewLink);
						previewLink = null;
					}
				}
			}
			
			protected function bordercontainer1_mouseDownHandler(event:MouseEvent):void
			{
				if(startItem != null)
				{
					drawingLink = true;
				}
			}
			
			protected function bordercontainer1_mouseUpHandler(event:MouseEvent):void
			{
				draggingItem = null;
				var connectNodes:VirtualNodeCollection = new VirtualNodeCollection();
				
				if(drawingLink && endItem != null)
				{
					if(startItem is CanvasLink)
					{
						if(endItem is CanvasNode)
						{
							var startLink:CanvasLink = startItem as CanvasLink;
							connectNodes.add(endItem.Node);
							if(VirtualLink.canEstablish(connectNodes))
							{
								slice.pushState();
								if(!startLink.link.addNode(endItem.Node))
								{
									slice.history.stateName = "Added " + endItem.Node.clientId + " to " + startLink.link.clientId;
									startLink.establishFromExisting(startLink.link);
								}
							}
						}
					}
					else if(startItem is CanvasNode)
					{
						if(endItem is CanvasNode)
						{
							connectNodes.add(startItem.Node);
							connectNodes.add(endItem.Node);
							if(VirtualLink.canEstablish(connectNodes))
							{
								slice.pushState();
								var newLink:VirtualLink = new VirtualLink(slice);
								if(!newLink.establish(connectNodes))
								{
									slice.history.stateName = "Established " + newLink.clientId;
									slice.links.add(newLink);
									addVirtualLinkToCanvas(newLink);
								}
							}
						}
						else if(endItem is CanvasLink)
						{
							var endLink:CanvasLink = endItem as CanvasLink;
							if(endLink.link.canAddNode(startItem.Node))
							{
								slice.pushState();
								if(!endLink.link.addNode(startItem.Node))
								{
									slice.history.stateName = "Added " + startItem.Node.clientId + " to " + endLink.link.clientId;
									endLink.establishFromExisting(endLink.link);
								}
							}
						}
					}
				}
				
				drawingLink = false;
				startItem = null;
				endItem = null;
				if(previewLink != null)
					removeElement(previewLink);
				previewLink = null;
			}
			
			// Drag and drop
			
			private var dropItem:* = null;
			protected function bordercontainer1_dragEnterHandler(event:DragEvent):void
			{
				if(!editable)
					return;
				dropItem = null;
				// Dragging a physical node?
				if(event.dragInitiator is List && (event.dragInitiator as List).selectedItem is PhysicalNode)
					dropItem = (event.dragInitiator as List).selectedItem;
				else if(event.dragSource.hasFormat("physicalNode"))
					dropItem = event.dragSource.dataForFormat("physicalNode");
				else if(event.dragSource.hasFormat("virtualNode"))
					dropItem = event.dragSource.dataForFormat("virtualNode");
				else if(event.dragSource.hasFormat("virtualLan"))
					dropItem = event.dragSource.dataForFormat("virtualLan");
				if(dropItem != null)
				{
					if(dropItem is PhysicalNode)
					{
						var dropNode:PhysicalNode = dropItem as PhysicalNode;
						if(!dropNode.available)
							return;
						if(dropNode.subNodeOf != null && !dropNode.subNodeOf.available)
							return;
						var existingNodes:VirtualNodeCollection = allNodes.VirtualNodes.getBoundTo(dropNode);
						if(existingNodes.length > 0)
						{
							if(!existingNodes.collection[0].Vm)
								return;
						}
					}
					else if(dropItem is VirtualNode)
					{
						var dropVirtNode:VirtualNode = dropItem as VirtualNode;
						dropVirtNode.slice = slice;
						
						// Make sure it's alright
						if(dropVirtNode.sliverType.sliverTypeSpecific != null
							&& !dropVirtNode.sliverType.sliverTypeSpecific.canAdd(dropVirtNode))
						{
							return;
						}
					}
					else if(dropItem is VirtualLink)
					{
						var dropVirtLan:VirtualLink = dropItem as VirtualLink;
						dropVirtLan.slice = slice;
					}
					DragManager.acceptDragDrop(SliceCanvas(event.currentTarget));
				}
			}
			
			protected function bordercontainer1_dragDropHandler(event:DragEvent):void
			{
				if(dropItem != null)
				{
					if(dropItem is PhysicalNode)
						addPhysicalNode(dropItem, mouseX-48, mouseY-12);
					else if(dropItem is VirtualNode)
						addBasicCloneOfVirtualNode(dropItem, mouseX-48, mouseY-12);
					else if(dropItem is VirtualLink)
						addBasicCloneOfVirtualLink(dropItem, mouseX-48, mouseY-12);
				}
			}
			
			protected function bordercontainer1_resizeHandler(event:ResizeEvent):void
			{
				for each(var node:CanvasNode in allNodes.collection)
				{
					node.ensureInside();
				}
			}
			
			public function toggleEditable(isEditable:Boolean):void
			{
				editable = isEditable;
				for each(var sn:CanvasNode in allNodes.collection)
					sn.editable = isEditable;
				
				for each(var cl:CanvasLink in allLinks.collection)
					cl.setEditable(editable);
			}
			
		]]>
	</fx:Script>
	
</s:BorderContainer>
