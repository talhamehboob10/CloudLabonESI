<?xml version="1.0" encoding="utf-8"?>

<!--

Copyright (c) 2008-2013 University of Utah and the Flux Group.

{{{GENIPUBLIC-LICENSE

GENI Public License

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and/or hardware specification (the "Work") to
deal in the Work without restriction, including without limitation the
rights to use, copy, modify, merge, publish, distribute, sublicense,
and/or sell copies of the Work, and to permit persons to whom the Work
is furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Work.

THE WORK IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE WORK OR THE USE OR OTHER DEALINGS
IN THE WORK.

}}}

-->

<!--

VirtualNode for use on the slice canvas

-->

<s:HGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:components="com.flack.shared.display.components.*"
		  initialize="ensureInside();"
		  gap="4"
		  paddingLeft="0"
		  paddingTop="0"
		  paddingRight="0"
		  paddingBottom="0">
	<fx:Style>
		@namespace s "library://ns.adobe.com/flex/spark";
		@namespace mx "library://ns.adobe.com/flex/mx";
		
		.normalNodeStyle {
			borderColor: #2F4F4F;
			backgroundColor: #EAEAEA;
		}
		
		.inprogressNodeStyle {
			borderColor: #B2B300;
			backgroundColor: #E6E600;
		}
		
		.failedNodeStyle {
			borderColor: #990000;
			backgroundColor: #F08080;
		}
		
		.pausedNodeStyle {
			borderColor: FF6600;
			backgroundColor: #FF9900;
		}
		
		.validNodeStyle {
			borderColor: #006600;
			backgroundColor: #27C427;
		}
		
	</fx:Style>
	<fx:Script>
		<![CDATA[
			import com.flack.geni.resources.virt.AggregateSliver;
			import com.flack.geni.resources.virt.Sliver;
			import com.flack.geni.resources.virt.VirtualComponent;
			import com.flack.geni.resources.virt.VirtualNode;
			import com.flack.shared.utils.ColorUtil;
			import com.flack.shared.utils.ImageUtil;
			import com.flack.shared.utils.StringUtil;
			
			import flash.filters.GlowFilter;
			
			public function setFilters(newFilters:Array):void
			{
				nodeContainer.filters = newFilters;
			}
			
			[Bindable]
			public var editable:Boolean = true;
			
			[Bindable]
			private var node:VirtualNode;
			public function set Node(newNode:VirtualNode):void
			{
				node = newNode;
				labelBackgroundColor = ColorUtil.colorsDark[node.manager.colorIdx];
				labelColor = ColorUtil.colorsLight[node.manager.colorIdx];
				setLocation(node.flackInfo.x,node.flackInfo.y);
				sliverTypeLabel.text = (node.exclusive ? "excl. " : "sh. ") + node.sliverType.name;
				if(node.services.loginServices != null && node.services.loginServices.length > 0)
				{
					hostnameLabel.text = node.services.loginServices[0].FullHostname;
					hostnameLabel.visible = hostnameLabel.includeInLayout = true;
				}
				else
					hostnameLabel.visible = hostnameLabel.includeInLayout = false;
				if(node.sliverType.selectedImage != null)
				{
					if(node.sliverType.selectedImage.id.full.length > 0)
						osidLabel.text = node.sliverType.selectedImage.Osid;
					else if(node.sliverType.selectedImage.url.length > 0)
						osidLabel.text = StringUtil.shortenString(node.sliverType.selectedImage.url, 20);
					else
						osidLabel.text = "";
					osidLabel.visible = osidLabel.includeInLayout = osidLabel.text.length > 0;
				}
				else
					osidLabel.visible = osidLabel.includeInLayout = false;
				if((!node.Bound && node.hardwareType.name.length > 0) || (node.Bound && node.Physical != null))
				{
					if(node.Bound)
						bindingLabel.text = "=> " + StringUtil.shortenString(node.Physical.name, 30);
					else
						bindingLabel.text = node.hardwareType.name;
					bindingLabel.visible = bindingLabel.includeInLayout = true;
				}
				else
					bindingLabel.visible = bindingLabel.includeInLayout = false;
				setStatus();
			}
			public function get Node():VirtualNode
			{
				return node;
			}
			
			public var canvas:SliceCanvas;
			
			[Bindable]
			private var labelColor:uint;
			[Bindable]
			private var labelBackgroundColor:uint;
			
			public function get AllowLink():Boolean
			{
				return node.manager.SupportsLinks && node.HasUsableExperimentalInterface;
			}
			
			public function setStatus():void
			{
				if(node != null)
				{
					if(node.unsubmittedChanges)
					{
						nodeContainer.styleName = "normalNodeStyle";
						toolTip = "Unsubmitted changes (Allocation: " +
							Sliver.readableAllocationState(node.allocationState) +
							" Operational: " + Sliver.readableOperationalState(node.operationalState) +")";
					}
					else
					{
						switch(node.allocationState)
						{
							case Sliver.ALLOCATION_PROVISIONED:
								switch(node.operationalState)
								{
									case Sliver.OPERATIONAL_READY:
										nodeContainer.styleName = "validNodeStyle";
										break;
									case Sliver.OPERATIONAL_FAILED:
										nodeContainer.styleName = "failedNodeStyle";
										toolTip = "Error: " + node.error;
										break;
									case Sliver.OPERATIONAL_STOPPING:
									case Sliver.OPERATIONAL_READY_BUSY:
									case Sliver.OPERATIONAL_CONFIGURING:
										nodeContainer.styleName = "inprogressNodeStyle";
										toolTip = "Status is changing...";
										break;
									case Sliver.OPERATIONAL_NOTREADY:
										nodeContainer.styleName = "pausedNodeStyle";
										toolTip = "Stopped";
										break;
									default:
										nodeContainer.styleName = "normalNodeStyle";
										toolTip = "Allocation: " + Sliver.readableAllocationState(node.allocationState) +
										          " Operational: " + Sliver.readableOperationalState(node.operationalState);
								}
								break;
							case Sliver.ALLOCATION_ALLOCATED:
							case Sliver.ALLOCATION_UPDATING:
								nodeContainer.styleName = "inprogressNodeStyle";
								toolTip = "Status is changing...";
								break;
							default:
								nodeContainer.styleName = "normalNodeStyle";
								toolTip = "Allocation: " + Sliver.readableAllocationState(node.allocationState) +
								" Operational: " + Sliver.readableOperationalState(node.operationalState);
						}
					}
				}
				else
				{
					nodeContainer.styleName = "normalNodeStyle";
					toolTip = "";
				}

				//adjustWidth();
			}
			
			public function clearStatus():void
			{
				toolTip = "";
				nodeContainer.styleName = "normalNodeStyle";
				
				//adjustWidth();
			}
			
			/*public function adjustWidth():void
			{
				validateNow();
				if(nameLabel != null)
				{
					//width = width + (nameLabel.measuredWidth - nameLabel.width);// + 5;
					validateNow();
					if(node != null)
						ensureInside();
				}
			}*/
			
			public function ensureInside(point:Point = null):void
			{
				var newX:Number = point == null ? x : point.x;
				var newY:Number = point == null ? y : point.y;
				// right edge
				if(parentDocument.width > 0 && (x + width) > parentDocument.width)
					newX = parentDocument.width - width;
				// bottom edge
				if(parentDocument.height > 0 && (y + height) > parentDocument.height)
					newY = parentDocument.height - height;
				// left edge
				if(x < 0)
					newX = 0;
				// top edge
				if(y < 0)
					newY = 0;
				move(newX, newY);
			}
			
			override public function move(x:Number, y:Number):void
			{
				if(node != null)
				{
					// Don't consider moving a change
					//if(x != node.flackInfo.x || y != node.flackInfo.y)
					//	node.unsubmittedChanges = true;
					node.flackInfo.x = x;
					node.flackInfo.y = y;
				}
				super.move(x, y);
			}
			
			public function showInfo(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				//DisplayUtil.viewVirtualNode(node);
				canvas.host.viewNode(node);
			}
			
			public function remove(event:MouseEvent = null):void
			{
				if(event != null)
					event.stopImmediatePropagation();
				canvas.removeCanvasNode(this);
			}
			
			public function removeFromCanvas():void
			{
				canvas.removeElement(this);
			}
			
			public function setLocation(newX:Number = -1, newY:Number = -1):void
			{
				var finalPoint:Point = new Point(newX, newY);
				if(newX == -1)
					finalPoint.x = Math.round(Math.random() * (parentDocument.width-60));
				if(newY == -1)
					finalPoint.y = Math.round(Math.random() * (parentDocument.height-28));
				
				// Make sure this is within bounds
				ensureInside(finalPoint);
			}
			
			public function get MiddlePoint():Point
			{
				return new Point(MiddleX, MiddleY);
			}
			
			public function get MiddleX():Number
			{
				return x + nodeContainer.width/2;
			}
			
			public function get MiddleY():Number
			{
				return y + height/2;
			}
			
			public function get ContainerWidth():Number
			{
				return nodeContainer.width;
			}
			
			public function get ContainerHeight():Number
			{
				return nodeContainer.height;
			}
			
			protected function bordercontainer1_mouseDownHandler(event:MouseEvent):void
			{
				canvas.draggingItem = this;
				canvas.setElementIndex(this, canvas.numElements-1);
			}
			
			protected function bordercontainer1_rollOverHandler(event:MouseEvent):void
			{
				if(canvas.SelectedNode == null || canvas.SelectedNode != node)
					nodeContainer.filters = [new GlowFilter(0x000000)];
				canvas.hoveredOverItem = this;
			}
			
			protected function bordercontainer1_rollOutHandler(event:MouseEvent):void
			{
				if(canvas.SelectedNode == null || canvas.SelectedNode != node)
					nodeContainer.filters = [];
				canvas.hoveredOverItem = null;
			}
			
			protected function bordercontainer1_clickHandler(event:MouseEvent):void
			{
				canvas.SelectedNode = node;
			}
			
		]]>
	</fx:Script>
	
	<s:BorderContainer id="nodeContainer"
					   buttonMode="true"
					   useHandCursor="true"
					   height="29"
					   borderStyle="solid"
					   styleName="normalNodeStyle"
					   mouseDown="bordercontainer1_mouseDownHandler(event)"
					   rollOver="bordercontainer1_rollOverHandler(event)"
					   rollOut="bordercontainer1_rollOutHandler(event)"
					   click="bordercontainer1_clickHandler(event)">
		<s:layout>
			<s:HorizontalLayout paddingBottom="2"
								paddingLeft="2"
								paddingRight="2"
								paddingTop="2"
								gap="2"
								verticalAlign="middle" />
		</s:layout>
		<components:ImageButton id="removeButton"
								icon="{ImageUtil.crossIcon}"
								enabled="{editable}"
								click="remove(event)"
								mouseDown="event.stopImmediatePropagation();" />
		<s:Label id="nameLabel"
				 text="{node.clientId}"
				 height="100%"
				 verticalAlign="middle"
				 textAlign="center"
				 color="{labelColor}"
				 backgroundColor="{labelBackgroundColor}"
				 maxDisplayedLines="1" />
		<components:ImageButton id="infoButton"
								icon="{ImageUtil.infoIcon}"
								click="showInfo(event)"
								mouseDown="event.stopImmediatePropagation();" />
	</s:BorderContainer>
	<s:VGroup id="extraGroup" gap="2">
		<s:Label id="hostnameLabel" fontSize="10" visible="false" includeInLayout="false" />
		<s:Label id="sliverTypeLabel" fontSize="10" />
		<s:Label id="osidLabel" fontSize="10" visible="false" includeInLayout="false" />
		<s:Label id="bindingLabel" fontSize="10" visible="false" includeInLayout="false" />
	</s:VGroup>
</s:HGroup>
